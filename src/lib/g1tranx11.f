      SUBROUTINE G1TRAN(INBUFR,ISTART,IEND)
C
C          ------------------------------------------------
C          ROUTINE NO. (1003)   VERSION (A8.1X11) 18:NOV:94
C          ------------------------------------------------
C
C          THIS CONVERTS PAIRS OF VALUES IN THE INPUT BUFFER
C          INTO CODES SUITABLE FOR DRIVING THE OUTPUT DEVICE.
C          (THIS VERSION IS TO INTERFACE TO X WINDOWS VERSION 11).
C
C
C          THE FUNCTIONS SUPPORTED HERE (GIVEN BY
C          THE X-VALUE OF ANY PAIR) ARE AS FOLLOWS:
C
C          =  -1, PLOT (AT THE CURRENT POSITION) A POINT,
C          =  -2, DRAW (AT THE CURRENT POS.) CHAR. <IYVAL>,
C              .
C          = -11, SET HARDWARE CHARACTER SIZE      <IYVAL>,
C          = -12, SET HARDWARE CHARACTER ANGLE     <IYVAL>,
C          = -13, SET HARDWARE CHARACTER ITALIC    <IYVAL>,
C          = -14, SET HARDWARE CHARACTER OBLATE    <IYVAL>,
C          = -15, SET HARDWARE LINE TYPE           <IYVAL>,
C              .
C          = -17, SET COLOUR NUMBER                <IYVAL>,
C          = -18, FILL POLYGON                     <IYVAL>,
C              .
C          = -20, BEGIN FILLED-AREA BOUNDARY       <IYVAL>,
C              .
C          = -41, SET A COLOUR TABLE ENTRY         <IDATA>,
C              .
C          = -43, SET BACKGROUND COLOUR            <IDATA>,
C          = -44, SET PARAMETERS FOR CROSS HATCHING
C              .
C          = -81, ERASE THE DEVICE WITH PROMPT/WAIT,
C          = -82, ERASE THE DEVICE WITHOUT PROMPT/WAIT,
C          = -83, DISPLAY THE GRAPHICS CURSOR,
C          = -84, CLEAR THE SYSTEM BUFFER,
C          = -85, END GRAPHICAL OUTPUT,
C
C
      INTEGER   INBUFR(IEND),IBLUIN(1),IGRNIN(1)
      LOGICAL   BOUND,UPDFRM
C
      COMMON /T1DRES/ DRESX,DRESY
      COMMON /T1FLBF/ IXSAVE(1000),IYSAVE(1000),ISAVSZ,INDSAV
      COMMON /T1FLSV/ BOUND
      COMMON /T1FUND/ NUMFUN,LENARG
      COMMON /T1HTCH/ IHTCHP(11,2)
      COMMON /T1JOBF/ IPOLYX(1000),IPOLYY(1000),ISIZEB,INPOLY
      COMMON /T1KDEF/ KOLLID,KOLBAD,MAXCLS
      COMMON /T1LOCD/ XLOCAD,YLOCAD
      COMMON /T1REPN/ NREPLC
      COMMON /T1X11B/ KOLOUR,KOLNUM,IREDIN(1),UPDFRM
C
      DATA IHTCOL /2/, IHTYPE /11/
C
C          EACH PAIR OF VALUES IS TAKEN IN TURN FROM THE
C          SUPPLIED BUFFER, THE REQUIRED ACTION IS DECODED.
C
C          IF <LENARG> IS NON-ZERO, A FUNCTION IS STILL ACTIVE.
C          THE BUFFER PAIR IS THEN PASSED ON DIRECTLY TO THE
C          RELEVANT FUNCTION. OTHERWISE, IF <IXVAL> < 0, A
C          NEW FUNCTION IS REQUIRED, OR IF <IXVAL> > 0, A
C          VECTOR (VISIBLE OR INVISIBLE) IS REQUIRED INSTEAD.
C
      DO 100 IPAIR= ISTART,IEND,2
        IXVAL= INBUFR(IPAIR)
        IYVAL= INBUFR(IPAIR+1)
        IF (UPDFRM) THEN
          CALL XCLRWN
          IF (MAXCLS.GT.0) CALL XSETFC(KOLOUR)

          UPDFRM= .FALSE.
        ENDIF
C
        IF (LENARG.GT.0) GO TO 29
        IF (IXVAL.LT.0)  GO TO 2
        IF (BOUND)       GO TO 24
C
C          THE FIRST PART PROVIDES VECTOR DRAWING:
C
C          IF <IYVAL> < 0, THE LINE IS NOT VISIBLE, AND
C          THE Y-VALUE HAS TO BE APPROPRIATELY READJUSTED.
C
        IF (IYVAL.GE.0) GO TO 1
C
        CALL G1VECT(IXVAL,-IYVAL-1,3)
        GO TO 100
C
    1   CALL G1VECT(IXVAL,IYVAL,1)
        GO TO 100
C
    2   IF (INPOLY.LE.1 .OR. BOUND) GO TO 3
C
        CALL XPOLYL(IPOLYX,IPOLYY,INPOLY)
        IPOLYX(1)= IPOLYX(INPOLY)
        IPOLYY(1)= IPOLYY(INPOLY)
        INPOLY= 1
C
C          THE REMAINDER OF THE ROUTINE IS FOR SPECIAL FUNCTIONS.
C          THE FUNCTION TYPE IS DETERMINED BY <IXVAL> AS FOLLOWS:
C
C          IF <IXVAL>= -1, A POINT IS PLOTTED.
C
    3   IF (IXVAL.NE.-1) GO TO 4
C
        CALL XPOLYM(IPOLYX,IPOLYY,1)
        GO TO 100
C
C          IF <IXVAL>= -2, A HARDWARE CHARACTER IS DRAWN
C          AFTER THE POSITION HAS BEEN SET PREVIOUSLY.
C
    4   IF (IXVAL.NE.-2) GO TO 5
C
        CALL XTEXT(IPOLYX(1),IPOLYY(1),IYVAL)
        GO TO 100
C
C          IF <IXVAL>= -11, HARDWARE CHARACTER SIZE IS SET.
C
    5   IF (IXVAL.NE.-11) GO TO 6
C
        CALL XCHARH(IYVAL)
        GO TO 100
C
C          IF <IXVAL>= -12, HARDWARE CHARACTER ROTATION IS SET.
C
    6   IF (IXVAL.NE.-12) GO TO 7
C
        CHUPX= -SIN(IYVAL*0.001)
        CHUPY= COS(IYVAL*0.001)
        CALL XCHARO(CHUPY,-CHUPX,CHUPX,CHUPY)
        GO TO 100
C
C          IF <IXVAL>= -13, HARDWARE CHAR. ITALIC IS SET.
C          (IT IS NOT IMPLEMENTED IN THIS VERSION)
C
    7   IF (IXVAL.NE.-13) GO TO 8
        GO TO 100
C
C          IF <IXVAL>= -14, HARDWARE CHAR. OBLATENESS IS SET.
C
    8   IF (IXVAL.NE.-14) GO TO 9
C
        CHEXPF= IYVAL*1000.0
        CALL XCHARB(CHEXPF)
        GO TO 100
C
C          IF <IXVAL>= -15, A HARDWARE BROKEN LINE IS SET.
C
    9   IF (IXVAL.NE.-15) GO TO 10
C
        CALL XLINET(IYVAL)
        GO TO 100
C
C          IF <IXVAL>= -16, HARDWARE INTENSITY IS SET.
C          (IT IS NOT IMPLEMENTED IN THIS VERSION).
C
   10   IF (IXVAL.NE.-16) GO TO 11
        GO TO 100
C
C          IF <IXVAL>= -17, THE COLOUR NUMBER IS SET.
C
   11   IF (IXVAL.NE.-17) GO TO 13
        IF (IYVAL.EQ.KOLOUR) GO TO 100
C
        KOLOUR= IYVAL
        IFROM= 1
   12   CALL XSETFC(KOLOUR)
        GO TO (100,17,19,21) , IFROM
C
C          IF <IXVAL>= -18, A POLYGON FILL IS DONE.
C
   13   IF (IXVAL.NE.-18) GO TO 22
        IF (INDSAV.GT.0) GO TO 14
        BOUND= .FALSE.
        GO TO 100
C
C          THIS PART CLOSES THE BOUNDARY IF IT IS OPEN.
C
   14   IF (IXSAVE(1).EQ.IXSAVE(INDSAV).AND.
     &      IYSAVE(1).EQ.IYSAVE(INDSAV)) GO TO 15
C
        CALL G1DISC(IXSAVE(1),IYSAVE(1))
   15   IF (IHTCHP(IHTYPE,2).NE.0) GO TO 16
C
C          THIS SECTION FOR SOLID AREA FILL.
C
        IF (MAXCLS.GT.0) CALL XFILLP(IXSAVE,IYSAVE,INDSAV)
C
        IPOLYX(1)= IXSAVE(INDSAV)
        IPOLYY(1)= IYSAVE(INDSAV)
        INPOLY= 1
        GO TO 21
C
   16   IF (IHTCHP(IHTYPE,1).LE.0) GO TO 21
C
C          THIS SECTION FOR CROSS-HATCHING.
C
        KOLSAV= KOLOUR
        IF (MOD(IHTCHP(IHTYPE,1),2).NE.1) GO TO 18
        IF (KOLOUR.EQ.IHTCHP(IHTCOL,1)) GO TO 17
C
        IFROM= 2
        KOLOUR= IHTCHP(IHTCOL,1)
        GO TO 12
C
   17   CALL G1SHAD(1)
   18   IF (IHTCHP(IHTYPE,1)/2.NE.1) GO TO 20
        IF (KOLOUR.EQ.IHTCHP(IHTCOL,2)) GO TO 19
C
        IFROM= 3
        KOLOUR= IHTCHP(IHTCOL,2)
        GO TO 12
C
   19   CALL G1SHAD(2)
   20   IF (KOLOUR.EQ.KOLSAV) GO TO 21
C
        IFROM= 4
        KOLOUR= KOLSAV
        GO TO 12
C
   21   BOUND= .FALSE.
        GO TO 100
C
C          IF <IXVAL>= -19, DEVICE RESOLUTION IS SET.
C          (IT IS NOT IMPLEMENTED IN THIS VERSION).
C
   22   IF (IXVAL.NE.-19) GO TO 23
        GO TO 100
C
C          IF <IXVAL>= -20, A FILLED AREA BOUNDARY IS BEGUN.
C
   23   IF (IXVAL.NE.-20) GO TO 27
        IF (IYVAL.GT.0) IHTCHP(IHTYPE,2)= 0
C
        BOUND= .TRUE.
        INDSAV= 0
        GO TO 100
C
   24   IF (IYVAL.GE.0) GO TO 25
C
        IYVAL= -IYVAL-1
        IF (INDSAV.EQ.0) GO TO 26
C
        CALL G1DISC(IXVAL,IYVAL)
        GO TO 100
C
C          IF THE BOUNDARY STARTS WITH A JOIN,
C          THE CURRENT POSITION IS SAVED FIRST.
C
   25   IF (INDSAV.GT.0) GO TO 26
C
        INDSAV= 1
        IXSAVE(1)= IPOLYX(1)
        IYSAVE(1)= IPOLYY(1)
   26   IF (INDSAV+3.GE.ISAVSZ) GO TO 100
C
        INDSAV= INDSAV+1
        IXSAVE(INDSAV)= IXVAL
        IYSAVE(INDSAV)= IYVAL
        GO TO 100
C
C          FUNCTION CODES -21 TO -39 ARE NOT YET DEFINED,
C          AND ARE RESERVED FOR FUTURE NON-ARGUMENT FUNCTIONS.
C
   27   IF (IXVAL.LE.-40) GO TO 28
        GO TO 100
C
C          FUNCTION CODES -40 TO -80 ARE ARGUMENT FUNCTIONS.
C          -<IYVAL> GIVES THE LENGTH OF THE ARGUMENT STRING.
C          <NUMFUN> STORES THE FUNCTION CODE CURRENTLY ACTIVE.
C
   28   IF (IXVAL.LT.-80) GO TO 35
C
        NUMFUN= IXVAL
        LENARG= -IYVAL
        GO TO 100
C
C          THIS SECTION DEALS WITH ARGUMENT-FUNCTIONS.
C          NOTE THAT <NUMFUN> IS USED INSTEAD OF <IXVAL>.
C
   29   LENARG= LENARG-1
C
C          IF <NUMFUN>= -40, DATA IS PASSED ON TRANSPARENTLY.
C          (IT IS NOT IMPLEMENTED IN THIS VERSION).
C
        IF (NUMFUN.NE.-40) GO TO 30
        GO TO 100
C
C          IF <NUMFUN>= -41, A COLOUR TABLE ENTRY IS SET.
C
   30   IF (NUMFUN.NE.-41) GO TO 32
        IF (LENARG.EQ.0) GO TO 31
C
        KOLNUM= IXVAL
        IREDIN(1)= IYVAL
        GO TO 100
C
   31   IGRNIN(1)= IXVAL
        IBLUIN(1)= IYVAL
        CALL XCOLTB(KOLNUM,IREDIN,IGRNIN,IBLUIN)
        GO TO 100
C
C          IF <NUMFUN>= -42, HARDWARE LINE TYPE IS SET.
C          (IT IS NOT IMPLEMENTED IN THIS VERSION).
C
   32   IF (NUMFUN.NE.-42) GO TO 33
        GO TO 100
C
C          IF <NUMFUN>= -43, BACKGROUND COLOUR IS SET.
C
   33   IF (NUMFUN.NE.-43) GO TO 34
        IF (LENARG.EQ.0) GO TO 31
C
        KOLNUM= IXVAL
        IREDIN(1)= IYVAL
C
        GO TO 100
C
C          IF <NUMFUN>= -44, SET HATCHING PARAMS.
C
   34   IF (NUMFUN.NE.-44) GO TO 100
C
        IHATPT= 11-LENARG
        IHTCHP(IHATPT,1)= IXVAL
        IHTCHP(IHATPT,2)= IYVAL
        GO TO 100
C
C          IF <IXVAL>= -81, AN ERASE WITH PROMPT IS DONE.
C
   35   IF (IXVAL.NE.-81) GO TO 36
C
        CALL XGETIN(IX,IY,KCHAR)
C
        GO TO 37
C
C          IF <IXVAL>= -82, AN ERASE WITHOUT PROMPT IS DONE.
C
   36   IF (IXVAL.NE.-82) GO TO 38
C
   37   UPDFRM= .TRUE.
        GO TO 100
C
C          IF <IXVAL>= -83, THE CURSOR IS DISPLAYED.
C
   38   IF (IXVAL.NE.-83) GO TO 39
C
        CALL XGETIN(IX,IY,KCHAR)
        XLOCAD= IX/DRESX
        YLOCAD= IY/DRESY
        NREPLC= KCHAR
        GO TO 100
C
C          IF <IXVAL>= -84, THE BUFFER IS CLEARED
C          WITHOUT ANY ADDITIONAL ACTION BEING TAKEN.
C
   39   IF (IXVAL.NE.-84) GO TO 40
C
        CALL XFLSBF
        GO TO 100
C
C          IF <IXVAL>= -85, GRAPHICAL OUTPUT IS ENDED.
C
   40   IF (IXVAL.NE.-85) GO TO 41
C
        CALL XCLOSE
        GO TO 100
C
C          IF <IXVAL>= -86, THE REQUIRED CHANNEL IS OPENED.
C
   41   IF (IXVAL.NE.-86) GO TO 42
        GO TO 100
C
C          IF <IXVAL>= -87, THE REQUIRED CHANNEL IS CLOSED.
C
   42   IF (IXVAL.NE.-87) GO TO 100
C
  100 CONTINUE
C
      RETURN
      END
