      SUBROUTINE G0OLAP
C
C          ------------------------------------------------
C          ROUTINE NO. (  45)   VERSION (A8.1)    15:AUG:91
C          ------------------------------------------------
C
C          THIS ROUTINE FINDS AND DISPOSES OF ANY OVERLAP
C          BETWEEN TWO BARS IN A THREE DIMENSIONAL BARCHART.
C
C          THE VALUES USED ARE :
C
C          [XEALIN] ARRAYS CONTAINING COORDINATES OF
C          [YEALIN] POINTS ON THE HIDDEN BAR.
C          [XCHLIN] ARRAYS CONTAINING COORDINATES OF
C          [YCHLIN] POINTS ON THE FRONT BAR.
C          [NUMLNS] THE NUMBER OF POINTS IN XCHLIN,YCHLIN.
C
      REAL    XINTS(22),YINTS(22),XEANEW(22),YEANEW(22)
      INTEGER INDICS(22),INTERS(22,2)
C
      COMMON /T0CHLN/ XEALIN(22),YEALIN(22),XCHLIN(6),YCHLIN(6),NUMLNS
      COMMON /T0INTS/ XLINE1(2),YLINE1(2),XLINE2(2),YLINE2(2)
      COMMON /T3LIMS/ IMAXI,RMAXI,RMINI
C
C
C          FIND LOCATION OF POINTS ON HIDDEN BAR IN RELATION
C          TO THE LINES IN THE FRONT BAR.  IF NONE LIE WITHIN
C          THEN RETURN UNCHANGED.  IF ALL LIE ON OR WITHIN BAR
C          RETURN SETING NUMBER OF POINTS TO ZERO.
C
      IF (NUMLNS.EQ.0) RETURN
C
      KNTINS= 0
      KNTON= 0
C
      DO 100 LINNUM= 1,NUMLNS
        CALL G0INSD(XEALIN(LINNUM),YEALIN(LINNUM),INDIC)
        INDICS(LINNUM)= INDIC
        IF (INDIC.EQ.0) KNTINS= KNTINS+1
        IF (INDIC.EQ.2) KNTON= KNTON+1
  100 CONTINUE
C
      IF (KNTINS+KNTON.EQ.0) RETURN
      IF (KNTINS+KNTON.LT.NUMLNS) GO TO 1
C
      NUMLNS= 0
      RETURN
C
C          CALCULATE THE INTERSECTIONS OF LINES IN THE
C          HIDDEN BAR WITH THE FRONT BAR.
C
    1 NOINTS= 0
C
      DO 200 LINNUM= 1,NUMLNS
        LINEXT= MOD(LINNUM,NUMLNS)+1
        IF (INDICS(LINNUM).NE.1.AND.INDICS(LINEXT).NE.1) GO TO 200
C
        DO 300 IPNT= 1,6
          NXTPNT= MOD(IPNT,6)+1
          XLINE1(1)= XEALIN(LINNUM)
          YLINE1(1)= YEALIN(LINNUM)
          XLINE1(2)= XEALIN(LINEXT)
          YLINE1(2)= YEALIN(LINEXT)
          XLINE2(1)= XCHLIN(IPNT)
          YLINE2(1)= YCHLIN(IPNT)
          XLINE2(2)= XCHLIN(NXTPNT)
          YLINE2(2)= YCHLIN(NXTPNT)
          CALL G0INTR(ICODE,XINT,YINT)
C
C          NO INTERSECT ?
C
          IF (ICODE.EQ.0) GO TO 300
C
          NOINTS= NOINTS+1
          INTERS(NOINTS,1)= LINNUM
          INTERS(NOINTS,2)= IPNT
          XINTS(NOINTS)= XINT
          YINTS(NOINTS)= YINT
 300    CONTINUE
 200  CONTINUE
C
C          DISPOSE OF ONE OF DOUBLE INTERSECTIONS WHICH OCCUR
C          AT THE JOINING OF TWO LINES IN THE FRONT BAR.
C
      I= 0
    2 I= I+1
      IF (I.GT.NOINTS) GO TO 3
      IF (ABS(XINTS(I)-XINTS(MOD(I,NOINTS)+1)).GE.RMINI) GO TO 2
      IF (ABS(YINTS(I)-YINTS(MOD(I,NOINTS)+1)).GE.RMINI) GO TO 2
C
      NOINTS= NOINTS-1
C
      DO 400 J= I,NOINTS
        XINTS(J)= XINTS(J+1)
        YINTS(J)= YINTS(J+1)
        INTERS(J,1)= INTERS(J+1,1)
        INTERS(J,2)= INTERS(J+1,2)
  400 CONTINUE
C
      GO TO 2
C
C          CREATE NEW BAR USING INTERSECTIONS.
C
    3 INEWLI= 0
      ICINTP= 1
C
      DO 500 ICL= 1,NUMLNS
C
C          DOES CURRENT LINE INTERSECT ?
C
        IF (ICINTP.GT.NOINTS.OR.INTERS(ICINTP,1).NE.ICL) GO TO 12
C
C          IF SO LOCATE NEXT VALID INTERSECT.
C
        INCINP= MOD(ICINTP,NOINTS)+1
C
C          ARE CURRENT AND NEXT INTERSECT BOTH WITH SAME LINE
C          IN FRONT BAR.
C
        IF (INTERS(ICINTP,1).EQ.INTERS(INCINP,1)) GO TO 6
C
C          IF NOT ARE ALL POINTS BETWEEN HIDDEN ?
C
        IPNT= INTERS(ICINTP,1)
    4   IPNT= IPNT+1
        IF (IPNT.GT.NUMLNS) IPNT= 1
        IF (IPNT.EQ.INTERS(INCINP,1)) GO TO 6
        IF (INDICS(IPNT).EQ.1) GO TO 5
        GO TO 4
C
C          IF NOT THIS SIDE OF BAR OUTSIDE.
C
    5   ICINTP= INCINP
        GO TO 500
C
C          IF CURRENT POINT LIES OUTWITH FRONT BAR PUT IT IN
C          NEW BAR.
C
    6   IF (INDICS(INTERS(ICINTP,1)).EQ.0) GO TO 7
C
        INEWLI= INEWLI+1
        XEANEW(INEWLI)= XEALIN(ICL)
        YEANEW(INEWLI)= YEALIN(ICL)
C
C          PUT INTERSECT IN NEW BAR
C
    7   INEWLI= INEWLI+1
        XEANEW(INEWLI)= XINTS(ICINTP)
        YEANEW(INEWLI)= YINTS(ICINTP)
C
C          IF ANY POINTS FROM FRONT BAR BETWEEN INTERSECTS
C          PUT THEM IN NEW BAR.
C
        IF (INTERS(ICINTP,2).EQ.INTERS(INCINP,2)) GO TO 10
C
        INSTRT= INTERS(ICINTP,2)
        INSEND= INTERS(INCINP,2)
        ITO= INSEND-INSTRT
        IF (ITO.LT.0) ITO= 6-INSTRT+INSEND
        IF (ITO.GT.3) GO TO 9
C
    8   INSTRT= INSTRT+1
        IF (INSTRT.EQ.INSEND+1) GO TO 10
        IF (INSTRT.GT.6) INSTRT= 1
C
        INEWLI= INEWLI+1
        XEANEW(INEWLI)= XCHLIN(INSTRT)
        YEANEW(INEWLI)= YCHLIN(INSTRT)
        GO TO 8
C
    9   INEWLI= INEWLI+1
        XEANEW(INEWLI)= XCHLIN(INSTRT)
        YEANEW(INEWLI)= YCHLIN(INSTRT)
        INSTRT= INSTRT-1
        IF (INSTRT.EQ.INSEND) GO TO 10
        IF (INSTRT.GT.6) INSTRT= 1
        GO TO 9
C
C          PUT INTERSECT OF NEXT LINE IN NEW BAR
C
   10   INEWLI= INEWLI+1
        XEANEW(INEWLI)= XINTS(INCINP)
        YEANEW(INEWLI)= YINTS(INCINP)
C
C          IF NEXT POINT LIES OUTWITH BAR PUT IT
C          IN NEW BAR.
C
        IF (INDICS(INTERS(INCINP,1)).EQ.0) GO TO 11
C
        INEWLI= INEWLI+1
        XEANEW(INEWLI)= XEALIN(INTERS(INCINP,1))
        YEANEW(INEWLI)= YEALIN(INTERS(INCINP,1))
   11   ICINTP= ICINTP+2
        GO TO 500
C
C          IF CURRENT POINT LIES OUTWITH FRONT BAR PUT IT IN
C          NEW BAR.
C
   12   IF (INDICS(ICL).EQ.0) GO TO 500
        IF (INDICS(ICL).EQ.2.AND.(INDICS(MOD(ICL,NUMLNS)+1).EQ.0.OR.
     &      INDICS(MOD(ICL,NUMLNS)+1).EQ.2)) GO TO 500
C
        INEWLI= INEWLI+1
        XEANEW(INEWLI)= XEALIN(ICL)
        YEANEW(INEWLI)= YEALIN(ICL)
  500 CONTINUE
C
C          STORE NEW BAR IN XEALIN,YEALIN BEFORE RETURNING.
C
      ICNT= 0
C
      DO 600 I= 1,INEWLI
C
C          IF A POINT WILL LIE ON TOP OF NEXT POINT
C          MISS IT OUT.
C
        IF (ABS(XEANEW(I)-XEANEW(MOD(I,INEWLI)+1)).LT.RMINI.AND.
     &      ABS(YEANEW(I)-YEANEW(MOD(I,INEWLI)+1)).LT.RMINI) GO TO 600
C
        ICNT= ICNT+1
        XEALIN(ICNT)= XEANEW(I)
        YEALIN(ICNT)= YEANEW(I)
  600 CONTINUE
C
      NUMLNS= ICNT
      RETURN
      END
