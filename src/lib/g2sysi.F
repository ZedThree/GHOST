      SUBROUTINE G2SYSI(ICOMND,NCHAN,NAMFIL,IDATA,LENDAT,IPLACE,ISTATE)
C
C          ------------------------------------------------
C          ROUTINE NO. (2005)   VERSION (A8.5)    07:NOV:96
C          ------------------------------------------------
C
C          THIS INTERFACES THE GHOST-80 SYSTEM WITH
C          THE OPERATING SYSTEM DISC FILE HANDLERS.
C
C          (THIS VERSION IS FOR THE UNIX OPERATING SYSTEM).
C
C
C          THE ARGUMENTS ARE AS FOLLOWS:
C
C          <ICOMND> THE FUNCTION REQUIRED:
C                   = 1, CHECK THE EXISTENCE OF THE FILE <NAMFIL>,
C                   = 2, CREATE THE FILE <NAMFIL>,
C                   = 3, CONNECT FILE <NAMFIL> TO CHANNEL <NCHAN>,
C                   = 4, CLOSE THE FILE ON CHANNEL <NCHAN> (IF <NCHAN>
C                        = 1 OR 2), OR CLOSE ALL OPEN FILES (OTHERWISE),
C                   = 5, POSITION TO THE GIVEN BLOCK ON CHANNEL <NCHAN>,
C                   = 6, READ THE CURRENT DATA BLOCK ON CHANNEL <NCHAN>,
C                   = 7, WRITE THE GIVEN DATA BLOCK TO CHANNEL <NCHAN>,
C                   = 8, DELETE THE FILE <NAMFIL>;
C          <NCHAN>  THE FILE CHANNEL (= 1 OR 2, EXCEPT AS ABOVE);
C          <NAMFIL> THE FILE NAME;
C          <IDATA>  THE INPUT/OUTPUT DATA BUFFER;
C          <LENDAT> THE LENGTH OF THE DATA BLOCK (WORDS)
C                   OR THE LENGTH OF THE FILENAME (CHARS.);
C          <IPLACE> THE DATA BUFFER START-OFFSET (WORDS)
C                   OR THE BLOCK POSITION (BLOCKS);
C          <ISTATE> THE RESULT CODE FOR THE REQUESTED OPERATION:
C                   =  0, THE OPERATION WAS SUCCESSFUL,
C                   =  1, NOT USED (RESERVED),
C                   =  2, END-OF-FILE HAS OCCURRED,
C                   =  3, THE FILE DOES NOT EXIST,
C                   =  4, A   CREATE REQUEST FAILED,
C                   =  5, AN  ACCESS REQUEST FAILED,
C                   =  6, A    CLOSE REQUEST FAILED,
C                   =  7, A POSITION ATTEMPT FAILED,
C                   =  8, A     READ ATTEMPT FAILED,
C                   =  9, A    WRITE ATTEMPT FAILED, EXCEPT
C                   = 10, A FAILURE DUE TO A READ-ONLY FILE,
C                   = 11, THE CURRENT BLOCK IS CORRUPT,
C                   = 12, A DELETE REQUEST FAILED.
C
C          NOTE:   ERROR STATES 4-9 AND 11-12 ARE IRRECOVERABLE.
C
C
      INTEGER   IDATA(*),NAMFIL(*),IFUNIT(2)
      LOGICAL   OPEN(2),RWTYPE(2),EXISTS,FLOPEN
      CHARACTER NMFIL*128
C
      COMMON /T2IDEN/ IDGRID
      COMMON /T3MACH/ NMCHI,NBITMC
      COMMON /T3SHUT/ K1ERR,K2ERR
C
      SAVE OPEN,RWTYPE,IPOSN,IFUNIT
C
      DATA OPEN /.FALSE.,.FALSE./,RWTYPE /.FALSE.,.FALSE./
C
C          <IRECL> IS THE LENGTH OF THE DATA RECORD.  THE UNITS IN
C          WHICH THE LENGTH IS SPECIFIED ARE MACHINE DEPENDENT.
C
C          THE UNITS ARE '4-CHARACTER WORDS' FOR THE FOLLOWING COMPUTERS:
C          VAX, MISTRAL, SILICON GRAPHICS
C          (SET IRECL TO 512).
C
C          THE UNITS ARE '2-CHARACTER WORDS' FOR THE FOLLOWING COMPUTERS:
C          PRIME (F77 AND FTN77 COMPILERS)
C          (SET IRECL TO 1024).
C
C          THE UNITS ARE 'CHARACTERS' FOR THE FOLLOWING COMPUTERS:
C          APOLLO, GEC, HP, IBMPC, IBM WS, SUN, WHITECHAPEL
C          (SET IRECL TO 2048).
C
C          THE UNITS ARE 'CHARACTERS' FOR THE FOLLOWING COMPUTERS:
C          CRAY
C          (SET IRECL TO 4096).
C
      PARAMETER ( IRECL = GHOST_RECL_UNIT*512 )
C
C
C          IF AN IRRECOVERABLE ERROR HAS OCCURRED, NO MORE IS DONE.
C
      ISTATE= 2
      IF (K2ERR.NE.0) RETURN
C
      ISTATE= 0
      ISTRT= IPLACE+1
      IF (ICOMND.LE.3.OR.ICOMND.EQ.8) THEN
C
        DO 100 ICHR= 1,LENDAT
          CALL G4GETK(NAMFIL,ICHR,NBITMC,NMCHI,NCHAR)
          NMFIL(ICHR:ICHR)= CHAR(NCHAR)
  100   CONTINUE
C
      END IF
C
      GO TO (1,2,4,9,11,12,14,16), ICOMND
C
C          THIS PART CHECKS FOR THE EXISTENCE OF THE GIVEN FILE.
C
    1 INQUIRE(FILE=NMFIL(:LENDAT),EXIST=EXISTS)
      IF (.NOT.EXISTS) ISTATE= 3
      GO TO 18
C
C          THIS PART CREATES THE REQUIRED FILE.
C
C          FIND A SPARE UNIT NUMBER.
C
    2 DO 200 IUNIT= 99,1,-1
        INQUIRE(IUNIT,OPENED=FLOPEN)
        IF (.NOT.FLOPEN) THEN
          ITMPUT= IUNIT
          GO TO 3
        ENDIF
C
  200 CONTINUE
C          
      ISTATE= 4
      GO TO 18
C      
    3 OPEN(UNIT=ITMPUT,FILE=NMFIL(:LENDAT),STATUS='NEW',ACCESS='DIRECT',
     &     RECL=IRECL,IOSTAT=ICODE)
      IF (ICODE.NE.0) ISTATE= 4
C
      CLOSE(UNIT=ITMPUT,IOSTAT=ICODE)
      IF (ICODE.NE.0) ISTATE= 4
      GO TO 18
C
C          THIS PART CONNECTS TO AN EXISTING FILE. A MARKER IS
C          SET WHEN THIS IS DONE. IF THE FILE CANNOT BE OPENED
C          FOR READ AND WRITE, READ-ONLY ACCESS IS TRIED INSTEAD.
C
C          FIND A SPARE UNIT NUMBER.
C
    4 DO 300 IUNIT= 99,1,-1
        INQUIRE(IUNIT,OPENED=FLOPEN)
        IF (.NOT.FLOPEN) THEN
          IFUNIT(NCHAN)= IUNIT
          GO TO 5
        ENDIF
C
  300 CONTINUE
C          
      ISTATE= 5
      GO TO 18
C        
    5 OPEN(IFUNIT(NCHAN),FILE=NMFIL(:LENDAT),STATUS='OLD',
     &     ACCESS='DIRECT',RECL= IRECL,IOSTAT=ICODE)
C     IF (ICODE.NE.0) GO TO 6
C
      RWTYPE(NCHAN)= .TRUE.
      GO TO 7
C
C   6 OPEN(IFUNIT(NCHAN),FILE=NMFIL(:LENDAT),STATUS='READ',
C    &     ACCESS='DIRECT',RECL=IRECL,IOSTAT=ICODE)
C     IF (ICODE.NE.0) GO TO 8
C
C     RWTYPE(NCHAN)= .FALSE.
    7 OPEN(NCHAN)= .TRUE.
      GO TO 18
C
C   8 ISTATE= 5
C     GO TO 18
C
C          THIS PART CLOSES THE GIVEN FILE, OR IF <NCHAN>= 0
C          CLOSES WHICHEVER FILES ARE CURRENTLY OPEN.
C
    9 IF (NCHAN.EQ.2) GO TO 10
      IF (NCHAN.EQ.0.AND..NOT.OPEN(1)) GO TO 10
      IF (.NOT.OPEN(1))                GO TO 18
C
      CLOSE(IFUNIT(1),IOSTAT=ICODE)
      IF (ICODE.EQ.0) OPEN(1)= .FALSE.
      IF (ICODE.NE.0) ISTATE= 6
      IF (NCHAN.NE.0) GO TO 18
   10 IF (.NOT.OPEN(2)) GO TO 18
C
      CLOSE(IFUNIT(2),IOSTAT=ICODE)
      IF (ICODE.EQ.0) OPEN(2)= .FALSE.
      IF (ICODE.NE.0) ISTATE= 6
      GO TO 18
C
C          THIS PART SETS THE CURRENT POSITION IN THE GIVEN FILE.
C
   11 IPOSN= IPLACE
      GO TO 18
C
C          THIS PART READS IN A BLOCK FROM THE CURRENT POSITION.
C          IF THE BLOCK DOES NOT BEGIN WITH '!!!' IT IS CORRUPT.
C
   12 READ(IFUNIT(NCHAN),REC=IPOSN,IOSTAT=ICODE)
     &       (IDATA(IOUT),IOUT=ISTRT,ISTRT+LENDAT-1)
      IF (ICODE.EQ.0) GO TO 13
C
      ISTATE= 8
      GO TO 18
C
   13 IF (IDATA(ISTRT)/256*256.EQ.IDGRID) GO TO 18
C
      ISTATE= 11
      GO TO 18
C
C          THIS PART WRITES A BLOCK FROM THE CURRENT POSITION. AN
C          ERROR RESULTS IF A WRITE IS ATTEMPTED ON A READ-ONLY FILE.
C
   14 IF (RWTYPE(NCHAN)) GO TO 15
C
      ISTATE= 10
      GO TO 18
C
   15 WRITE(IFUNIT(NCHAN),REC=IPOSN,IOSTAT=ICODE)
     &       (IDATA(IOUT),IOUT=ISTRT,ISTRT+LENDAT-1)
      IF (ICODE.EQ.0) GO TO 18
C
      ISTATE= 9
      GO TO 18
C
C          THIS PART DELETES THE REQUIRED FILE.
C
C          FIND A SPARE UNIT NUMBER.
C
   16 DO 400 IUNIT= 99,1,-1
        INQUIRE(IUNIT,OPENED=FLOPEN)
        IF (.NOT.FLOPEN) THEN
          ITMPUT= IUNIT
          GO TO 17
        ENDIF
C
  400 CONTINUE
C  
      ISTATE= 12
      GO TO 18
C      
   17 OPEN(ITMPUT,FILE=NMFIL(:LENDAT),STATUS='OLD',ACCESS='DIRECT',
     &     RECL=IRECL,IOSTAT=ICODE)
      IF (ICODE.NE.0) ISTATE= 12
C
      CLOSE(ITMPUT,STATUS='DELETE',IOSTAT=ICODE)
      IF (ICODE.NE.0) ISTATE= 12
C
C          BEFORE EXIT, IRRECOVERABLE ERRORS CAUSE A SHUT DOWN.
C
   18 IF (ISTATE.LT.4)  RETURN
      IF (ISTATE.EQ.10) RETURN
      IF (OPEN(1)) CLOSE(IFUNIT(1),IOSTAT=ICODE)
      IF (ICODE.EQ.0) OPEN(1)= .FALSE.
      IF (OPEN(2)) CLOSE(IFUNIT(2),IOSTAT=ICODE)
      IF (ICODE.EQ.0) OPEN(2)= .FALSE.
C
      CALL G2SHUT(-1,1)
C
      RETURN
      END
