      SUBROUTINE INTRP2(XPOSNS,YPOSNS,ZPOSNS,NPNTS,XPOS,YPOS,
     &                  RADIUS,TOL,VALUE,IREPLY)
C
C          ------------------------------------------------
C          ROUTINE NO. ( 130)   VERSION (A8.2)    24:NOV:86
C          ------------------------------------------------
C
C          THIS ESTIMATES THE HEIGHT AT A GIVEN POINT OF A SURFACE
C          DEFINED BY A SET OF HEIGHTS AT ARBITRARY X-Y POSITIONS.
C
C
C          THE ARGUMENTS ARE AS FOLLOWS:
C
C          [XPOSNS] THE SET OF SURFACE POINT X-COORDINATES,
C          [YPOSNS] THE SET OF SURFACE POINT Y-COORDINATES,
C          [ZPOSNS] THE SET OF SURFACE POINT Z-COORDINATES,
C          <NPNTS>  THE NUMBER OF DATA POINTS PROVIDED (AND THE
C                   MIN. SIZE OF EACH OF THE PRECEDING ARRAYS),
C          <XPOS>   THE INTERPOLATION POINT X-COORDINATE,
C          <YPOS>   THE INTERPOLATION POINT Y-COORDINATE,
C          <RADIUS> THE RADIUS OF ACCEPTANCE OF DATA POINTS,
C          <TOL>    THE CAPTURE RADIUS OF A SINGLE DATA POINT,
C          <VALUE>  THE (RETURNED) ESTIMATED HEIGHT VALUE,
C          <IREPLY> THE (RETURNED) REPLY CODE:
C                   = 0, THE INTERPOLATION VALUE IS VALID,
C                   = 1, THE INTERPOLATION WAS UNSUCCESSFUL.
C
C
C          THE HEIGHT VALUE IS INTERPOLATED USING A MEAN WEIGHTED
C          INVERSELY AS THE FOURTH POWER OF THE DISTANCE, USING
C          ONLY THOSE DATA POINTS WHOSE POSITION IS WITHIN THE
C          ACCEPTANCE RADIUS OF THE INTERPOLATION POINT. HOWEVER,
C          IF A DATA POINT IS FOUND WHICH IS WITHIN THE CAPTURE
C          RADIUS OF THE INTERPOLATION POINT, THAT HEIGHT IS USED
C          AS THE SURFACE VALUE AND NO INTERPOLATION IS ATTEMPTED.
C
C
      REAL    XPOSNS(NPNTS),YPOSNS(NPNTS),ZPOSNS(NPNTS)
C
      COMMON /T3LIMS/ IMAXI,RMAXI,RMINI
C
C
      SUMWTS= 0.0
      SUMHTS= 0.0
      TOLSQ= TOL*TOL
      RADSQ= RADIUS*RADIUS
C
      DO 100 ISCAN= 1,NPNTS
        XDIFF= XPOSNS(ISCAN)-XPOS
        YDIFF= YPOSNS(ISCAN)-YPOS
        DISTSQ= XDIFF*XDIFF+YDIFF*YDIFF
        IF (DISTSQ.GT.RADSQ) GO TO 100
C
        IF (DISTSQ.GT.TOLSQ) GO TO 1
        IREPLY= 0
        VALUE= ZPOSNS(ISCAN)
        RETURN
C
    1   WEIGHT= 1.0/(DISTSQ*DISTSQ)
        SUMWTS= SUMWTS+WEIGHT
        SUMHTS= SUMHTS+ZPOSNS(ISCAN)*WEIGHT
  100 CONTINUE
C
      IREPLY= 1
      IF (ABS(SUMWTS).LT.RMINI) RETURN
C
      IREPLY= 0
      VALUE= SUMHTS/SUMWTS
C
      RETURN
      END
