      SUBROUTINE BARCHT(BASPOS,BARWID,BARPOS,VALUES,ISTART,ISTOP)
C
C          ------------------------------------------------
C          ROUTINE NO. (  85)   VERSION (A8.3)    14:JUL:88
C          ------------------------------------------------
C
C          THIS DRAWS A BAR CHART OF A GIVEN SET OF VALUES,
C                  WITH HORIZONTAL OR VERTICAL BARS.
C
C
C          THE ARGUMENTS ARE AS FOLLOWS:
C
C          <BASPOS> IS THE BASELINE ORIGIN OF THE GRAPH,
C          <BARWID> IS THE THICKNESS OF THE BARS,
C          [BARPOS] ARE THE POSITIONS OF THE BARS,
C          [VALUES] ARE THE VALUES TO BE GRAPHED,
C          <ISTART> IS THE LOWER BOUNDARY, AND
C          <ISTOP>  IS THE UPPER BOUNDARY OF THE ARRAYS.
C
C
      REAL    BAUTO(2),VAUTO(2),BARPOS(ISTOP),VALUES(ISTOP),RDATA(1)
      INTEGER IDATA(1)
C
      COMMON /T0BRTY/ IBRTYP
      COMMON /T0DBND/ IDRBND
      COMMON /T0KLST/ LSTCL0(100),LENLST
      COMMON /T0PPOS/ XPLOT0,YPLOT0
      COMMON /T0TRAC/ IPRINT
      COMMON /T0TRAR/ RTRAC1,RTRAC2,RTRAC3,RTRAC4
      COMMON /T3LIMS/ IMAXI,RMAXI,RMINI
C
      DATA RDATA /0.0/
C
C
      RTRAC1= BARWID
      IF (IPRINT.EQ.1) CALL G0MESG(133,1)
C
C          AUTOMATIC MAPPING IS DONE FOR THE GIVEN VALUES.
C          THE CURRENT PLOTTING POSITION IS SAVED, THE
C          HISTOGRAM IS DRAWN, THEN THE POSITION IS RESTORED.
C
      IF (ISTOP.LT.ISTART) RETURN
C
      BAUTO(1)= BARPOS(ISTART)-BARWID*0.5
      BAUTO(2)= BARPOS(ISTOP)+BARWID*0.5
      VAUTO(1)= BASPOS
      VAUTO(2)= BASPOS
C
      DO 100 INDEX= ISTART,ISTOP
        VAL=VALUES(INDEX)
        IF (VAL.LT.VAUTO(1)) VAUTO(1)= VAL
        IF (VAL.GT.VAUTO(2)) VAUTO(2)= VAL
  100 CONTINUE
C
      IF (ABS(BAUTO(2)-BAUTO(1)).LT.RMINI) RETURN
      IF (ABS(VAUTO(2)-VAUTO(1)).LT.RMINI) RETURN
      IF (IBRTYP.EQ.0) CALL G0AUTO(BAUTO,VAUTO,1,2,1,2,1)
      IF (IBRTYP.NE.0) CALL G0AUTO(VAUTO,BAUTO,1,2,1,2,1)
C
      XHERE= XPLOT0
      YHERE= YPLOT0
      IF (LENLST.LE.0) GO TO 4
C
      KOLIND= 1
C
      DO 200 IBAR= ISTART,ISTOP
        IF (LSTCL0(KOLIND).EQ.0) GO TO 3
C
        IDATA(1)= 0
        IF (LSTCL0(KOLIND).LT.0) IDATA(1)= 1
C
        CALL G3LINK(5,13,-1,IDATA,RDATA)
        IDATA(1)= IABS(LSTCL0(KOLIND))
        CALL G3LINK(5,3,-1,IDATA,RDATA)
        BRPOS= BARPOS(IBAR)-BARWID*0.5
        BRPOS1= BRPOS+BARWID
        IF (IBRTYP.NE.0) GO TO 1
C
        CALL POSITN(BRPOS,BASPOS)
        CALL JOIN(BRPOS,VALUES(IBAR))
        CALL JOIN(BRPOS1,VALUES(IBAR))
        CALL JOIN(BRPOS1,BASPOS)
        CALL JOIN(BRPOS,BASPOS)
        GO TO 2
C
    1   CALL POSITN(BASPOS,BRPOS)
        CALL JOIN(VALUES(IBAR),BRPOS)
        CALL JOIN(VALUES(IBAR),BRPOS1)
        CALL JOIN(BASPOS,BRPOS1)
        CALL JOIN(BASPOS,BRPOS)
    2   CALL G3LINK(5,4,0,IDATA,RDATA)
    3   KOLIND= KOLIND+1
        IF (KOLIND.GT.LENLST) KOLIND= 1
  200 CONTINUE
C
      IF (IDRBND.EQ.0) GO TO 6
C
    4 DO 300 IBAR= ISTART,ISTOP
        BRPOS= BARPOS(IBAR)-BARWID*0.5
        BRPOS1= BRPOS+BARWID
        IF (IBRTYP.NE.0) GO TO 5
C
        CALL POSITN(BRPOS,BASPOS)
        CALL JOIN(BRPOS,VALUES(IBAR))
        CALL JOIN(BRPOS1,VALUES(IBAR))
        CALL JOIN(BRPOS1,BASPOS)
        GO TO 300
C
    5   CALL POSITN(BASPOS,BRPOS)
        CALL JOIN(VALUES(IBAR),BRPOS)
        CALL JOIN(VALUES(IBAR),BRPOS1)
        CALL JOIN(BASPOS,BRPOS1)
  300 CONTINUE
C
      IF (IBRTYP.EQ.0) CALL JOIN(BAUTO(1),BASPOS)
      IF (IBRTYP.NE.0) CALL JOIN(BASPOS,BAUTO(1))
C
    6 CALL POSITN(XHERE,YHERE)
      RETURN
      END
