      SUBROUTINE CONLBL(LABELS,NLABLS)
C
C          ------------------------------------------------
C          ROUTINE NO. ( 120)   VERSION (A8.1)    10:OCT:86
C                         === FORTRAN-77 ===
C          ------------------------------------------------
C
C          THIS STORES CONTOUR LABELS
C
C          THE ARGUMENTS ARE AS FOLLOWS:
C
C          [LABELS] CONTAINS THE LABELS TO BE WRITTEN ON THE CONTOURS.
C          <NLABLS> IS THE NUMBER OF LABELS.
C
C
      CHARACTER LABELS(NLABLS)*(*)
C
      COMMON /T0CLBL/ LLBCON
      COMMON /T0CNLB/ LBLCON(10,50)
      COMMON /T0TRAC/ IPRINT
      COMMON /T3MACH/ NMCHI,NBITMC
      COMMON /T3SPAC/ ISPACE(1)
C
C
      CALL G3INIT(2)
      IF (IPRINT.EQ.1) CALL G0MESG(176,0)
C
      LLBCON= NLABLS
      IF (LLBCON.LT.0) LLBCON= 0
      IF (LLBCON.EQ.0) RETURN
      IF (LLBCON.GT.50) LLBCON= 50
C
      LENLAB= LEN(LABELS(1))
      IF (LENLAB.GT.40) LENLAB= 40
C
      IPOS= 0
      LIMIT= NMCHI*10
      ISPAC= 0
      CALL G4GETK(ISPACE,NMCHI,NBITMC,NMCHI,ISPAC)
C
      DO 100 NOLAB= 1,LLBCON
        DO 200 ISET= 1,LIMIT
          IPOS= IPOS+1
          NCHAR= ISPAC
          IF (ISET.LE.LENLAB) NCHAR= ICHAR(LABELS(NOLAB)(ISET:ISET))
C
          CALL G4PUTK(LBLCON,IPOS,NBITMC,NMCHI,NCHAR)
  200   CONTINUE
  100 CONTINUE
C
      RETURN
      END
