      SUBROUTINE SECELL(XSTART,YSTART,ANGLE,ECCEN)
C
C          ------------------------------------------------
C          ROUTINE NO. ( 106)   VERSION (A8.3)    14:JUL:88
C          ------------------------------------------------
C
C          THIS DRAWS A SECTOR OF AN ELLIPSE WHICH IS
C          CENTRED ON THE CURRENT PLOTTING POSITION.
C
C
C          <XSTART> IS THE STARTING X-COORDINATE,
C          <YSTART> IS THE STARTING Y-COORDINATE,
C          <ANGLE>  IS THE ARC ANGLE ANTI-CLOCKWISE
C                   FROM THE STARTING POSITION,
C          <ECCEN>  IS THE ECCENTRICITY OF THE ELLIPSE.
C
C
      REAL    RDATA(4)
      INTEGER IDATA(1)
C
      COMMON /T0ACON/ ANGCON
      COMMON /T0DBND/ IDRBND
      COMMON /T0KFIL/ KOLFL0
      COMMON /T0MAPP/ X1MAP0,X2MAP0,Y1MAP0,Y2MAP0
      COMMON /T0PPOS/ XPLOT0,YPLOT0
      COMMON /T0TRAC/ IPRINT
      COMMON /T0TRAR/ RTRAC1,RTRAC2,RTRAC3,RTRAC4
      COMMON /T3CONS/ PI
      COMMON /T3NBYR/ NBYTR
C
      DATA IDATA /0/
C
C
      RTRAC1= XSTART
      RTRAC2= YSTART
      RTRAC3= ANGLE
      RTRAC4= ECCEN
      IF (IPRINT.EQ.1) CALL G0MESG(135,4)
C
      RDATA(1)= XSTART-XPLOT0
      RDATA(2)= YSTART-YPLOT0
      RDATA(4)= ABS(ECCEN)
C
C          CALCULATE THE TRUE ROTATION ANGLE ALLOWING FOR
C          DIFFERENT SCALING ON THE X AND Y AXES.
C
      YSCALE= (X2MAP0-X1MAP0)/(Y2MAP0-Y1MAP0)
      YCOORD= RDATA(2)*YSCALE
      ANG= ANGLE*ANGCON
      SINANG= SIN(ANG)
      COSANG= COS(ANG)
      ANUM= RDATA(4)*SINANG*(RDATA(1)*RDATA(1)+YCOORD*YCOORD)
      DENOM= COSANG*(RDATA(1)*RDATA(1)+
     &       YCOORD*YCOORD*RDATA(4)*RDATA(4))+
     &       SINANG*RDATA(1)*YCOORD*(RDATA(4)*RDATA(4)-1)
      RDATA(3)= ATAN2(ANUM,DENOM)
      IF ((RDATA(3)*ANG).LT.0.0)
     &     RDATA(3)= RDATA(3)+PI*SIGN(1.0,ANG)
    1   IF (ABS(RDATA(3)-ANG).LT.PI*0.5) GO TO 2
        RDATA(3)= RDATA(3)+PI*SIGN(1.0,ANG)
       GO TO 1
C
    2 XC= XPLOT0
      YC= YPLOT0
      YSCALE= YSCALE*RDATA(4)
      XE= XPLOT0+(XSTART-XPLOT0)*COS(RDATA(3))
     &    -(YSTART-YPLOT0)*SIN(RDATA(3))*YSCALE
      YE= YPLOT0+(XSTART-XPLOT0)*SIN(RDATA(3))/YSCALE
     &    +(YSTART-YPLOT0)*COS(RDATA(3))
      IF (KOLFL0.EQ.0) GO TO 3
C
      IDATA(1)= 0
      IF (KOLFL0.LT.0) IDATA(1)= 1
C
      CALL G3LINK(5,13,-1,IDATA,RDATA)
      IDATA(1)= IABS(KOLFL0)
      CALL G3LINK(5,3,-1,IDATA,RDATA)
      CALL POSITN(XC,YC)
      CALL JOIN(XSTART,YSTART)
      CALL POSITN(XC,YC)
      CALL G3LINK(0,9,4*NBYTR,IDATA,RDATA)
      CALL POSITN(XE,YE)
      CALL JOIN(XC,YC)
      CALL G3LINK(5,4,0,IDATA,RDATA)
      IF (IDRBND.EQ.0) RETURN
C
    3 CALL POSITN(XC,YC)
      CALL JOIN(XSTART,YSTART)
      CALL POSITN(XC,YC)
      CALL G3LINK(0,9,4*NBYTR,IDATA,RDATA)
      CALL POSITN(XE,YE)
      CALL JOIN(XC,YC)
C
      RETURN
      END
