      SUBROUTINE CONTIL(SURFAS,ISTRTX,ISTOPX,NPTSX,ISTRTY,ISTOPY,NPTSY,
     &                  CLEVLS,ISTRTL,ISTOPL,XGRIDS,YGRIDS)
C
C          ------------------------------------------------
C          ROUTINE NO. ( 124)   VERSION (A8.8)    15:MAY:87
C          ------------------------------------------------
C
C          THIS DRAWS STRAIGHT-ELEMENT CONTOURS ON AN IRREGULAR GRID.
C
C
C          THE ARGUMENTS ARE AS FOLLOWS:
C
C          [SURFAS]  IS THE ARRAY OF SURFACE HEIGHT VALUES,
C          <ISTRTX>  IS THE LOWER X-EXTENT,
C          <ISTOPX>  IS THE UPPER X-EXTENT, WHILE
C          <ISTRTY>  AND
C          <ISTOPY>  ARE THE CORRESPONDIMG Y-BOUNDS.
C          <NPTSX>   IS THE ACTUAL ARRAY X-EXTENT, AND
C          <NPTSY>   IS THE ACTUAL ARRAY Y-EXTENT.
C          [CLEVLS]  CONTAINS THE VALUES OF CONTOUR HEIGHTS,
C          <ISTRTL>  IS THE STARTING POINT, AND
C          <ISTOPL>  IS THE END POINT OF THIS ARRAY.
C          [XGRIDS]  ARE THE GRID X-POSITIONS.
C          [YGRIDS]  ARE THE GRID Y-POSITIONS.
C
C
      REAL    SURFAS(NPTSX,NPTSY),CLEVLS(ISTOPL),
     &        XGRIDS(NPTSX),YGRIDS(NPTSY)
      LOGICAL OPENCO,ERRON,CURVED
C
      COMMON /T0ACON/ ANGCON
      COMMON /T0CANG/ STANG0,CRANG0
      COMMON /T0CLBL/ LLBCON
      COMMON /T0CLEV/ LEVEL,HEIGHT
      COMMON /T0CMAP/ MAPBIT(6,192),ISTPTX,ISTPTY
      COMMON /T0CTYP/ OPENCO,CURVED
      COMMON /T0CWID/ CWIDX,CWIDY
      COMMON /T0PPOS/ XPLOT0,YPLOT0
      COMMON /T0TRAC/ IPRINT
      COMMON /T0TRAI/ ITRAC1,ITRAC2,ITRAC3,ITRAC4
      COMMON /T0WNDO/ X1WND0,X2WND0,Y1WND0,Y2WND0
      COMMON /T3ERRS/ ERRON,NUMERR
C
      DATA NBITSW /32/
C
C
      ITRAC1= ISTRTX
      ITRAC2= ISTOPX
      ITRAC3= ISTRTY
      ITRAC4= ISTOPY
      IF (IPRINT.EQ.1) CALL G0MESG(48,8)
C
      ILENX= ISTOPX-ISTRTX
      ILENY= ISTOPY-ISTRTY
      IF (ISTRTX.LT.1.OR.ISTRTY.LT.1)         GO TO 901
      IF (ILENX.LT.1.OR.ILENX.GT.192)         GO TO 901
      IF (ILENY.LT.1.OR.ILENY.GT.192)         GO TO 901
      IF (ISTOPX.GT.NPTSX.OR.ISTOPY.GT.NPTSY) GO TO 901
      IF (ISTRTL.LT.1.OR.ISTRTL.GT.ISTOPL)    RETURN
C
C          THE ROUTE-TRACING FLAG, THE CURVE METHOD AND THE
C          CHARACTER MAGNIFICATION ARE SAVED, THEN TRACE IS
C          DISABLED, CURVE METHOD (1) SET AND MAGN. (6) SET.
C
      IPRSAV= IPRINT
      IPRINT= 0
      CURVED= .FALSE.
      ANGSAV= STANG0
      UNSAVE= ANGCON
      ANGCON= 1.0
      IF (LLBCON.GT.0) CALL CTRORI(0.5)
C
      CALL G0AUTO(XGRIDS,YGRIDS,ISTRTX,ISTOPX,ISTRTY,ISTOPY,1)
      XHERE= XPLOT0
      YHERE= YPLOT0
      CWIDX= (X2WND0-X1WND0)/(ISTOPX-ISTRTX)
      CWIDY= (Y2WND0-Y1WND0)/(ISTOPY-ISTRTY)
C
C          TO BEGIN WITH, SOME CONSTANTS ARE SET UP.
C
      ISTRX1= ISTRTX+1
      ISTRY1= ISTRTY+1
      ISTPX1= ISTOPX-1
      ISTPY1= ISTOPY-1
C
C          LOOP-100 DOES ALL THE CONTOURS AT EACH HEIGHT IN TURN.
C          THE BIT ARRAY <MAPBIT> IS INITIALISED FOR EACH HEIGHT,
C          ELEMENTS BEING SET TO '1' WHEN A CONTOUR LINE OF THE
C          CURRENT HEIGHT CROSSES AN EDGE IN AN UPWARDS DIRECTION
C          PROVIDED 'HIGH GROUND' LIES TO THE RIGHT OF THE LINE.
C
      DO 100 LEVELH= ISTRTL,ISTOPL
        HEIGHT= CLEVLS(LEVELH)
        LEVEL= LEVELH
C
        DO 200 ISTPTY= ISTRY1,ISTPY1
          DO 200 ISTPTX= ISTRX1,ISTOPX
            IBIT= 0
            IF (SURFAS(ISTPTX-1,ISTPTY).LT.HEIGHT.AND.
     &          SURFAS(ISTPTX,  ISTPTY).GE.HEIGHT)     IBIT= 1
C
            IXWRD= (ISTPTX-ISTRX1)/NBITSW+1
            IXBIT= MOD((ISTPTX-ISTRX1),NBITSW)+1
            CALL G4PUTB(MAPBIT(IXWRD,(ISTPTY-ISTRTY)),IXBIT,IBIT)
  200   CONTINUE
C
C          <OPENCO> IS INITIALISED AND A SEARCH FOR OPEN
C          CONTOURS BEGINNING ON EACH EDGE IS MADE IN TURN:
C
C          CONTOURS BEGINNING ON THE EDGE <ISTPTY>= <ISTRTY>
C          ARE FOUND, THEN FOLLOWED AND DRAWN USING <G0CTIA>.
C
        OPENCO= .TRUE.
        ISTPTY=  ISTRTY
C
        DO 300 ISTPTX= ISTRX1,ISTOPX
          IF (SURFAS(ISTPTX-1,ISTPTY).LT.HEIGHT.AND.
     &        SURFAS(ISTPTX,  ISTPTY).GE.HEIGHT)
     &        CALL G0CTIA(SURFAS,ISTRTX,ISTOPX,NPTSX,ISTRTY,ISTOPY,
     &                    NPTSY,XGRIDS,YGRIDS,-1,0)
  300   CONTINUE
C
C          CONTOURS BEGINNING ON THE EDGE <ISTPTX>= <ISTOPX>
C          ARE FOUND, THEN FOLLOWED AND DRAWN USING <G0CTIA>.
C
        ISTPTX= ISTOPX
C
        DO 400 ISTPTY= ISTRY1,ISTOPY
          IF (SURFAS(ISTPTX,ISTPTY-1).LT.HEIGHT.AND.
     &        SURFAS(ISTPTX,  ISTPTY).GE.HEIGHT)
     &        CALL G0CTIA(SURFAS,ISTRTX,ISTOPX,NPTSX,ISTRTY,ISTOPY,
     &                    NPTSY,XGRIDS,YGRIDS,0,-1)
  400   CONTINUE
C
C          CONTOURS BEGINNING ON THE EDGE <ISTPTY>= <ISTOPY>
C          ARE FOUND, THEN FOLLOWED AND DRAWN USING <G0CTIA>.
C
        ISTPTY= ISTOPY
C
        DO 500 NEGIX= ISTRTX,ISTPX1
          ISTPTX= ISTPX1+ISTRTX-NEGIX
          IF (SURFAS(ISTPTX+1,ISTPTY).LT.HEIGHT.AND.
     &        SURFAS(ISTPTX,  ISTPTY).GE.HEIGHT)
     &        CALL G0CTIA(SURFAS,ISTRTX,ISTOPX,NPTSX,ISTRTY,ISTOPY,
     &                    NPTSY,XGRIDS,YGRIDS,1,0)
  500   CONTINUE
C
C          CONTOURS BEGINNING ON THE EDGE <ISTPTX>= <ISTRTX>
C          ARE FOUND, THEN FOLLOWED AND DRAWN USING <G0CTIA>.
C
        ISTPTX= ISTRTX
C
        DO 600 NEGIY= ISTRTY,ISTPY1
          ISTPTY= ISTPY1+ISTRTY-NEGIY
          IF (SURFAS(ISTPTX,ISTPTY+1).LT.HEIGHT.AND.
     &        SURFAS(ISTPTX,  ISTPTY).GE.HEIGHT)
     &        CALL G0CTIA(SURFAS,ISTRTX,ISTOPX,NPTSX,ISTRTY,ISTOPY,
     &                    NPTSY,XGRIDS,YGRIDS,0,1)
  600   CONTINUE
C
C          A SEARCH IS MADE FOR CLOSED CONTOURS. <OPENCO> IS
C          SET TO .FALSE. AND <MAPBIT> IS SCANNED: IF THE
C          ELEMENT (ISTPTX,ISTPTY) IS '1', A CLOSED CONTOUR
C          STARTS FROM THE LINE JOINING (ISTPTX-1,ISTPTY) TO
C          (ISTPTX,ISTPTY) AND IT IS FOLLOWED USING <G0CTIA>.
C
        OPENCO= .FALSE.
C
        DO 700 NEGIY= ISTRY1,ISTPY1
          ISTPTY= ISTOPY+ISTRTY-NEGIY
C
          DO 700 NEGIX= ISTRTX,ISTPX1
            ISTPTX= ISTOPX+ISTRTX-NEGIX
            IXWRD= (ISTPTX-ISTRX1)/NBITSW+1
            IXBIT= MOD((ISTPTX-ISTRX1),NBITSW)+1
            CALL G4GETB(MAPBIT(IXWRD,(ISTPTY-ISTRTY)),IXBIT,IBIT)
            IF (IBIT.EQ.1)
     &              CALL G0CTIA(SURFAS,ISTRTX,ISTOPX,NPTSX,ISTRTY,
     &                          ISTOPY,NPTSY,XGRIDS,YGRIDS,-1,0)
  700   CONTINUE
  100 CONTINUE
C
C          THE ENTRY STATE IS ALWAYS RESTORED BEFORE ENDING.
C
      CALL POSITN(XHERE,YHERE)
      CALL CTRORI(ANGSAV)
      ANGCON= UNSAVE
      IPRINT= IPRSAV
      RETURN
C
 901  NUMERR= 11
      IF (ERRON) CALL G0ERMS
C
      RETURN
      END
