      SUBROUTINE HATANG(ANGLE,IENABL)
C
C          ------------------------------------------------
C          ROUTINE NO. ( 330)   VERSION (A8.1)    12:OCT:95
C          ------------------------------------------------
C
C          THIS SETS THE ANGLE FOR CROSS-HATCHING.
C
C
C          THE ARGUMENTS ARE AS FOLLOWS:
C
C          <ANGLE> , THE ANGLE TO BE SET,
C          <IENABL>, CONTROLS WHICH PARAMETERS ARE SET:
C                =1, TO APPLY TO THE FIRST SET OF LINES,
C                =2, TO APPLY TO THE SECOND SET OF LINES,
C                =3, TO APPLY TO BOTH SETS OF LINES.
C
C
      REAL    RDATA(2)
      INTEGER IDATA(1)
C
      COMMON /T0ACON/ ANGCON
      COMMON /T0HANG/ HTANG0(255,2)
      COMMON /T0HFLG/ NHFLG0(255)
      COMMON /T0HNUM/ IHATN0
      COMMON /T0TRAC/ IPRINT
      COMMON /T0TRAR/ RTRAC1,RTRAC2,RTRAC3,RTRAC4
      COMMON /T3NBYR/ NBYTR
C
      DATA IDATA /0/
C
C
      CALL G3INIT(2)
      RTRAC1= ANGLE
      IF (IPRINT.EQ.1) CALL G0MESG(177,1)
      IF (IENABL.EQ.0) RETURN
C
      HTANG= ANGLE*ANGCON
      IENAB= IENABL
      IF (IENAB.LT.1.OR.IENAB.GT.3) IENAB= 3
      IF (MOD(IENAB,2).EQ.1) HTANG0(IHATN0,1)= HTANG
      IF (IENAB/2.EQ.1)      HTANG0(IHATN0,2)= HTANG
C
      NHFLG0(IHATN0)= 1
C
      RDATA(1)= HTANG
      RDATA(2)= IENAB
      CALL G3LINK(5,5,2*NBYTR,IDATA,RDATA)
C
      RETURN
      END
