      SUBROUTINE REGRID(XPOSNS,YPOSNS,ZPOSNS,NPNTS,XLEFT,XRIGHT,
     &                  YLOWER,YUPPER,SURFAS,NPTSX,NPTSY,INTERP)
C
C          ------------------------------------------------
C          ROUTINE NO. ( 128)   VERSION (A8.2)    04:MAR:91
C          ------------------------------------------------
C
C          THIS ESTIMATES THE SURFACE HEIGHTS ON A REGULAR GRID,
C          GIVEN HEIGHTS OF THE SURFACE AT RANDOM COPLANAR POINTS.
C
C
C          THE ARGUMENTS ARE AS FOLLOWS:
C
C          [XPOSNS] THE SET OF X-COORDINATES,
C          [YPOSNS] THE SET OF Y-COORDINATES,
C          [ZPOSNS] THE SET OF Z-COORDINATES,
C                   (ALL OF THESE MUST HAVE AT LEAST <NPNTS> ELEMENTS)
C          <NPNTS>  THE NUMBER OF SURFACE POINTS (THIS MUST BE > 5),
C          <XLEFT>  THE LEFT-HAND EDGE OF THE GRID RECTANGLE,
C          <XRIGHT> THE RIGHT-HAND EDGE OF THE GRID RECTANGLE,
C          <YLOWER> THE LOWER EDGE OF THE GRID RECTANGLE,
C          <YUPPER> THE UPPER EDGE OF THE GRID RECTANGLE,
C          [SURFAS] AN OUTPUT ARRAY OF SIZE <NPTSX> BY <NPTSY>,
C                   WHICH (IF THE ROUTINE IS SUCCESSFUL) CONTAINS
C                   THE ESTIMATED SURFACE HEIGHTS FOR THE GRID.
C                   THE ELEMENT (1,1) CONTAINS THE HEIGHT AT
C                   (<XLEFT>,<YLOWER>) AND THE ELEMENT (<NPTSX>,<NPTSY>)
C                   CONTAINS THE HEIGHT AT (<XRIGHT>,<YUPPER>).
C          <NPTSX>  THE NO. OF GRID INTERVALS IN THE X-DIRECTION,
C          <NPTSY>  THE NO. OF GRID INTERVALS IN THE Y-DIRECTION,
C          <INTERP> IS AN INTERPOLATION ROUTINE WHICH ESTIMATES THE
C                   HEIGHT AT A GIVEN GRID POINT. TWO VERSIONS ARE
C                   SUPPLIED WITH GHOST-80, INTRP1 AND INTRP2. THOSE
C                   WHO WISH TO PROVIDE THEIR OWN VERSION SHOULD REFER
C                   TO THE SPECIFICATION GIVEN AT THE START OF EITHER.
C
C
C          THE SUBROUTINE ESTIMATES SURFACE HEIGHTS ON A REGULAR GRID
C          IN THE RECTANGLE DEFINED BY THE GIVEN X AND Y RANGES. ONLY
C          POINTS WITHIN A CIRCLE OF RADIUS <RADIUS> CENTRED ON A GRID
C          POINT ARE USED IN THE ESTIMATION OF ITS SURFACE HEIGHT.
C          <RADIUS> IS INITIALLY SET SO THAT AN ESTIMATED 30 POINTS LIE
C          INSIDE THE CIRCLE. IF THE SIZE OF <RADIUS> PROVES TOO SMALL,
C          IT IS INCREASED UNTIL A RELIABLE CALCULATION CAN BE MADE.
C
C
      REAL    XPOSNS(NPNTS),YPOSNS(NPNTS),ZPOSNS(NPNTS),
     &        SURFAS(NPTSX,NPTSY)
      LOGICAL ERRON
C
      EXTERNAL INTERP
C
      COMMON /T0TRAC/ IPRINT
      COMMON /T3CONS/ PI
      COMMON /T3ERRS/ ERRON,NUMERR
C
C
      CALL G3INIT(2)
C
      IF (IPRINT.EQ.1) CALL G0MESG(145,0)
C
C          SOME OF THE INPUT ARGUMENTS ARE CHECKED FOR VALIDITY.
C
      IF (NPNTS.LE.5.OR.NPTSX.LT.1.OR.NPTSY.LT.1) GO TO 901
C
      X1= XPOSNS(1)
      X2= XPOSNS(1)
      Y1= YPOSNS(1)
      Y2= YPOSNS(1)
      DO 100 IPT= 2,NPNTS
        TEMPX= XPOSNS(IPT)
        TEMPY= YPOSNS(IPT)
        X1= AMIN1(X1,TEMPX)
        X2= AMAX1(X2,TEMPX)
        Y1= AMIN1(Y1,TEMPY)
        Y2= AMAX1(Y2,TEMPY)
  100 CONTINUE
C
C          THE RECTANGLE ENCLOSING ALL GRID AND DATA POINTS IS FOUND.
C
      X1= AMIN1(X1,XLEFT,XRIGHT)
      X2= AMAX1(X2,XLEFT,XRIGHT)
      Y1= AMIN1(Y1,YLOWER,YUPPER)
      Y2= AMAX1(Y2,YLOWER,YUPPER)
C
C          DATA POINTS WITHIN A RADIUS <RADIUS> OF A GRID POINT ARE
C          TO BE USED TO COMPUTE THE APPROXIMATION. THE RADIUS
C          <RADIUS> IS CALCULATED SO THAT ON AVERAGE THIRTY POINTS LIE
C          INSIDE A CIRCLE OF RADIUS <RADIUS> CENTRED ON A GRID POINT.
C
      XRANGE= X2-X1
      YRANGE= Y2-Y1
      AREA= XRANGE*YRANGE*30.0/FLOAT(NPNTS)
      STARAD= SQRT(AREA/PI)
C
C          IF A DATA POINT IS WITHIN DISTANCE <TOL> OF A GRID POINT,
C          THE SURFACE HEIGHT AT THE DATA POINT IS ACCEPTED AS THE
C          HEIGHT AT THE GRID POINT. THE VALUE OF <TOL> IS SET HERE.
C
      DIAM= SQRT(XRANGE*XRANGE+YRANGE*YRANGE)
      TOL= 0.001*DIAM
      IF (STARAD.LT.TOL) STARAD= TOL
C
C          THE FOLLOWING DOUBLE LOOP CONSTRUCTS THE REGULAR GRID.
C
      XSTEP= (XRIGHT-XLEFT)/FLOAT(NPTSX-1)
      YSTEP= (YUPPER-YLOWER)/FLOAT(NPTSY-1)
      DO 200 IGX= 1,NPTSX
        XPOS= XLEFT+FLOAT(IGX-1)*XSTEP
        DO 300 IGY= 1,NPTSY
          YPOS= YLOWER+FLOAT(IGY-1)*YSTEP
          RADIUS= STARAD
C
C          THE REMEDY FOR AN ERROR IS TO INCREASE <RADIUS>
C          UNTIL THE WHOLE AREA IS COVERED. IF THIS FAILS,
C          THE ATTEMPT IS ABANDONED AND AN ERROR EXIT TAKEN.
C
    2       CALL INTERP(XPOSNS,YPOSNS,ZPOSNS,NPNTS,XPOS,YPOS,RADIUS,TOL,
     &                  VALUE,IREPLY)
            IF (IREPLY.EQ.0) GO TO 3
C
            IF (RADIUS.GT.DIAM) GO TO 902
            RADIUS= 2.0*RADIUS
            GO TO 2
C
    3     SURFAS(IGX,IGY)= VALUE
  300   CONTINUE
  200 CONTINUE
      RETURN
C
  901 NUMERR= 35
      CALL G0ERMS
      RETURN
C
  902 NUMERR= 36
      CALL G0ERMS
C
      RETURN
      END
