      SUBROUTINE G3FLIN(NMFILE,NMPICT,LFILEN,LPICTN,NUMPIC)
C
C          ------------------------------------------------
C          ROUTINE NO. (3003)   VERSION (A7.2A)   21:FEB:86
C          ------------------------------------------------
C
C          THIS READS GRID DATA FROM THE GIVEN GRIDFILE
C          AND CALLS THE APPROPRIATE CLASS ROUTINES TO
C          PERFORM THE REQUIRED OPERATIONS. IF A PICTURE
C          NAME OR NUMBER IS SUPPLIED, ONLY THAT ONE IS
C          DONE; OTHERWISE THE WHOLE GRIDFILE IS DONE.
C
C          (THIS VERSION IS FOR NORMAL GRIDFILE USE).
C
C
C          THE ARGUMENTS ARE AS FOLLOWS:
C
C          <NMFILE> THE FILE NAME.
C          <NMPICT> THE PICTURE NAME.
C          <LFILEN> THE NO. OF CHARS. IN THE FILE NAME.
C          <LPICTN> THE NO. OF CHARS. IN THE PICTURE NAME.
C          <NUMPIC> THE PICTURE NUMBER (IF > 0, THIS TAKES
C                   PRECEDENCE OVER THE PICTURE NAME).
C
C
      REAL    RDATA(64)
      INTEGER IDATA(255),NMFILE(32),NMPICT(4),INBUFR(512)
      LOGICAL ERRON
      LOGICAL FUNCOD,LENCOD,WHOLFL
C
      COMMON /T2BUFF/ IBUFFR(512),INDEXB,IBLOKL
      COMMON /T2INFI/ INFOFI(32),INFOPI(32)
      COMMON /T2INLI/ LNFOFI,LNFOPI
      COMMON /T3CPAK/ NGCHI
      COMMON /T3ERRS/ ERRON,NUMERR
      COMMON /T3MACH/ NMCHI,NBITMC
      COMMON /T3NBYR/ NBYTR
C
C
C          THE FLAGS ARE INITIALISED. WHOLE-FILE DECODING IS SET IF
C          NEITHER PICTURE NAME NOR NUMBER IS GIVEN. LOOP-2 FETCHES
C          EACH SUCCESSIVE PICTURE BLOCK IN TURN UNTIL THE END OF
C          THE PICTURE (OR FILE) IS REACHED, WHILE LOOP-4 GETS EACH
C          GRID FUNCTION FROM THE CURRENT BLOCK, DECODES IT, GETS THE
C          ASSOCIATED DATA, THEN CALLS THE APPROPRIATE CLASS ROUTINE.
C
      FUNCOD= .TRUE.
      LENCOD= .FALSE.
      WHOLFL= (NUMPIC.EQ.0.AND.LPICTN.LE.0)
      NPIC= IABS(NUMPIC)
      IF (WHOLFL) NPIC= 1
C
C          BEFORE EACH PICTURE, A 'BEGIN-INSERT' IS SENT.
C
    1   CALL G3LINK(1,3,0,IDATA,RDATA)
    2     CALL G2FILE(NMFILE,NMPICT,INFOFI,INBUFR,
     &                LFILEN,LPICTN,LNFOFI,NPIC,3,ISTATE)
          IF (ISTATE.GT.2) GO TO 9
C
C          IF THERE IS INFORMATION ASSOCIATED WITH A PICTURE,
C          IT IS LOADED INTO THE PICTURE (INPUT) INFORMATION
C          ARRAY WHEN THE FIRST (PICTURE) HEADER BLOCK IS READ.
C
          LNFOPI= MOD(INBUFR(1),256)
          NWORDS= 0
          IF (LNFOPI.LE.0) GO TO 3
C
          NWORDS= ((LNFOPI-1)/NMCHI)+1
C
          DO 100 LOAD= 1,NWORDS
            INFOPI(LOAD)= INBUFR(LOAD+2)
  100     CONTINUE
C
    3     INDEXI= (NWORDS+2)*NGCHI
    4       INDEXI= INDEXI+1
            CALL G4GETK(INBUFR,INDEXI,8,NGCHI,ICHAR)
C
C          THE GRID DATA CAN BE INTERPRETED AS FUNCTION
C          CODES, ARGUMENT LENGTHS, OR ARGUMENT VALUES:
C
C          THIS SECTION DECODES FUNCTION CODES ('NULL' IS IGNORED).
C
            IF (.NOT.FUNCOD) GO TO 5
C
            ICLASS= (ICHAR/32)
            IFUNCO= MOD(ICHAR,32)
            IF (IFUNCO.EQ.0) GO TO 8
C
            LENCOD= .TRUE.
            FUNCOD= .FALSE.
            GO TO 8
C
C          THIS SECTION GETS THE LENGTH AND RESETS THE ARGUMENT INDEX.
C
    5       IF (.NOT.LENCOD) GO TO 6
C
            LENGTH= ICHAR
            LENCOD= .FALSE.
            INDEXC= 0
            IF (LENGTH.EQ.0) GO TO 7
            GO TO 8
C
C          THIS SECTION UNPACKS THE REQUIRED AMOUNT OF GRID DATA
C          AND REPACKS IT INTO THE CLASS-FUNCTION ARGUMENT ARRAY.
C
    6       INDEXC= INDEXC+1
            IDATA(INDEXC)= ICHAR
            CALL G4PUTC(RDATA,INDEXC,8,NBYTR,ICHAR)
            IF (INDEXC.LT.IABS(LENGTH)) GO TO 8
C
C          LASTLY, THE DATA IS SENT TO THE POST-PROCESSOR VIA <G3LINK>.
C
    7       FUNCOD= .TRUE.
            CALL G3LINK(ICLASS,IFUNCO,LENGTH,IDATA,RDATA)
C
C          DATA IN THE CURRENT BUFFER IS DECODED UNTIL IT BECOMES
C          EMPTY. BLOCKS ARE READ UNTIL <ISTATE>= 2 IS RETURNED
C          (END OF FILE), OR IF <ISTATE>= 1 IS RETURNED (END OF
C          PICTURE) WHEN ONLY A SINGLE PICTURE IS BEING EXECUTED.
C
    8       IF (INDEXI.LT.IBLOKL*NGCHI) GO TO 4
          IF (ISTATE.EQ.0)              GO TO 2
C
C          AFTER EACH PICTURE HAS BEEN READ, AN 'END-INSERT'
C          FUNCTION IS SENT AFTERWARDS. IF A COMPLETE FILE
C          IS BEING READ, A 'NEW-PICTURE' IS SENT AS WELL.
C
    9   IF (NUMPIC.GE.0.AND..NOT.WHOLFL) CALL G3LINK(1,1,0,IDATA,RDATA)
        IF (NUMPIC.LT.0.OR.WHOLFL) CALL G3LINK(1,4,0,IDATA,RDATA)
        IF (NUMPIC.LT.0.OR.WHOLFL) CALL G3LINK(3,3,0,IDATA,RDATA)
        IF (ISTATE.EQ.2) RETURN
        IF (ISTATE.GT.2) GO TO 901
        IF (.NOT.WHOLFL) RETURN
C
        NPIC= NPIC+1
        GO TO 1
C
C          THIS PART OUTPUTS AN ERROR MESSAGE.
C
  901 NUMERR= ISTATE+1998
      IF (ERRON) CALL G2ERMS
C
      RETURN
      END
