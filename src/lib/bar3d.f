      SUBROUTINE BAR3D(VALUES,ISTRTX,ISTOPX,NPTSX,ISTRTY,ISTOPY,NPTSY)
C
C          ------------------------------------------------
C          ROUTINE NO. (  41)   VERSION (A8.1)    27:OCT:88
C          ------------------------------------------------
C
C          THIS ROUTINE DRAWS A THREE DIMENSIONAL BARCHART OF
C          SUPPLIED DATA.
C
C          THE ARGUMENTS ARE AS FOLLOWS:
C
C          [VALUES] ARE THE VALUES TO BE GRAPHED,
C          <ISTRTX> IS THE FIRST X VALUE TO BE USED,
C          <ISTOPX> IS THE LAST X VALUE TO BE USED,
C          <NPTSX>  IS THE MAXIMUM SIZE OF VALUES IN X,
C          <ISTRTY> IS THE FIRST Y VALUE TO BE USED,
C          <ISTOPY> IS THE LAST Y VALUE TO BE  USED,
C          <NPTSY>  IS THE MAXIMUM SIZE OF VALUES IN Y.
C
      REAL    VALUES(NPTSX,NPTSY),ARG(2),RDATA(1)
      INTEGER IDATA(1)
      LOGICAL ERRON
C
      COMMON /T0AUTM/ MAPNUM
      COMMON /T0BANG/ B3ANG
      COMMON /T0BASP/ SIZRAT
      COMMON /T0BBAS/ IBSTYP,BSORIG,BSMAX
      COMMON /T0BKLM/ KMTHOD
      COMMON /T0CHLN/ XEALIN(22),YEALIN(22),XCHLIN(6),YCHLIN(6),NUMLNS
      COMMON /T0DBND/ IDRBND
      COMMON /T0KLST/ LSTCL0(100),LENLST
      COMMON /T0KMAT/ MATCL0(55,55),MTROWS,MTCOLS
      COMMON /T0MACT/ MRKMAP,MRKWIN
      COMMON /T0MAPA/ X1MAPV,X2MAPV,Y1MAPV,Y2MAPV
      COMMON /T0MAPP/ X1MAP0,X2MAP0,Y1MAP0,Y2MAP0
      COMMON /T0PPOS/ XPLOT0,YPLOT0
      COMMON /T0TRAC/ IPRINT
      COMMON /T0TRAI/ ITRAC1,ITRAC2,ITRAC3,ITRAC4
      COMMON /T0WNDO/ X1WND0,X2WND0,Y1WND0,Y2WND0
      COMMON /T3ERRS/ ERRON,NUMERR
      COMMON /T3LIMS/ IMAXI,RMAXI,RMINI
C
      DATA ARG  /0.0,1.0/
C
C          FIRST FIND SUITABLE MAPPING, SAVING CURRENT
C          MAP AND WINDOW VALUES.
C
      ITRAC1= ISTRTX
      ITRAC2= ISTOPX
      ITRAC3= ISTRTY
      ITRAC4= ISTOPY
      IF (IPRINT.EQ.1) CALL G0MESG(171,8)
C
      ILENX= ISTOPX-ISTRTX
      ILENY= ISTOPY-ISTRTY
      XLEN= ILENX+SIZRAT
      YLEN= ILENY+SIZRAT
      IF (ISTRTX.LT.1.OR.ISTRTY.LT.1)         GO TO 901
      IF (ILENX.LT.0.OR.ILENY.LT.0)           GO TO 901
      IF (ISTOPX.GT.NPTSX.OR.ISTOPY.GT.NPTSY) GO TO 901
C
      NUMSAV= MAPNUM
      IPRSAV= IPRINT
      IPRINT= 0
      CALL G0AUTO(ARG,ARG,1,2,1,2,1)
      XPSAV= XPLOT0
      YPSAV= YPLOT0
      X1MSAV= X1MAPV
      X2MSAV= X2MAPV
      Y1MSAV= Y1MAPV
      Y2MSAV= Y2MAPV
      X1WSAV= X1WND0
      X2WSAV= X2WND0
      Y1WSAV= Y1WND0
      Y2WSAV= Y2WND0
      MRKSAV= MRKWIN
      DIFF= X2WND0-X1WND0
      IF (ABS(DIFF).LT.RMINI) RETURN
C
      XM1= (X1MAPV-X1WND0)/DIFF
      XM2= (X2MAPV-X1WND0)/DIFF
      DIFF= Y2WND0-Y1WND0
      IF (ABS(DIFF).LT.RMINI) RETURN
C
      YM1= (Y1MAPV-Y1WND0)/DIFF
      YM2= (Y2MAPV-Y1WND0)/DIFF
      CALL MAP(XM1,XM2,YM1,YM2)
      CALL WINDOW(-0.5,1.5,-0.5,1.5)
C
C          CALCULATE SUITABLE BAR WIDTH.
C
      ANGLE= ATAN(SIN(B3ANG)*(Y2MAP0-Y1MAP0)/
     &           (COS(B3ANG)*(X2MAP0-X1MAP0)))
      SBRANG= SIN(ANGLE)
      CBRANG= COS(ANGLE)
      COSASQ= CBRANG*CBRANG
      COSINA= CBRANG*SBRANG
      SIZE= 1.0/(XLEN+COSASQ*YLEN)
      BRSIZE= SIZE*SIZRAT
      XSTRT= SIZE*XLEN
      YSPOSN= COSINA*BRSIZE
C
C          CALCULATE SUITABLE SCALE FOR BAR HEIGHT.
C
      VALMAX= RMINI
      SCALE= RMAXI
      CRSCA= RMAXI
      IBRFLG= 0
      IF (IBSTYP.EQ.0) BSORIG= VALUES(ISTRTX,ISTRTY)
C
      DO 100 ICURY= ISTRTY,ISTOPY
C
        DO 100 ICURX= ISTRTX,ISTOPX
          IF (IBSTYP.NE.0.AND.VALUES(ICURX,ICURY).GT.BSORIG) IBRFLG= 1
          IF (IBSTYP.EQ.0.AND.VALUES(ICURX,ICURY).LT.BSORIG)
     &                                      BSORIG= VALUES(ICURX,ICURY)
  100 CONTINUE
C
      DO 200 ICRYBR= ISTRTY,ISTOPY
        VLINHI= VALUES(ISTRTX,ICRYBR)
C
        DO 300 ICRXBR= ISTRTX,ISTOPX
          IF (VALUES(ICRXBR,ICRYBR).GT.VLINHI)
     &                                    VLINHI= VALUES(ICRXBR,ICRYBR)
  300   CONTINUE
C
        IF (IBSTYP.NE.0.AND.VLINHI.GT.BSMAX) VLINHI= BSMAX
        IF (VALMAX.LT.VLINHI) VALMAX= VLINHI
        IF (VLINHI.GT.BSORIG) CRSCA= (1.0-YSPOSN)/(VLINHI-BSORIG)
        IF (CRSCA.LT.SCALE) SCALE= CRSCA
C
        YSPOSN= YSPOSN+COSINA*SIZE
  200 CONTINUE
C
      IF (IBRFLG.EQ.0.AND.IBSTYP.NE.0) SCALE= 0.0
C
C          PLOT ONE BAR AT A TIME, STARTING IN BOTTOM RIGHT.
C
      XPOSN= XSTRT
      YPOSN= 0.0
      CXOFFS= COSASQ*BRSIZE
      CYOFFS= COSINA*BRSIZE
C
      DO 400 ICRYBR= ISTRTY,ISTOPY
C
        DO 500 ICRXBR= ISTOPX,ISTRTX,-1
          CZOFFS= SCALE*(AMIN1(VALMAX,VALUES(ICRXBR,ICRYBR))-BSORIG)
          IF (VALUES(ICRXBR,ICRYBR).LT.BSORIG) CZOFFS= 0.0
C
C          DEAL WITH EACH OF THE THREE PARTS TO A BAR
C          ONE AT A TIME.
C
          DO 600 IWHSUR= 1,3
            IF (IWHSUR.NE.1) GO TO 2
C
C          SIDE OF BAR.
C
            XEALIN(1)= XPOSN
            YEALIN(1)= YPOSN
            XEALIN(2)= XPOSN+CXOFFS
            YEALIN(2)= YPOSN+CYOFFS
            XEALIN(3)= XPOSN+CXOFFS
            YEALIN(3)= YPOSN+CYOFFS+CZOFFS
            XEALIN(4)= XPOSN
            YEALIN(4)= YPOSN+CZOFFS
            XSMLST= XPOSN
            XLRGST= XPOSN+CXOFFS
            YSMLST= YPOSN
            YLRGST= YPOSN+CYOFFS+CZOFFS
            GO TO 4
C
    2       IF (IWHSUR.NE.2) GO TO 3
C
C          TOP OF BAR.
C
            XEALIN(1)= XPOSN
            YEALIN(1)= YPOSN+CZOFFS
            XEALIN(2)= XPOSN+CXOFFS
            YEALIN(2)= YPOSN+CYOFFS+CZOFFS
            XEALIN(3)= XPOSN-BRSIZE+CXOFFS
            YEALIN(3)= YPOSN+CYOFFS+CZOFFS
            XEALIN(4)= XPOSN-BRSIZE
            YEALIN(4)= YPOSN+CZOFFS
            XSMLST= XPOSN-BRSIZE
            YSMLST= YPOSN+CZOFFS
            GO TO 4
C
C          FRONT OF BAR.
C
    3       XEALIN(1)= XPOSN
            YEALIN(1)= YPOSN
            XEALIN(2)= XPOSN
            YEALIN(2)= YPOSN+CZOFFS
            XEALIN(3)= XPOSN-BRSIZE
            YEALIN(3)= YPOSN+CZOFFS
            XEALIN(4)= XPOSN-BRSIZE
            YEALIN(4)= YPOSN
            XLRGST= XPOSN
            YSMLST= YPOSN
            YLRGST= YPOSN+CZOFFS
    4       IF (ABS(XLRGST-XSMLST).LT.RMINI.OR.
     &          ABS(YLRGST-YSMLST).LT.RMINI) GO TO 600
C
            CHXPOS= XSTRT
            CHYPOS= 0.0
            NUMLNS= 4
C
C          CHECK FOR ANY BAR ALREADY PLOTTED OVERLAPPING
C          WITH CURRENT BAR.
C
            DO 700 ICHYBR= ISTRTY,ICRYBR
C
              DO 800 ICHXBR= ISTOPX,ISTRTX,-1
C
                IF (ICHYBR.GE.ICRYBR.AND.ICHXBR.LE.ICRXBR) GO TO 800
                IF (CHXPOS-BRSIZE.GE.XLRGST) GO TO 5
                IF (CHXPOS+CXOFFS.LE.XSMLST) GO TO 800
C
                CHZOFS= SCALE*(AMIN1(VALMAX,VALUES(ICHXBR,ICHYBR))
     &                  -BSORIG)
                IF (VALUES(ICHXBR,ICHYBR).LT.BSORIG) CHZOFS= 0.0
                IF (CHYPOS+CHZOFS+CYOFFS.LE.YSMLST) GO TO 5
C
                XCHLIN(1)= CHXPOS
                YCHLIN(1)= CHYPOS
                XCHLIN(2)= CHXPOS+CXOFFS
                YCHLIN(2)= CHYPOS+CYOFFS
                XCHLIN(3)= CHXPOS+CXOFFS
                YCHLIN(3)= CHYPOS+CYOFFS+CHZOFS
                XCHLIN(4)= CHXPOS-BRSIZE+CXOFFS
                YCHLIN(4)= CHYPOS+CYOFFS+CHZOFS
                XCHLIN(5)= CHXPOS-BRSIZE
                YCHLIN(5)= CHYPOS+CHZOFS
                XCHLIN(6)= CHXPOS-BRSIZE
                YCHLIN(6)= CHYPOS
                CALL G0OLAP
    5           CHXPOS= CHXPOS-SIZE
  800         CONTINUE
C
              CHYPOS= CHYPOS+COSINA*SIZE
              CHXPOS= XSTRT+FLOAT(ICHYBR-ISTRTY+1)*COSASQ*SIZE
  700       CONTINUE
C
C          IF SURFACE IS HIDDEN THERE IS NO NEED TO DRAW IT.
C
            IF (NUMLNS.EQ.0) GO TO 600
C
C          IF COLOURING NECESARY CHOOSE CURRENT COLOUR
C          ACCORDING TO DESIRED METHOD.
C
            IF (KMTHOD.EQ.0) GO TO 10
            IF (KMTHOD.NE.1) GO TO 6
C
C          COLOUR FILL METHOD 1
C
            KOLINX= MOD(ICRXBR,LENLST)
            IF (KOLINX.EQ.0) KOLINX= LENLST
C
            KOLLOR= LSTCL0(KOLINX)
            GO TO 9
C
    6       IF (KMTHOD.NE.2) GO TO 7
C
C          COLOUR FILL METHOD 2
C
            KOLINY= MOD(ICRYBR,LENLST)
            IF (KOLINY.EQ.0) KOLINY= LENLST
C
            KOLLOR= LSTCL0(KOLINY)
            GO TO 9
C
    7       IF (KMTHOD.NE.3) GO TO 8
C
C          COLOUR FILL METHOD 3
C
            IND= MOD(IWHSUR,3)+1
            KOLLOR= LSTCL0(IND)
            GO TO 9
C
C          COLOUR FILL METHOD 4
C
    8       KOLINX= MOD(ICRXBR,MTROWS)
            IF (KOLINX.EQ.0) KOLINX= MTROWS
C
            KOLINY= MOD(ICRYBR,MTCOLS)
            IF (KOLINY.EQ.0) KOLINY= MTCOLS
C
            KOLLOR= MATCL0(KOLINX,KOLINY)
C
C          ALL METHODS
C
    9       IF (KOLLOR.EQ.0) GO TO 10
C
            IDATA(1)= 0
            IF (KOLLOR.LT.0) IDATA(1)= 1
C
            CALL G3LINK(5,13,-1,IDATA,RDATA)
            IDATA(1)= IABS(KOLLOR)
            CALL G3LINK(5,3,-1,IDATA,RDATA)
            CALL POSITN(XEALIN(NUMLNS),YEALIN(NUMLNS))
C
            DO 900 I= 1,NUMLNS
              CALL JOIN(XEALIN(I),YEALIN(I))
  900       CONTINUE
C
            CALL G3LINK(5,4,0,IDATA,RDATA)
            IF (IDRBND.EQ.0) GO TO 600
C
C          DRAW LINES IN CURRENT BAR SEGMENT.
C
   10       CALL POSITN(XEALIN(NUMLNS),YEALIN(NUMLNS))
C
            DO 1000 I= 1,NUMLNS
              CALL JOIN(XEALIN(I),YEALIN(I))
 1000       CONTINUE
C
  600     CONTINUE
C
          XPOSN= XPOSN-SIZE
  500   CONTINUE
C
        YPOSN= YPOSN+COSINA*SIZE
        XPOSN= XSTRT+FLOAT(ICRYBR-ISTRTY+1)*COSASQ*SIZE
  400 CONTINUE
C
C          THE ENTRY STATE IS RESTORED BEFORE RETURNING
C
      CALL WINFOL
      MAPNUM= NUMSAV
      CALL G0MAPS(X1MSAV,X2MSAV,Y1MSAV,Y2MSAV)
      IF (MRKSAV.NE.0) CALL WINDOW(X1WSAV,X2WSAV,Y1WSAV,Y2WSAV)
C
      CALL POSITN(XPSAV,YPSAV)
      IPRINT= IPRSAV
      RETURN
C
  901 NUMERR= 9
      IF (ERRON) CALL G0ERMS
      RETURN
      END
