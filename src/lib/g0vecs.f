      SUBROUTINE G0VECS(XPT,YPT,NPTS)
C
C          ------------------------------------------------
C          ROUTINE NO. (  77)   VERSION (A8.5)    25:NOV:86
C          ------------------------------------------------
C
C          THIS GENERATES A SERIES OF SHORT VECTORS
C          FROM A GIVEN SET OF X AND Y COORDINATES.
C
C
C          [XPT]    ARE THE X-COORDINATES AND
C          [YPT]    ARE THE Y-COORDINATES OF THE POINTS,
C          <NPTS>   IS THE NUMBER OF POINTS GIVEN.
C
C
      REAL    RDATA(1),XPT(NPTS),YPT(NPTS),SCALE(2)
      INTEGER IDATA(1),ISHVEC(254)
C
      COMMON /T0PPOS/ XPLOT0,YPLOT0
      COMMON /T3LIMS/ IMAXI,RMAXI,RMINI
      COMMON /T3NBYR/ NBYTR
C
      DATA RDATA /0.0/, IDATA /0/
C
C
C          IF THERE ARE LESS THAN TWO POINTS, THE SUBROUTINE EXITS.
C          IF THERE ARE ONLY TWO POINTS, A STRAIGHT LINE IS DRAWN.
C
      NVECS= NPTS-1
      IF (NVECS.LE.0) RETURN
      IF (NVECS.GT.126) NVECS= 126
C
      CALL POSITN(XPT(1),YPT(1))
      IF (NVECS.EQ.1) GO TO 1
C
C          THE MAXIMUM X AND Y VECTORS ARE FOUND AND USED
C          TO CALCULATE THE SCALE FACTORS, WHICH ARE STORED.
C
      VXMAX= 0.0
      VYMAX= 0.0
C
      DO 100 ITEST= 1,NVECS
        XVEC= ABS(XPT(ITEST+1)-XPT(ITEST))
        IF (XVEC.GT.VXMAX) VXMAX= XVEC
C
        YVEC= ABS(YPT(ITEST+1)-YPT(ITEST))
        IF (YVEC.GT.VYMAX) VYMAX= YVEC
  100 CONTINUE
C
      SCALE(1)= VXMAX/127.0
      SCALE(2)= VYMAX/127.0
      IF (ABS(SCALE(1)).LT.RMINI) SCALE(1)= 1.0
      IF (ABS(SCALE(2)).LT.RMINI) SCALE(2)= 1.0
C
      CALL G3LINK(0,6,2*NBYTR,IDATA,SCALE)
C
C          THE SERIES OF SHORT VECTORS IS THEN CALCULATED,
C          (ZERO LENGTH VECTORS ARE OMITTED) AND STORED.
C
      IXOLD= 0
      IYOLD= 0
      INDEX= 1
C
      DO 200 IPT= 1,NVECS
        IXVEC= (XPT(IPT+1)-XPT(1))/SCALE(1)+0.5
        IYVEC= (YPT(IPT+1)-YPT(1))/SCALE(2)+0.5
        IF (IXVEC.EQ.IXOLD.AND.IYVEC.EQ.IYOLD) GO TO 200
C
        ISHVEC(INDEX)=   IXVEC-IXOLD+128
        ISHVEC(INDEX+1)= IYVEC-IYOLD+128
        INDEX= INDEX+2
        IXOLD= IXVEC
        IYOLD= IYVEC
  200 CONTINUE
C
      INDEX= INDEX-1
      CALL G3LINK(0,7,-INDEX,ISHVEC,RDATA)
C
C          A LINE IS THEN DRAWN TO THE FINAL POSITION.
C
      XPLOT0= XPLOT0+IXOLD*SCALE(1)
      YPLOT0= YPLOT0+IYOLD*SCALE(2)
    1 CALL JOIN(XPT(NPTS),YPT(NPTS))
C
      RETURN
      END
