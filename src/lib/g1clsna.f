       SUBROUTINE G1CLSN(ICODE,LENGTH,IDATA,RDATA)
C
C          ------------------------------------------------
C          ROUTINE NO. (1019)   VERSION (A8.5A)   07:NOV:96
C          ------------------------------------------------
C
C          THIS OUTPUTS FUNCTIONS OF ALL CLASSES TO THE TRANGRID FILE.
C
C
C          THE ARGUMENTS ARE AS FOLLOWS:
C
C          <ICODE>  GIVES THE FUNCTION CODE (WHICH
C                   SHOULD BE IN THE RANGE 0 TO 255),
C          <LENGTH> GIVES THE DATA LENGTH (IN BYTES),
C          [IDATA]  IS THE INTEGER DATA ARRAY, AND
C          [RDATA]  IS THE ALTERNATIVE REAL DATA ARRAY.
C
C
      REAL    RDATA(*)
      INTEGER IDATA(*),IDUMMY(1)
      LOGICAL INTDAT
      LOGICAL PREADY
C
      COMMON /T1TRAA/ ICODEB(26),INDXB
      COMMON /T1TRBB/ RFUNOB(64),IFUNOB(257)
      COMMON /T1TRFN/ KFILEN(128)
      COMMON /T1TRSV/ PREADY,KNAML
      COMMON /T2INFO/ INFOFO(32),INFOPO(32)
      COMMON /T2INLO/ LNFOFO,LNFOPO
      COMMON /T2OPNA/ NAMEFO(32),NAMEPO(4)
      COMMON /T2OPNL/ LNFILN,LNPICN
      COMMON /T3MACH/ NMCHI,NBITMC
      COMMON /T3NBYR/ NBYTR
C
      DATA IDUMMY /0/
C
C
C          ILLEGAL FUNCTION CODES ARE IGNORED.
C
      IF (ICODE.LE.0)                    RETURN
      IF (ICODE.GT. 14.AND.ICODE.LT. 32) RETURN
      IF (ICODE.GT. 36.AND.ICODE.LT. 59) RETURN
      IF (ICODE.GT. 81.AND.ICODE.LT. 96) RETURN
      IF (ICODE.GT.108.AND.ICODE.LT.128) RETURN
      IF (ICODE.GT.132.AND.ICODE.LT.160) RETURN
      IF (ICODE.GT.176.AND.ICODE.LT.192) RETURN
      IF (ICODE.GT.192.AND.ICODE.LT.224) RETURN
      IF (ICODE.GT.241)                  RETURN
C
C          THE DATA TYPE IS FOUND FROM THE CODE.
C
      INTDAT= .TRUE.
      IF (ICODE.GE.  2.AND.ICODE.LE.  6) INTDAT= .FALSE.
      IF (ICODE.GE.  8.AND.ICODE.LE.  9) INTDAT= .FALSE.
      IF (ICODE.GE. 12.AND.ICODE.LE. 13) INTDAT= .FALSE.
      IF (ICODE.GE. 68.AND.ICODE.LE. 69) INTDAT= .FALSE.
      IF (ICODE.EQ. 72.OR .ICODE.EQ. 74) INTDAT= .FALSE.
      IF (ICODE.EQ.161.OR .ICODE.EQ.165) INTDAT= .FALSE.
      IF (ICODE.GE.225.AND.ICODE.LE.230) INTDAT= .FALSE.
      IF (ICODE.GE.232.AND.ICODE.LE.237) INTDAT= .FALSE.
      IF (ICODE.EQ.239)                  INTDAT= .FALSE.
C
C          IF A NEW PICTURE IS BEGUN, PICTURE READY IS SET.
C
      IF (ICODE.NE.99.AND.ICODE.NE.107) GO TO 1
C
      PREADY= .TRUE.
      RETURN
C
C          IF <ICODE>= 34, A <GREND> OPERATION IS EXECUTED.
C          THE <END-OF-TRANGRID> INSTRUCTION IS ADDED TO THE
C          INTEGER ARGUMENT LIST AND THE OUTPUT COMPLETED BY
C          CALLING <G1TRAN>. THE LAST LINE IS THEN PADDED WITH
C          ZEROES BY CLEARING THE BUFFER AND CALLING <G1DVIO>.
C
    1 IF (ICODE.NE.34) GO TO 3
C
      IFUNOB(1)= 63
      IFUNOB(2)= 0
      CALL G1TRAN(2)
      IF (INDXB.EQ.1) GO TO 2
C
      DO 100 ICLEAR= INDXB,26
        ICODEB(ICLEAR)= 0
  100 CONTINUE
      CALL G1DVIO(3,ICODEB,26)
C
    2 CALL G1DVIO(2,IDUMMY,1)
      RETURN
C
C          IF A NEW PICTURE IS READY, THE FILE AND PICTURE NAMES
C          ARE WRITTEN OUT AS APPROPRIATE BEFORE ANY NEW OUTPUT.
C
    3 IF (.NOT.PREADY) GO TO 9
C
C          IF THE FILE NAME HAS CHANGED SINCE LAST TIME, THE FILE
C          NAME AND INFORMATION ARE WRITTEN TO THE TRANGRID FILE.
C
C          THIS SECTION CHECKS FOR A CHANGE OF NAME.
C
      IF (KNAML.NE.LNFILN) GO TO 4
C
      DO 200 ICOMPR= 1,LNFILN
        CALL G4GETK(NAMEFO,ICOMPR,NBITMC,NMCHI,ICHAR)
        IF (ICHAR.NE.KFILEN(ICOMPR)) GO TO 4
  200 CONTINUE
      GO TO 6
C
C          THIS SECTION STORES THE NEW NAME AND WRITES IT.
C
    4 KNAML= LNFILN
      IFUNOB(1)= 59
      IFUNOB(2)= LNFILN
C
      DO 300 LOAD= 1,LNFILN
        CALL G4GETK(NAMEFO,LOAD,NBITMC,NMCHI,ICHAR)
        KFILEN(LOAD)= ICHAR
        CALL G4COCO(ICHAR,IGHOCH)
        IFUNOB(LOAD+2)= IGHOCH
  300 CONTINUE
      CALL G1TRAN(2)
C
C          THE FILE INFORMATION IS SENT, EVEN IF IT HAS ZERO LENGTH.
C
      IFUNOB(1)= 60
      IFUNOB(2)= LNFOFO
      IF (LNFOFO.LE.0) GO TO 5
C
      DO 400 LOAD= 1,LNFOFO
        CALL G4GETK(INFOFO,LOAD,NBITMC,NMCHI,ICHAR)
        CALL G4COCO(ICHAR,IGHOCH)
        IFUNOB(LOAD+2)= IGHOCH
  400 CONTINUE
    5 CALL G1TRAN(2)
C
C          THIS SECTION WRITES THE PICTURE NAME TO THE TRANGRID FILE.
C
    6 IFUNOB(1)= 61
      IFUNOB(2)= LNPICN
      IF (LNPICN.LE.0) GO TO 7
C
      DO 500 LOAD= 1,LNPICN
        CALL G4GETK(NAMEPO,LOAD,NBITMC,NMCHI,ICHAR)
        CALL G4COCO(ICHAR,IGHOCH)
        IFUNOB(LOAD+2)= IGHOCH
  500 CONTINUE
    7 CALL G1TRAN(2)
C
C          THE PICTURE INFORMATION IS SENT, EVEN IF IT HAS ZERO LENGTH.
C
      IFUNOB(1)= 62
      IFUNOB(2)= LNFOPO
      IF (LNFOPO.LE.0) GO TO 8
C
      DO 600 LOAD= 1,LNFOPO
        CALL G4GETK(INFOPO,LOAD,NBITMC,NMCHI,ICHAR)
        CALL G4COCO(ICHAR,IGHOCH)
        IFUNOB(LOAD+2)= IGHOCH
  600 CONTINUE
    8 CALL G1TRAN(2)
      PREADY= .FALSE.
C
C          THIS PART PROVIDES NORMAL FUNCTION/DATA OUTPUT.
C
    9 IFUNOB(1)= ICODE
C
C          THE TWO CASES A) INTEGER DATA OR NO DATA
C          AND B) REAL DATA ARE ENCODED DIFFERENTLY.
C
      IF (.NOT.INTDAT) GO TO 11
C
C          THIS PART DEALS WITH INTEGER DATA OR NO DATA.
C
      LEN= IABS(LENGTH)
      IFUNOB(2)= LEN
      IF (LEN.LE.0) GO TO 10
C
      DO 700 IWORD= 1,LEN
        IFUNOB(IWORD+2)= IDATA(IWORD)
  700 CONTINUE
   10 CALL G1TRAN(2)
      RETURN
C
C          THIS PART DEALS WITH REAL DATA.
C
   11 LIMIT= LENGTH/NBYTR
      IFUNOB(2)= LIMIT
      IF (LIMIT.LE.0) GO TO 12
C
      DO 800 IWORD= 1,LIMIT
        RFUNOB(IWORD)= RDATA(IWORD)
  800 CONTINUE
   12 CALL G1TRAN(1)
C
      RETURN
      END
