      SUBROUTINE PIELBL(LABELS,NLABLS)
C
C          ------------------------------------------------
C          ROUTINE NO. (  87)   VERSION (A8.3)    20:JAN:92
C                         === FORTRAN-77 ===
C          ------------------------------------------------
C
C          THIS LABELS ALL THE SECTORS OF A PIECHART.
C
C          THE ARGUMENTS ARE AS FOLLOWS:
C
C          [LABELS] CONTAINS THE LABELS TO BE WRITTEN ON THE SECTORS.
C          <NLABLS> IS THE NUMBER OF LABELS.
C
C
      LOGICAL ERRON
      CHARACTER LABELS(NLABLS)*(*)
C
      COMMON /T0ACON/ ANGCON
      COMMON /T0CANG/ STANG0,CRANG0
      COMMON /T0CDIM/ MAGN0,OBLAT0
      COMMON /T0CSPA/ X1CHR0,X2CHR0,Y1CHR0,Y2CHR0
      COMMON /T0KLAB/ LABCL0(100),LENLAB
      COMMON /T0LATT/ KOLIN0,ITHIK0
      COMMON /T0MAPP/ X1MAP0,X2MAP0,Y1MAP0,Y2MAP0
      COMMON /T0PIAN/ ANGPIE
      COMMON /T0PIE/  XCPIE,YCPIE,PIEVAL(50),NPVALS,PIERAD,PIETOT,SIZLIM
      COMMON /T0PILM/ MPIELB
      COMMON /T0PPOS/ XPLOT0,YPLOT0
      COMMON /T0TRAC/ IPRINT
      COMMON /T3CONS/ PI
      COMMON /T3ERRS/ ERRON,NUMERR
C
C
      IF (IPRINT.EQ.1) CALL G0MESG(137,0)
      IF (NLABLS.LE.0) RETURN
      IF (NPVALS.GT.50) GO TO 901
      IF (NLABLS.NE.NPVALS) GO TO 902
C
      IPRSAV= IPRINT
      IPRINT= 0
      XSAVE= XPLOT0
      YSAVE= YPLOT0
      KOLLSV= KOLIN0
      KOLLIN= 1
      ORSAVE= STANG0/ANGCON
      MAGSAV= MAGN0
      NCHRS= LEN(LABELS(1))
      YSCALE= (Y2MAP0-Y1MAP0)/(X2MAP0-X1MAP0)
      XCSAV1= X1CHR0
      XCSAV2= X2CHR0
      YCSAV1= Y1CHR0
      YCSAV2= Y2CHR0
      CALL CSPACE(0.0,0.0,0.0,0.0)
      IF (MPIELB.GT.1) GO TO 1
C
      CALL CTRORI(0.0)
      SIZE= AMIN1(0.17,SIZLIM)*ABS(YSCALE)/SQRT(NCHRS*OBLAT0)
      CALL CTRSIZ(SIZE*PIERAD)
    1 ADANGL= ANGPIE
C
      DO 100 LCOUNT= 1,NLABLS
        ANGLE= ABS(PIEVAL(LCOUNT))*PIETOT
        ANGLAB= ANGLE*0.5+ADANGL
        ADANGL= ADANGL+ANGLE
        COSLAB= COS(ANGLAB)
        SINLAB= SIN(ANGLAB)*YSCALE
        TEST= COSLAB*(X2MAP0-X1MAP0)
        OFFSET= 0.0
        IF (PIEVAL(LCOUNT).LT.0.0) OFFSET= 0.1
C
        RAD= PIERAD*(0.85+OFFSET)
        RX= RAD*COSLAB+XCPIE
        RY= RAD*SINLAB+YCPIE
        IF (LENLAB.GT.0) CALL LINCOL(LABCL0(KOLLIN))
C
        KOLLIN= KOLLIN+1
        IF (KOLLIN.GT.LENLAB) KOLLIN= 1
C
        DO 200 ICHAR= 1,NCHRS
          NCHARS= NCHRS-ICHAR+1
          IF (LABELS(LCOUNT)(NCHARS:NCHARS).NE.' ') GO TO 2
  200   CONTINUE
C
    2   NCHARS= NCHRS-NCHARS
        CALL POSITN(RX,RY)
        IF (MPIELB.GT.1) GO TO 4
        IF (COSLAB.GE.0.0) CALL CTRORI(ANGLAB/ANGCON)
        IF (COSLAB.LT.0.0) CALL CTRORI((ANGLAB-PI)/ANGCON)
        IF (TEST.LT.0.0) GO TO 3
C
        CALL SPACE(NCHARS-1)
        CALL TCSEND(LABELS(LCOUNT))
        GO TO 100
C
    3   CALL TYPECS(LABELS(LCOUNT))
        GO TO 100
C
    4   RX= RX+0.25*PIERAD*COSLAB
        RY= RY+0.25*PIERAD*SINLAB
        CALL JOIN(RX,RY)
        RX= RX+0.15*PIERAD*SIGN(1.0,COSLAB)
        CALL JOIN(RX,RY)
        IF (TEST.LT.0.0) GO TO 5
C
        CALL SPACE(1)
        CALL TYPECS(LABELS(LCOUNT))
        GO TO 100
C
    5   CALL SPACE(NCHARS)
        CALL TCSEND(LABELS(LCOUNT))
  100 CONTINUE
C
      CALL LINCOL(KOLLSV)
      CALL POSITN(XSAVE,YSAVE)
      CALL CTRORI(ORSAVE)
      CALL CTRMAG(MAGSAV)
      CALL CSPACE(XCSAV1,XCSAV2,YCSAV1,YCSAV2)
      IPRINT= IPRSAV
C
      RETURN
C
  901 NUMERR= 25
      GO TO 903
C
  902 NUMERR= 26
  903 IF (ERRON) CALL G0ERMS
C
      RETURN
      END
