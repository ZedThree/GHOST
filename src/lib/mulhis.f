      SUBROUTINE MULHIS(XORIG,YORIG,BARWID,VALSTS,ISTART,ISTOP,NOVALS,
     &                  NOSETS)
C
C          ------------------------------------------------
C          ROUTINE NO. (  94)   VERSION (A8.1)    14:JUL:88
C          ------------------------------------------------
C
C          THIS DRAWS A SET OF HISTOGRAMS FROM GIVEN VALUES.
C          BARS CAN BE HORIZONTAL OR VERTICAL.
C          THIS USES ABSOLUTE VALUES.
C
C
C          THE ARGUMENTS ARE AS FOLLOWS:
C
C          <XORIG>  IS THE X-ORIGIN OF THE GRAPH,
C          <YORIG>  IS THE Y-ORIGIN OF THE GRAPH,
C          <BARWID> IS THE THICKNESS OF THE BARS,
C          [VALSTS] ARE THE VALUES TO BE GRAPHED,
C          <ISTART> IS THE LOWER BOUNDARY, AND
C          <ISTOP>  IS THE UPPER BOUNDARY OF THE BAR NUMBERS,
C          <NOVALS> IS THE TOTAL NUMBER OF BARS IN THE VALUES ARRAY,
C          <NOSETS> IS THE TOTAL NUMBER OF SETS OF BARS IN THE VALUES ARRAY.
C
C
      REAL    BAUTO(2),VAUTO(2),VALSTS(NOVALS,NOSETS),RDATA(1)
      INTEGER IDATA(1)
C
      COMMON /T0BRTY/ IBRTYP
      COMMON /T0DBND/ IDRBND
      COMMON /T0KLST/ LSTCL0(100),LENLST
      COMMON /T0MULF/ INFLGS(100)
      COMMON /T0PPOS/ XPLOT0,YPLOT0
      COMMON /T0TRAC/ IPRINT
      COMMON /T0TRAR/ RTRAC1,RTRAC2,RTRAC3,RTRAC4
      COMMON /T3LIMS/ IMAXI,RMAXI,RMINI
C
      DATA RDATA /0.0/
C
C
      RTRAC1= XORIG
      RTRAC2= YORIG
      RTRAC3= BARWID
      IF (IPRINT.EQ.1) CALL G0MESG(160,3)
C
      BORIG= XORIG
      BASPOS= YORIG
      IF (IBRTYP.EQ.0) GO TO 1
C
      BORIG= YORIG
      BASPOS= XORIG
C
C          AUTOMATIC MAPPING IS DONE FOR THE GIVEN VALUES.
C          THE CURRENT PLOTTING POSITION IS SAVED, THE
C          HISTOGRAM IS DRAWN, THEN THE POSITION IS RESTORED.
C
    1 NUMBR= ISTOP-ISTART+1
      IF (NUMBR.LT.1) RETURN
C
      BAUTO(1)= BORIG
      BAUTO(2)= BORIG+NUMBR*BARWID
      VAUTO(1)= BASPOS
      VAUTO(2)= BASPOS
C
      DO 100 ISTCNT= 1,NOSETS
        IF (ISTCNT.GT.100) GO TO 2
        IF (INFLGS(ISTCNT).EQ.0) GO TO 100
C
    2   DO 200 IBRCNT= ISTART,ISTOP
          VAL=VALSTS(IBRCNT,ISTCNT)
          IF (VAL.LT.VAUTO(1)) VAUTO(1)= VAL
          IF (VAL.GT.VAUTO(2)) VAUTO(2)= VAL
  200   CONTINUE
  100 CONTINUE
C
      IF (ABS(BAUTO(2)-BAUTO(1)).LT.RMINI.OR.
     &    ABS(VAUTO(2)-VAUTO(1)).LT.RMINI) RETURN
      IF (IBRTYP.EQ.0) CALL G0AUTO(BAUTO,VAUTO,1,2,1,2,1)
      IF (IBRTYP.NE.0) CALL G0AUTO(VAUTO,BAUTO,1,2,1,2,1)
C
      XHERE= XPLOT0
      YHERE= YPLOT0
      IF (LENLST.LE.0) GO TO 10
C
      BRPOS= BORIG
C
      DO 300 IBAR= ISTART,ISTOP
        BRPOS1=BRPOS+BARWID
C
        DO 400 IEASET= 1,NOSETS
          IF (IEASET.GT.100) GO TO 3
          IF (INFLGS(IEASET).EQ.0) GO TO 400
C
    3     KOLIND= MOD(IEASET,LENLST)
          IF (KOLIND.EQ.0) KOLIND= LENLST
          IF (LSTCL0(KOLIND).EQ.0) GO TO 400
C
          IDATA(1)= 0
          IF (LSTCL0(KOLIND).LT.0) IDATA(1)= 1
C
          CALL G3LINK(5,13,-1,IDATA,RDATA)
          IDATA(1)= IABS(LSTCL0(KOLIND))
          CALL G3LINK(5,3,-1,IDATA,RDATA)
          CURVAL= VALSTS(IBAR,IEASET)
          FROM= BASPOS
          IF (CURVAL.LT.BASPOS) GO TO 5
C
          DO 500 ISTCNT= 1,NOSETS
            IF (ISTCNT.GT.100) GO TO 4
            IF (INFLGS(ISTCNT).EQ.0) GO TO 500
C
    4       VAL= VALSTS(IBAR,ISTCNT)
            IF (VAL.LT.CURVAL.AND.VAL.GT.FROM) FROM= VAL
  500     CONTINUE
C
          GO TO 7
C
    5     DO 600 ISTCNT= 1,NOSETS
            IF (ISTCNT.GT.100) GO TO 6
            IF (INFLGS(ISTCNT).EQ.0) GO TO 600
C
    6       VAL= VALSTS(IBAR,ISTCNT)
            IF (VAL.GT.CURVAL.AND.VAL.LT.FROM) FROM= VAL
  600     CONTINUE
C
    7     IF (IBRTYP.NE.0) GO TO 8
C
          CALL POSITN(BRPOS,FROM)
          CALL JOIN(BRPOS,CURVAL)
          CALL JOIN(BRPOS1,CURVAL)
          CALL JOIN(BRPOS1,FROM)
          CALL JOIN(BRPOS,FROM)
          GO TO 9
C
    8     CALL POSITN(FROM,BRPOS)
          CALL JOIN(CURVAL,BRPOS)
          CALL JOIN(CURVAL,BRPOS1)
          CALL JOIN(FROM,BRPOS1)
          CALL JOIN(FROM,BRPOS)
    9     CALL G3LINK(5,4,0,IDATA,RDATA)
  400   CONTINUE
C
        BRPOS= BRPOS1
  300 CONTINUE
C
      IF (IDRBND.EQ.0) GO TO 14
C
   10 BRPOS1= BORIG
C
      DO 700 IBAR= ISTART,ISTOP
        RLOWST= BASPOS
        HIGST= BASPOS
        BRPOS=  BRPOS1
        BRPOS1= BRPOS+BARWID
C
        DO 800 ISTCNT= 1,NOSETS
C
C       DO FOR EACH VALUE IN BAR
C
          IF (ISTCNT.GT.100) GO TO 11
          IF (INFLGS(ISTCNT).EQ.0) GO TO 800
C
   11     CURVAL= VALSTS(IBAR,ISTCNT)
          IF (CURVAL.LT.RLOWST) RLOWST= CURVAL
          IF (CURVAL.GT.HIGST) HIGST= CURVAL
          IF (IBRTYP.NE.0) GO TO 12
C
          CALL POSITN(BRPOS,CURVAL)
          CALL JOIN(BRPOS1,CURVAL)
          GOTO 800
C
   12     CALL POSITN(CURVAL,BRPOS)
          CALL JOIN(CURVAL,BRPOS1)
  800   CONTINUE
C
        IF (IBRTYP.NE.0) GO TO 13
C
        CALL POSITN(BRPOS,RLOWST)
        CALL JOIN(BRPOS,HIGST)
        CALL POSITN(BRPOS1,HIGST)
        CALL JOIN(BRPOS1,RLOWST)
        GO TO 700
C
   13   CALL POSITN(RLOWST,BRPOS)
        CALL JOIN(HIGST,BRPOS)
        CALL POSITN(HIGST,BRPOS1)
        CALL JOIN(RLOWST,BRPOS1)
  700 CONTINUE
C
C     DRAW BASELINE
C
      IF (IBRTYP.EQ.0) CALL POSITN(BRPOS1,BASPOS)
      IF (IBRTYP.NE.0) CALL POSITN(BASPOS,BRPOS1)
C
      CALL JOIN(XORIG,YORIG)
   14 CALL POSITN(XHERE,YHERE)
      RETURN
      END
