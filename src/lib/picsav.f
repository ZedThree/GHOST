      SUBROUTINE PICSAV(NMFILE,NMPICT)
C
C          ------------------------------------------------
C          ROUTINE NO. ( 310)   VERSION (A8.8)    28:JAN:93
C                         === FORTRAN-77 ===
C          ------------------------------------------------
C
C          THIS TAKES THE PICTURE IN THE SCRATCH GRIDFILE
C          AND SAVES IT INTO THE GIVEN PICTURE AND GRIDFILE.
C
C
      REAL      RDATA(1)
      INTEGER   NEWFIL(32),NEWPIC(4),IDATA(1)
      LOGICAL   BLANK
      LOGICAL   SCRTCH,ERRON
      CHARACTER NMFILE*(*),NMPICT*(*)
C
      COMMON /T2SAVE/ SCRTCH
      COMMON /T3ERRS/ ERRON,NUMERR
      COMMON /T3FILL/ LENDEF,LENSCR
      COMMON /T3FILN/ NAMDEF(32),NAMSCR(32)
      COMMON /T3MACH/ NMCHI,NBITMC
      COMMON /T3SPAC/ ISPACE(1)
C
      DATA RDATA /0.0/, IDATA /0/
C
C
C          IF SCRATCH MODE IS NOT SET, THIS ROUTINE DOES NOTHING.
C          OTHERWISE, THE GIVEN NAMES ARE COPIED INTO HOLD AREAS.
C
      IF (.NOT.SCRTCH) RETURN
C
      LFILEN= LEN(NMFILE)
      IF (LFILEN.LE.128) GO TO 1
      LFILEN= 128
      NUMERR= 19
      IF (ERRON) CALL G0ERMS
C
    1 LPICTN= LEN(NMPICT)
      IF (LPICTN.LE.16) GO TO 2
      LPICTN= 16
      NUMERR= 19
      IF (ERRON) CALL G0ERMS
C
    2 LIMIT= NMCHI*32
      BLANK= .TRUE.
      ISPAC= 0
      CALL G4GETK(ISPACE,NMCHI,NBITMC,NMCHI,ISPAC)
C
      DO 100 ISET= 1,LIMIT
        NCHAR= ISPAC
        IF (ISET.LE.LFILEN) NCHAR= ICHAR(NMFILE(ISET:ISET))
        IF (NCHAR.NE.ISPAC) BLANK= .FALSE.
        CALL G4PUTK(NEWFIL,ISET,NBITMC,NMCHI,NCHAR)
  100 CONTINUE
C
      IF (.NOT.BLANK) GO TO 3
C
      DO 200 ISET= 1,32
        NEWFIL(ISET)= NAMDEF(ISET)
  200 CONTINUE
C
      LFILEN= LENDEF
C
    3 LIMIT= NMCHI*4
      BLANK= .TRUE.
C
      DO 300 ISET= 1,LIMIT
        NCHAR= ISPAC
        IF (ISET.LE.LPICTN) NCHAR= ICHAR(NMPICT(ISET:ISET))
        IF (NCHAR.NE.ISPAC) BLANK= .FALSE.
        CALL G4PUTK(NEWPIC,ISET,NBITMC,NMCHI,NCHAR)
  300 CONTINUE
C
      IF (BLANK) LPICTN= 0
C
C          <G3FLSV> IS THEN USED TO SAVE THE PICTURE.
C
      CALL G3LINK(3,10,0,IDATA,RDATA)
      CALL G3LINK(1,5,0,IDATA,RDATA)
      CALL G3FLSV(NEWFIL,NEWPIC,LFILEN,LPICTN)
C
      RETURN
      END
