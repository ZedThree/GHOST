      SUBROUTINE G0CURV(XPT,YPT,LIMLO,LIMHI,IOPEN)
C
C          ------------------------------------------------
C          ROUTINE NO. ( 104)   VERSION (A7.5)    11:FEB:85
C          ------------------------------------------------
C
C          THIS DRAWS AN OPEN OR CLOSED CURVE
C          THROUGH A GIVEN SET OF DATA POINTS.
C
C
C          THE ARGUMENTS ARE:
C
C          [XPT]    ARE THE X-COORDINATES AND
C          [YPT]    ARE THE Y-COORDINATES OF THE DATA POINTS,
C          <LIMLO>  IS THE LOWER LIMIT, AND
C          <LIMHI>  IS THE UPPER LIMIT OF THE POINTS IN THE ARRAYS.
C          <IOPEN>  IS SET TO 0 FOR OPEN AND 1 FOR CLOSED CURVES.
C
C
      REAL    XPT(LIMHI),YPT(LIMHI),RDATA(256)
      INTEGER IDATA(1)
C
      COMMON /T0CURV/ MCURV0
      COMMON /T3NBYR/ NBYTR
C
C
      IF (LIMLO.GE.LIMHI) RETURN
C
      IDATA(1)= IOPEN
      CALL G3LINK(0,10,-1,IDATA,RDATA)
      IDATA(1)= MCURV0
      CALL G3LINK(0,11,-1,IDATA,RDATA)
C
      INDEX= 0
      DO 100 IPT= LIMLO,LIMHI
        INDEX= INDEX+2
        IF (INDEX.LT.255/NBYTR) GO TO 1
        INDEX= (INDEX-2)*NBYTR
        CALL G3LINK(0,12,INDEX,IDATA,RDATA)
        INDEX= 2
    1   RDATA(INDEX-1)= XPT(IPT)
        RDATA(INDEX)=   YPT(IPT)
  100 CONTINUE
      INDEX= INDEX*NBYTR
      CALL G3LINK(0,13,INDEX,IDATA,RDATA)
C
      RETURN
      END
