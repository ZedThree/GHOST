      SUBROUTINE YSCALI(DIY)
C
C          ------------------------------------------------
C          ROUTINE NO. ( 210)   VERSION (A7.6)    11:FEB:85
C          ------------------------------------------------
C
C          THIS DRAWS A Y-AXIS WITH (OPTIONAL) ANNOTATION
C          ON THE LEFT WINDOW EDGE, AND CORRESPONDING TICK
C          MARKS (ONLY) ON THE RIGHT WINDOW EDGE, WITH THE
C          MARKING INTERVALS SET BY <DIY>.
C
C
C          [Y-LINEAR MARKING].
C
C
      REAL    RDATA(1)
      INTEGER IDATA(1)
C
      COMMON /T0APOS/ AXPOSX,AXPOSY
      COMMON /T0CSPA/ X1CHR0,X2CHR0,Y1CHR0,Y2CHR0
      COMMON /T0NOTA/ NOTATA
      COMMON /T0PPOS/ XPLOT0,YPLOT0
      COMMON /T0TRAC/ IPRINT
      COMMON /T0TRAR/ RTRAC1,RTRAC2,RTRAC3,RTRAC4
      COMMON /T0WNDO/ X1WND0,X2WND0,Y1WND0,Y2WND0
      COMMON /T3LIMS/ IMAXI,RMAXI,RMINI
C
      DATA RDATA /0.0/
C
C
      RTRAC1= DIY
      IF (IPRINT.EQ.1) CALL G0MESG(88,1)
C
C          IF THE WINDOW AREA IS WRONG, NOTHING MORE IS
C          DONE. OTHERWISE, THE CURRENT C-SPACE ARGUMENTS
C          ARE SAVED AND C-SPACE IS SWITCHED OFF, THEN
C          THE CURRENT PLOTTING POSITION IS STORED.
C
      IF (ABS(X1WND0-X2WND0).LT.RMINI.OR.
     &    ABS(Y1WND0-Y2WND0).LT.RMINI) RETURN
C
      IPRSAV= IPRINT
      IPRINT= 0
      XCSAV1= X1CHR0
      XCSAV2= X2CHR0
      YCSAV1= Y1CHR0
      YCSAV2= Y2CHR0
      CALL CSPACE(0.0,0.0,0.0,0.0)
C
      XHERE= XPLOT0
      YHERE= YPLOT0
C
C          THE TICK MARK POSITIONS AND END POINTS
C          ARE CALCULATED, THEN THE AXIS IS DRAWN.
C
      IDATA(1)= 1
      CALL G3LINK(0,14,-1,IDATA,RDATA)
C
      AXXSAV= AXPOSX
      AXYSAV= AXPOSY
      AXPOSX= Y1WND0
      AXPOSY= X1WND0
C
      CALL G0DIVS(2,DIY)
      CALL G0TICK
      CALL G0PLAY(1)
C
      NTATSV= NOTATA
      NOTATA= 0
      AXPOSX= Y2WND0
      AXPOSY= X2WND0
C
      CALL G0TICK
      CALL G0PLAY(0)
C
      IDATA(1)= 0
      CALL G3LINK(0,14,-1,IDATA,RDATA)
C
C          FINALLY, THE PREVIOUS CHAR.-SPACE
C          AND PLOTTING POSITION ARE RESTORED.
C
      CALL CSPACE(XCSAV1,XCSAV2,YCSAV1,YCSAV2)
      CALL POSITN(XHERE,YHERE)
      AXPOSX= AXXSAV
      AXPOSY= AXYSAV
      NOTATA= NTATSV
      IPRINT= IPRSAV
C
      RETURN
      END
