      SUBROUTINE PTGRAF(XPOSNS,YPOSNS,ISTART,ISTOP,MKOPEN)
C
C          ------------------------------------------------
C          ROUTINE NO. (  82)   VERSION (A8.6)    14:JUL:88
C          ------------------------------------------------
C
C          THIS JOINS A SET OF POINTS WITH STRAIGHT-LINE
C          SEGMENTS AND PUTS A MARKER CHARACTER AT EACH POINT.
C
C
C          [XPOSNS] ARE THE X-COORDINATES OF THE POINTS.
C          [YPOSNS] ARE THE Y-COORDINATES OF THE POINTS.
C          <ISTART> IS THE LOWER BOUNDARY AND
C          <ISTOP>  IS THE UPPER BOUNDARY OF THE ARRAY.
C          <MKOPEN> SETS THE MARKER CHAR. (IF NON-ZERO)
C                   AND ALSO DETERMINES THE CURVE TYPE:
C                   >= 0, THE CURVE IS OPEN, OR
C                   < 0, THE CURVE IS CLOSED.
C
C
      REAL    RDATA(2),XPOSNS(ISTOP),YPOSNS(ISTOP)
      INTEGER IDATA(1)
C
      COMMON /T0DBND/ IDRBND
      COMMON /T0KFIL/ KOLFL0
      COMMON /T0MRKS/ MARKC0
      COMMON /T0PPOS/ XPLOT0,YPLOT0
      COMMON /T0TRAC/ IPRINT
      COMMON /T0TRAI/ ITRAC1,ITRAC2,ITRAC3,ITRAC4
      COMMON /T3NBYR/ NBYTR
C
      DATA RDATA /0.0,0.0/
C
C
      ITRAC1= ISTART
      ITRAC2= ISTOP
      ITRAC3= MKOPEN
      IF (IPRINT.EQ.1) CALL G0MESG(110,7)
      IF (ISTART.GE.ISTOP) RETURN
C
C          AUTOMATIC MAPPING WILL BE DONE IF NECESSARY.
C
      CALL G0AUTO(XPOSNS,YPOSNS,ISTART,ISTOP,ISTART,ISTOP,0)
      IGRDFN= 4
      IF (MKOPEN.GE.0.OR.KOLFL0.EQ.0) GO TO 1
C
      IDATA(1)= 0
      IF (KOLFL0.LT.0) IDATA(1)= 1
C
      CALL G3LINK(5,13,-1,IDATA,RDATA)
      IDATA(1)= IABS(KOLFL0)
      CALL G3LINK(5,3,-1,IDATA,RDATA)
      CALL POSITN(XPOSNS(ISTART),YPOSNS(ISTART))
      LOLIM= ISTART+1
C
      DO 100 IJOIN= LOLIM,ISTOP
        CALL JOIN(XPOSNS(IJOIN),YPOSNS(IJOIN))
  100 CONTINUE
C
      CALL JOIN(XPOSNS(ISTART),YPOSNS(ISTART))
      CALL G3LINK(5,4,0,IDATA,RDATA)
      IF (IDRBND.EQ.0) IGRDFN= 5
C
    1 CALL POSITN(XPOSNS(ISTART),YPOSNS(ISTART))
      KCHAR= IABS(MKOPEN)
      IF (KCHAR.EQ.0.OR.KCHAR.GT.255) GO TO 2
C
      MARKC0= KCHAR
      IDATA(1)= KCHAR
      CALL G3LINK(2,9,-1,IDATA,RDATA)
C
    2 DO 200 IJOIN= ISTART,ISTOP
        RDATA(1)= XPOSNS(IJOIN)
        RDATA(2)= YPOSNS(IJOIN)
        CALL G3LINK(0,IGRDFN,2*NBYTR,IDATA,RDATA)
  200 CONTINUE
C
      IF (MKOPEN.LT.0) GO TO 3
C
      XPLOT0= XPOSNS(ISTOP)
      YPLOT0= YPOSNS(ISTOP)
C
      RETURN
C
    3 RDATA(1)= XPOSNS(ISTART)
      RDATA(2)= YPOSNS(ISTART)
      CALL G3LINK(0,IGRDFN,2*NBYTR,IDATA,RDATA)
      XPLOT0= XPOSNS(ISTART)
      YPLOT0= YPOSNS(ISTART)
C
      RETURN
      END
