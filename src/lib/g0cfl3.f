      SUBROUTINE G0CFL3(X1,X2,X3,Y1,Y2,Y3,V1,V2,V3,CLEVLS,ISTRTL,
     &                  ISTOPL,NQ1,NQ2)
C
C          ------------------------------------------------
C          ROUTINE NO. ( 187)   VERSION (A8.1)    18:MAY:98
C          ------------------------------------------------
C
C          ALL THREE SIDES ARE CROSSED BY CONTOUR LINES,AND LINE '12'
C          IS CROSSED BY ALL LINES.  VERTEX 2 HAS THE LOWEST VERTEX-VALUE.
C          LINE '23' AND LINE '12' HAVE NQ1 INTERNAL QUADRILATERALS.
C          LINE '31' AND LINE '12' HAVE NQ2 INTERNAL QUADRILATERALS.
C
      REAL CLEVLS(ISTOPL)
C
      DATA DUM /0.0/
C
C
C          FIND THE COLOUR AND THE FIRST CONTOUR HEIGHT.
C
      INDCL= 1
C
      DO 100 I= ISTRTL,ISTOPL
        IF (V2.GE.CLEVLS(I)) INDCL= INDCL+1
  100 CONTINUE
C
C          DO FIRST TRIANGLE.
C
      FACTOR= (CLEVLS(INDCL)-V3)/(V3-V2)
      XP1= X3+(X3-X2)*FACTOR
      YP1= Y3+(Y3-Y2)*FACTOR
      FACTOR= (CLEVLS(INDCL)-V1)/(V1-V2)
      XP2= X1+(X1-X2)*FACTOR
      YP2= Y1+(Y1-Y2)*FACTOR
      CALL G0CFL5(INDCL,3,XP1,XP2,X2,DUM,DUM,YP1,YP2,Y2,DUM,DUM)
      INDCL= INDCL+1
C
C          DO NQ1 QUADRILATERALS.
C
      IF (NQ1.LT.1) GO TO 1
C
      DO 200 J= 1,NQ1
        FACTOR= (CLEVLS(INDCL)-V3)/(V3-V2)
        XQ1= X3+(X3-X2)*FACTOR
        YQ1= Y3+(Y3-Y2)*FACTOR
        FACTOR= (CLEVLS(INDCL)-V1)/(V1-V2)
        XQ2= X1+(X1-X2)*FACTOR
        YQ2= Y1+(Y1-Y2)*FACTOR
        CALL G0CFL5(INDCL,4,XP1,XP2,XQ2,XQ1,DUM,YP1,YP2,YQ2,YQ1,DUM)
        INDCL= INDCL+1
        XP1=XQ1
        YP1=YQ1
        XP2=XQ2
        YP2=YQ2
  200 CONTINUE
C
C          DO THE PENTAGON.
C
    1 FACTOR= (CLEVLS(INDCL)-V1)/(V1-V3)
      XQ1= X1+(X1-X3)*FACTOR
      YQ1= Y1+(Y1-Y3)*FACTOR
      FACTOR= (CLEVLS(INDCL)-V1)/(V1-V2)
      XQ2= X1+(X1-X2)*FACTOR
      YQ2= Y1+(Y1-Y2)*FACTOR
      CALL G0CFL5(INDCL,5,XP1,XP2,XQ2,XQ1,X3,YP1,YP2,YQ2,YQ1,Y3)
      INDCL= INDCL+1
      XP1=XQ1
      YP1=YQ1
      XP2=XQ2
      YP2=YQ2
C
C          DO NQ2 QUADRILATERALS.
C
      IF (NQ2.LT.1) GO TO 2
C
      DO 300 I= 1,NQ2
        FACTOR= (CLEVLS(INDCL)-V1)/(V1-V3)
        XQ1= X1+(X1-X3)*FACTOR
        YQ1= Y1+(Y1-Y3)*FACTOR
        FACTOR= (CLEVLS(INDCL)-V1)/(V1-V2)
        XQ2= X1+(X1-X2)*FACTOR
        YQ2= Y1+(Y1-Y2)*FACTOR
        CALL G0CFL5(INDCL,4,XP1,XP2,XQ2,XQ1,DUM,YP1,YP2,YQ2,YQ1,DUM)
        INDCL= INDCL+1
        XP1=XQ1
        YP1=YQ1
        XP2=XQ2
        YP2=YQ2
  300 CONTINUE
C
C          DO THE LAST TRIANGLE.
C
    2 CALL G0CFL5(INDCL,3,XP1,XP2,X1,DUM,DUM,YP1,YP2,Y1,DUM,DUM)
C
      RETURN
      END
