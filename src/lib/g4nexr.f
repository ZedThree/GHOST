      SUBROUTINE G4NEXR(IARRAY,ISTRT,ISTOP,ISIZE,RVALUE,NDIGS,IPOSN)
C
C          ------------------------------------------------
C          ROUTINE NO. ( 279)   VERSION (A7.4)    03:JAN:86
C          ------------------------------------------------
C
C          THIS DECODES THE FIRST CONTIGUOUS SEQUENCE OF DIGITS
C          IN A GIVEN CHARACTER STRING INTO A REAL NUMBER. THE
C          FIRST POSITION AFTER THE DIGIT SEQUENCE IS ALSO FOUND.
C
C
C          THE INPUT ARGUMENTS ARE AS FOLLOWS:
C
C          [IARRAY] HOLDS THE CHARACTER STRING (GHOST CHARS.),
C          <ISTRT>  IS THE STARTING POINT OF THE STRING,
C          <ISTOP>  IS THE STOPPING POINT OF THE STRING,
C          <ISIZE>  IS THE TOTAL SIZE OF THE ARRAY.
C
C
C          AND THE OUTPUT ARGUMENTS ARE AS FOLLOWS:
C
C          <RVALUE> IS THE VALUE FOUND FROM THE STRING,
C          <NDIGS>  IS THE NUMBER OF CONSECUTIVE DIGITS FOUND,
C          <IPOSN>  IS THE NEXT POSITION AFTER THE DIGIT SEQUENCE
C                   (AND IS SET TO ZERO IF NO CHARACTERS REMAIN).
C
C
      INTEGER IARRAY(ISIZE)
C
      DATA KPOINT /46/
C
C
      RVALUE= 0.0
      IPOSN= 0
      NDIGS= 0
      IF (ISTOP.LT.ISTRT) RETURN
C
C          THE PART OF THE NUMBER BEFORE THE DECIMAL POINT IS
C          FIRST DECODED. IF THERE ARE NO MORE DIGITS BEFORE
C          THE FIRST SEPARATOR WHICH IS NOT A SPACE OR POINT,
C          THE NUMBER IS COMPLETE; OTHERWISE THE STRING OF
C          DIGITS AFTER THE POINT IS DECODED AND ADDED IN. IF
C          A POINT PRECEDES THE DIGITS, THIS PART IS IGNORED.
C
      CALL G4NEXI(IARRAY,ISTRT,ISTOP,ISIZE,IVALUE,LEN,IPOS1)
      ISIGN=1
      IF (IVALUE.GE.0) GO TO 1
C
      ISIGN= -1
      IVALUE= IVALUE+1
    1 IF (IPOS1.LE.0) RETURN
C
      IPOSN= IPOS1-LEN-1
      IF (IARRAY(IPOSN).EQ.KPOINT) GO TO 2
C
      RVALUE= IVALUE
      NDIGS= LEN
      IPOSN= IPOS1
      IF (IPOSN.GE.ISTOP) RETURN
      IF (IARRAY(IPOSN).NE.KPOINT) RETURN
C
    2 IPOSN= IPOSN+1
      CALL G4NEXC(IARRAY,IPOSN,ISTOP,ISIZE,+2,IPOS1)
      IF (IPOS1.LE.0) RETURN
C
      CALL G4NEXC(IARRAY,IPOSN,ISTOP,ISIZE,-4,IPOS2)
      IF (IPOS2.NE.IPOS1) RETURN
C
      CALL G4NEXI(IARRAY,IPOS1,ISTOP,ISIZE,IVALUE,LEN,IPOS2)
      IPOSN= IPOS2
      NDIGS= NDIGS+LEN
      RVALUE= RVALUE+ISIGN*IVALUE*(10.0**(-LEN))
C
      RETURN
      END
