      SUBROUTINE G0TICK
C
C          ------------------------------------------------
C          ROUTINE NO. ( 232)   VERSION (A7.3)    11:FEB:85
C          ------------------------------------------------
C
C          GIVEN THE AXIS AND ITS POSITION, THIS CALCULATES THE
C          END-POSITIONS OF THE TICK MARKS SUCH THAT THEY ALWAYS
C          APPEAR TO BE 8/1000 AND 5/1000 OF THE CURRENT WINDOW SIZE,
C          NO MATTER WHICH KIND OF MAPPING IS CURRENTLY BEING USED.
C
C
C          THE FOLLOWING ARGUMENTS ARE SUPPLIED THROUGH COMMON:
C
C          <KAXIS>  GIVES THE CURRENT AXIS BY ITS MODULUS:
C                   = 1 FOR THE X-AXIS, AND
C                   = 2 FOR THE Y-AXIS.
C          <AXPOSX> IS THE POSITION OF THE X-AXIS ALONG Y.
C          <AXPOSY> IS THE POSITION OF THE Y-AXIS ALONG X.
C
C
C          THE FOLLOWING ARGUMENTS ARE RETURNED THROUGH COMMON:
C
C          <TKEN1A> AND
C          <TKEN1B> ARE THE END-POINTS OF THE 5/1000TH.-SIZE TICK MARK.
C          <TKEN2A> AND
C          <TKEN2B> ARE THE END-POINTS OF THE 8/1000TH.-SIZE TICK MARK.
C
C
      COMMON /T0AARG/ KAXIS
      COMMON /T0APOS/ AXPOSX,AXPOSY
      COMMON /T0ATIK/ TKEN1A,TKEN1B,TKEN2A,TKEN2B
      COMMON /T0MAPT/ MAPNO0
      COMMON /T0WNDO/ X1WND0,X2WND0,Y1WND0,Y2WND0
C
      ENDTKX(DY,Y1WND0,Y2WND0)= AXPOSX*((Y1WND0/Y2WND0)**DY)
      ENDTKY(DX,X1WND0,X2WND0)= AXPOSY*((X1WND0/X2WND0)**DX)
C
C
      IF (IABS(KAXIS).NE.1) GO TO 2
C
C          THIS SECTION FINDS THE X-AXIS TICK MARKS
C          FOR EITHER LINEAR OR LOGARITHMIC Y-MAPPING.
C
      IF (MAPNO0.EQ.2.OR.MAPNO0.EQ.4) GO TO 1
C
      TIKL= 0.005*(Y2WND0-Y1WND0)
      TKEN1A= AXPOSX+TIKL
      TKEN1B= AXPOSX-TIKL
      TKEN2A= AXPOSX+1.6*TIKL
      TKEN2B= AXPOSX-1.6*TIKL
      RETURN
C
    1 TKEN1A= ENDTKX(0.005,Y2WND0,Y1WND0)
      TKEN1B= ENDTKX(0.005,Y1WND0,Y2WND0)
      TKEN2A= ENDTKX(0.008,Y2WND0,Y1WND0)
      TKEN2B= ENDTKX(0.008,Y1WND0,Y2WND0)
      RETURN
C
C          THIS SECTION FINDS THE Y-AXIS TICK MARKS
C          FOR EITHER LINEAR OR LOGARITHMIC X-MAPPING.
C
    2 IF (MAPNO0.EQ.3.OR.MAPNO0.EQ.4) GO TO 3
C
      TIKL= 0.005*(X2WND0-X1WND0)
      TKEN1A= AXPOSY+TIKL
      TKEN1B= AXPOSY-TIKL
      TKEN2A= AXPOSY+1.6*TIKL
      TKEN2B= AXPOSY-1.6*TIKL
      RETURN
C
    3 TKEN1A= ENDTKY(0.005,X2WND0,X1WND0)
      TKEN1B= ENDTKY(0.005,X1WND0,X2WND0)
      TKEN2A= ENDTKY(0.008,X2WND0,X1WND0)
      TKEN2B= ENDTKY(0.008,X1WND0,X2WND0)
      RETURN
      END
