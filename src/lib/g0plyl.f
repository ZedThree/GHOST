      SUBROUTINE G0PLYL(LINE)
C
C          ------------------------------------------------
C          ROUTINE NO. ( 238)   VERSION (A8.7)    11:JAN:88
C          ------------------------------------------------
C
C          THIS DRAWS TICK MARKS AND (OPTIONALLY) AN AXIS LINE,
C          WITH LOGARITHMIC MARKINGS ALONG THE Y-DIRECTION.
C
C          <LINE>   CONTROLS WHETHER THE AXIS LINE IS DRAWN:
C                   IF ZERO, ONLY TICK MARKS ARE DRAWN,
C                   OTHERWISE, THE AXIS LINE IS ALSO DONE.
C
C
C          THE FOLLOWING ARGUMENTS ARE SUPPLIED THROUGH COMMON:
C
C          <NOTATA> IF ZERO, NO ANNOTATION IS REQUIRED,
C                   IF NON-ZERO, ANNOTATION IS DONE.
C          <AXPOSY> THE POSITION OF THE Y-AXIS ALONG X.
C          <DIVLY>  GIVES THE Y-AXIS SIGN.
C          <NSKIPY> IS THE Y-AXIS START-POINT EXPONENT.
C          <NTIKLY> IS THE Y-AXIS START-POINT MANTISSA.
C          <NTIKHY> IS THE NO. OF Y-AXIS TICK MARKS.
C          <NDECSY> IS THE NO. OF DECADE MARKS ON THE Y-AXIS.
C          <NCHRSY> IS THE NO. OF CHARS. IN Y-AXIS ANNOTATION.
C          <KAXIS>  INDICATES WHETHER ANNOTATION MAY BE SUPPRESSED:
C                   > 0, NO, OR
C                   < 0, YES, IF IT INTERFERES WITH THE OTHER AXIS.
C          <TKEN1A> AND
C          <TKEN1B> ARE THE END-POINTS OF THE 5/1000TH.-SIZE TICK MARK.
C          <TKEN2A> AND
C          <TKEN2B> ARE THE END-POINTS OF THE 8/1000TH.-SIZE TICK MARK.
C
C
      REAL    RDATA(1)
      INTEGER IDECFC(3)
C
      COMMON /T0AARG/ KAXIS
      COMMON /T0ADIY/ DIVLY,NTIKLY,NTIKHY
      COMMON /T0ANOD/ KDIRX,KDIRY
      COMMON /T0ANOY/ KANNY,NCHRSY,NAFTPY
      COMMON /T0APOS/ AXPOSX,AXPOSY
      COMMON /T0ASKY/ NSKIPY,NDECSY
      COMMON /T0ATIK/ TKEN1A,TKEN1B,TKEN2A,TKEN2B
      COMMON /T0NOTA/ NOTATA
      COMMON /T0WNDO/ X1WND0,X2WND0,Y1WND0,Y2WND0
      COMMON /T3LIMS/ IMAXI,RMAXI,RMINI
C
      DATA RDATA /0.0/, IDECFC /120,49,48/
C
C
C          THIS PART DRAWS THE AXIS, IF REQUIRED.
C
      IF (LINE.EQ.0) GO TO 1
C
      CALL POSITN(AXPOSY,Y1WND0)
      CALL   JOIN(AXPOSY,Y2WND0)
C
C          IF THERE ARE NO TICK MARKS, NO MORE NEED BE DONE.
C          OTHERWISE, THE CURRENT FONT IS SAVED, THE START POINT
C          IS FOUND, THEN LOOP-100 DRAWS EACH TICK MARK (OF THE
C          APPROPRIATE SIZE) IN TURN, ANNOTATING IT AS REQUIRED.
C
    1 IF (NTIKHY.LE.0) RETURN
C
      IPOS= NTIKLY-1
      IDEC= NSKIPY
      MULTPT= 3
      IF (IPOS.GT.2.AND.IPOS.LT.8) MULTPT= IPOS+1
C
      DO 100 ITICK= 1,NTIKHY
        TKENDA= TKEN1A
        TKENDB= TKEN1B
        IPOS= IPOS+1
        IF (IPOS.LT.10) GO TO 2
C
        IPOS= 1
        IDEC= IDEC+1
        TKENDA= TKEN2A
        TKENDB= TKEN2B
C
C          THE APPROPRIATE-SIZE TICK MARK IS DRAWN. IF ANNOTATION
C          IS NOT REQUIRED, OR IF THIS POSITION IS NOT ON A DECADE
C          INTERVAL WHEN THERE ARE A LARGE NUMBER OF LOG. CYCLES
C          PRESENT, LOOP-100 CONTINUES WITH THE NEXT TICK MARK.
C
    2   YPOS= DIVLY*IPOS*(10.0**(IDEC))
        CALL POSITN(TKENDA,YPOS)
        CALL   JOIN(TKENDB,YPOS)
        IF (NOTATA.EQ.0) GO TO 100
C
C          THIS SECTION SUPPRESSES ANNOTATION WHEN THIS
C          COINCIDES WITH THE OTHER AXIS, IF REQUIRED.
C
        IF (KAXIS.GT.0) GO TO 3
        IF (ABS(AXPOSY-X1WND0).LT.RMINI.AND.KDIRX.LE.0) GO TO 3
        IF (ABS(AXPOSY-X2WND0).LT.RMINI.AND.KDIRX.GE.1) GO TO 3
C
        YDIFF= ALOG(YPOS/AXPOSX)
        IF (ABS(YDIFF).LT.0.04*ALOG(Y2WND0/Y1WND0)) GO TO 6
C
C          WHEN ANNOTATION IS TO BE DONE, THE POSITION IS SET,
C          THE APPROPRIATE FORMAT SELECTED, AND THE VALUE PRINTED.
C          IF THERE ARE MORE THAN 10 TICK MARKS, INTEGER+EXPONENT
C          FORMAT IS USED; OTHERWISE NORMAL REAL OR INTEGER FORMAT
C          IS USED, PROVIDED THE VALUES ARE NOT TOO LARGE OR SMALL.
C
    3   CALL POSITN(AXPOSY,YPOS)
        IF (NTIKHY.GT.10) GO TO 5
        IF (NSKIPY.LT.-2.OR.NSKIPY.GT.3) GO TO 5
C
C          THESE SECTIONS PROVIDE NORMAL FORMAT (INTEGER OR REAL).
C
        IF (NSKIPY.LT.0) GO TO 4
        IF (KDIRY.LE.0) CALL SPACE(-NCHRSY-1)
        IF (KDIRY.GE.1) CALL SPACE(2)
C
        IVALUE= IPOS*(10**IDEC)
        IF (DIVLY.LT.0.0) IVALUE= -IVALUE
C
        CALL TYPENI(IVALUE)
        GO TO 100
C
    4   IF (KDIRY.LE.0) CALL SPACE(-NCHRSY)
        IF (KDIRY.GE.1) CALL SPACE(1)
C
        CALL TYPENF(YPOS,-NSKIPY)
        GO TO 100
C
C          THESE SECTIONS PROVIDE INTEGER-EXPONENT FORM.
C
    5   IF (KDIRY.LE.0) CALL SPACE(-3)
        IF (KDIRY.GE.1) CALL SPACE(1)
C
        IVALUE= IPOS
        IF (DIVLY.LT.0.0) IVALUE= -IVALUE
        IF (NTIKHY.LE.37.OR.IPOS.EQ.1) CALL TYPENI(IVALUE)
 
C
C          THIS PART PROVIDES THE DECADE MULTIPLYING FACTOR.
C
        IF (IPOS.NE.MULTPT) GO TO 100
C
        MULTPT= 3
        IF (KDIRY.LE.0) CALL SPACE(-6)
        IF (KDIRY.GE.1) CALL SPACE(1)
C
        CALL G3LINK(2,11,-3,IDECFC,RDATA)
        CALL SUPFIX
        IF (IDEC.GE.0) CALL SPACE(-1)
C
        CALL TYPENI(IDEC)
        CALL NORMAL
        GO TO 100
C
    6   IF (MULTPT.EQ.IPOS) MULTPT= 3
  100 CONTINUE
C
      RETURN
      END
