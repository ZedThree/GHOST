      SUBROUTINE G0INSD(XPOINT,YPOINT,IFLAG)
C
C          ------------------------------------------------
C          ROUTINE NO. (  46)   VERSION (A8.1)    14:MAY:87
C          ------------------------------------------------
C
C          THIS ROUTINE FINDS IF A POINT LIES OUTSIDE,
C          BEHIND OR ON GIVEN BAR FOR THE THREE DIMENSIONAL
C          BARCHART ROUTINE.
C
C          THE PARAMETERS ARE :
C
C          <XPOINT> ARE THE COORDINATES OF THE
C          <YPOINT> POINT BEING CHECKED.
C          <IFLAG>  VALUE RETURNED :
C                                0 : INSIDE,
C                                1 : OUTSIDE,
C                                2 : ON EDGE.
C
C          THE OTHER VALUES USED ARE :
C
C          [XCHLIN] ARRAYS CONTAINING THE COORDINATES
C          [YCHLIN] OF THE BAR.
C
C
      COMMON /T0CHLN/ XEALIN(22),YEALIN(22),XCHLIN(6),YCHLIN(6),NUMLNS
      COMMON /T0INTS/ XLINE1(2),YLINE1(2),XLINE2(2),YLINE2(2)
      COMMON /T3LIMS/ IMAXI,RMAXI,RMINI
C
C          DOES POINT LIE OUTSIDE BAR ?
C
      IFLAG= 1
      IF (XPOINT.GT.XCHLIN(2).OR.XPOINT.LT.XCHLIN(5).OR.
     &    YPOINT.GT.YCHLIN(3).OR.YPOINT.LT.YCHLIN(1)) RETURN
C
C          IS POINT IN SLOPING AREA OF BAR ?
C
      IF (XPOINT.LT.XCHLIN(4).AND.YPOINT.GT.YCHLIN(5)) GO TO 2
      IF (XPOINT.GT.XCHLIN(1).AND.YPOINT.LT.YCHLIN(2)) GO TO 1
C
C          DOES POINT LIE ON BAR ?
C
      IFLAG= 2
      IF (ABS(XPOINT-XCHLIN(2)).LT.RMINI) RETURN
      IF (ABS(XPOINT-XCHLIN(5)).LT.RMINI) RETURN
      IF (ABS(YPOINT-YCHLIN(1)).LT.RMINI) RETURN
      IF (ABS(YPOINT-YCHLIN(3)).LT.RMINI) RETURN
      GO TO 4
C
C          POINT IN BOTTOM RIGHT AREA.
C
    1 IDIS= 1
      GO TO 3
C
C          POINT IN TOP LEFT AREA.
C
    2 IDIS= 4
    3 XLINE1(1)= XPOINT
      YLINE1(1)= YCHLIN(IDIS)
      XLINE1(2)= XPOINT
      YLINE1(2)= YCHLIN(IDIS+1)
      XLINE2(1)= XCHLIN(IDIS)
      YLINE2(1)= YCHLIN(IDIS)
      XLINE2(2)= XCHLIN(IDIS+1)
      YLINE2(2)= YCHLIN(IDIS+1)
      CALL G0INTR(ICODE,XINT,YINT)
      IF (IDIS.EQ.1.AND.YPOINT.LT.YINT) RETURN
      IF (IDIS.EQ.4.AND.YPOINT.GT.YINT) RETURN
C
      IFLAG= 2
      IF (ABS(YPOINT-YINT).LT.RMINI) RETURN
C
C          POINT LIES INSIDE BAR.
C
    4 IFLAG= 0
      RETURN
      END
