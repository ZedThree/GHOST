      SUBROUTINE G1CHAR(NCHAR)
C
C          ------------------------------------------------
C          ROUTINE NO. (1032)   VERSION (A8.7)    14:JUL:97
C          ------------------------------------------------
C
C          THIS DRAWS OUT THE CHARACTER NO. <NCHAR>.
C
C
      INTEGER IXPTS(30),IYPTS(30),IXUNDL(7),IYUNDL(7)
      LOGICAL DRAW,DRAWN
      LOGICAL TYPMOD,KHRDW1,OPEN,CURTRA
C
      COMMON /T1CATT/ IUNDL1,ITAL1
      COMMON /T1CDCC/ SINCHR,COSCHR
      COMMON /T1CDIM/ MAGN1,OBLAT1
      COMMON /T1CFIN/ INDLEN(2,256),WIDCHR(256)
      COMMON /T1CFON/ KFONT1
      COMMON /T1CMOD/ TYPMOD
      COMMON /T1CPOS/ XCHAR,YCHAR
      COMMON /T1DENA/ OPEN
      COMMON /T1DLIM/ DLIMX,DLIMY
      COMMON /T1DRES/ DRESX,DRESY
      COMMON /T1HRDC/ KHRDW1
      COMMON /T1HRDM/ MAPCHR(256)
      COMMON /T1LIND/ PATREM
      COMMON /T1PPAT/ XPLOTA,YPLOTA
      COMMON /T1PPBT/ XPLOTB,YPLOTB
      COMMON /T1SPEC/ XSPEC(4600),YSPEC(4600),ICHPTR(128),NEXTPT,NSTORE
      COMMON /T1TCOB/ XCBX,XCBY,YCBX,YCBY,RPICXB,RPICYB,RDEVXB,RDEVYB
      COMMON /T1TCOC/ XCCX,XCCY,YCCX,YCCY,RPICXC,RPICYC,RDEVXC,RDEVYC,
     &                CURTRA
      COMMON /T1TSPA/ X1TYPW,X2TYPW,Y1TYPW,Y2TYPW
C
      DATA IXUNDL /7,7,6,0,7,0,6/, IYUNDL /0,2,1,1,0,0,0/,
     &     CHRSIZ /1.428571E-4/, DOTSIZ /0.0005/
C
C
C          IF NO DEVICE CHANNEL IS ON, NOTHING CAN BE DONE.
C
C          IF TYPING MODE HAS BEEN SET, THE CHAR. IS NOT
C          DRAWN IF IT LIES OUTSIDE THE TYPEWRITER WINDOW.
C
      IF (.NOT.OPEN)                            RETURN
      IF (TYPMOD) THEN
        IF ((XCHAR-X1TYPW)*(XCHAR-X2TYPW).GT.0.0) RETURN
        IF ((YCHAR-Y1TYPW)*(YCHAR-Y2TYPW).GT.0.0) RETURN
      ENDIF
C
C          IF HARDWARE CHARS. ARE BEING DONE, A CHECK IS MADE
C          TO ENSURE THE POSITION LIES WITHIN THE DEVICE WINDOW.
C
      IF (KHRDW1) THEN
C
        XCHARB= XCBX*(XCHAR-RPICXB)+XCBY*(YCHAR-RPICYB)+RDEVXB
        YCHARB= YCBX*(XCHAR-RPICXB)+YCBY*(YCHAR-RPICYB)+RDEVYB
        IF (.NOT.CURTRA) THEN
          XCHARA= XCHARB
          YCHARA= YCHARB
        ELSE
          XCHARA= XCCX*(XCHARB-RPICXC)+XCCY*(YCHARB-RPICYC)+RDEVXC
          YCHARA= YCCX*(XCHARB-RPICXC)+YCCY*(YCHARB-RPICYC)+RDEVYC
        ENDIF
C
        IF (XCHARA.LT.0.0.OR.XCHARA.GT.DLIMX) RETURN
        IF (YCHARA.LT.0.0.OR.YCHARA.GT.DLIMY) RETURN
C
        IXCHAR= DRESX*XCHARA+0.5
        IYCHAR= DRESY*YCHARA+0.5
        CALL G1FILB(IXCHAR,IYCHAR,-2,MAPCHR(NCHAR+1))
        RETURN
C
      ELSE
C
        XSAVEB= XPLOTB
        YSAVEB= YPLOTB
        XSAVEA= XPLOTA
        YSAVEA= YPLOTA
        REMSAV= PATREM
        IF (KFONT1.LT.11) THEN
C
C          FOR SOFTWARE CHARS., FIRST THE SPECIFICATION IS
C          FETCHED. IF UNDERLINE IS REQUIRED, THE NECESSARY
C          EXTRA SPECIFICATION IS APPENDED USING LOOP-100.
C
          CALL G1SPEC(NCHAR,KFONT1,IXPTS,IYPTS,LENGTH)
          IF (IUNDL1.GT.0) THEN
C
            NADD= IUNDL1*3+1
C
            DO 100 IADD= 1,NADD
              IAPPND= LENGTH+IADD
              IXPTS(IAPPND)= IXUNDL(IADD)
              IYPTS(IAPPND)= IYUNDL(IADD)
  100       CONTINUE
C
            LENGTH= LENGTH+NADD
          ENDIF
C
C          IF THE FINAL SPEC. HAS ZERO LENGTH, NOTHING IS DONE.
C
C          LOOP-200 SCANS THROUGH THE COORDINATE PAIRS,
C          DRAWING THE ELEMENTAL LINES OF THE CHARACTER.
C          SPECIAL FUNCTIONS ARE GIVEN BY <IXV>= 7, THE
C          ACTION BEING DETERMINED BY <IYV> AS FOLLOWS:
C
C          = 0, THE NEXT ELEMENTAL LINE IS NOT VISIBLE,
C          = 1, THE Y-OFFSET IS SET TO -3,
C          = 2, THE Y-OFFSET IS SET TO -2,
C          = 3, THE Y-OFFSET IS SET TO -1,
C          = 4, THE Y-OFFSET IS SET TO  0,
C          = 5, THE Y-OFFSET IS SET TO +1,
C          = 6, THE Y-OFFSET IS SET TO +2,
C          = 7, (UNDEFINED).
C
C          A LOZENGE IS DRAWN WHEN TWO SUCCESSIVE VECTORS ARE
C          NOT VISIBLE, OR IF THE LAST VECTOR IS NOT VISIBLE.
C
          IF (LENGTH.LE.0) RETURN
C
          IYOFF= 0
          DRAW= .FALSE.
          DRAWN= .TRUE.
C
          DO 200 IPT= 1,LENGTH
            IXV= IXPTS(IPT)
            IYV= IYPTS(IPT)
            IF (IXV.LE.6) GO TO 1
            IF (IYV.NE.0) THEN
              IYOFF= IYV-4
              GO TO 200
            ENDIF
C
            DRAW= .FALSE.
            IF (.NOT.DRAWN) GO TO 2
            GO TO 200
C
    1       IYV= IYV+IYOFF
            X1= (IXV-3)*MAGN1*CHRSIZ
            Y1= (IYV-2)*MAGN1*CHRSIZ
            X1= (X1+Y1*ITAL1*0.25)*OBLAT1
            XNOW= XCHAR+X1*COSCHR-Y1*SINCHR
            YNOW= YCHAR+X1*SINCHR+Y1*COSCHR
            IF (.NOT.DRAW.AND.IPT.EQ.LENGTH) GO TO 2
C
            CALL G1LINE(XNOW,YNOW,DRAW)
            DRAWN= DRAW
            DRAW= .TRUE.
            GO TO 200
C
    2       CALL G1LINE(XNOW,       YNOW+DOTSIZ,.FALSE.)
            CALL G1LINE(XNOW-DOTSIZ,YNOW,       .TRUE.)
            CALL G1LINE(XNOW,       YNOW-DOTSIZ,.TRUE.)
            CALL G1LINE(XNOW+DOTSIZ,YNOW,       .TRUE.)
            CALL G1LINE(XNOW,       YNOW+DOTSIZ,.TRUE.)
  200     CONTINUE
C
        ELSE
          NCHAR1= NCHAR+1
          IF (KFONT1.LT.101) THEN
C
C          FONTS 11-24 ARE HIGH-QUALITY FONTS.
C
            IF (NCHAR1.GT.128) THEN
              IPOS= 1
              IF (NCHAR1.NE.NSTORE) CALL G1CHIO(2,NCHAR1)
C
              NSTORE= NCHAR1
            ELSE
              IF (ICHPTR(NCHAR1).EQ.0) CALL G1CHIO(2,NCHAR1)
C
              IPOS= ICHPTR(NCHAR1)
            ENDIF
C
            DRAW= .FALSE.
            IP= IPOS-1
C
            DO 400 IPT= 1,INDLEN(2,NCHAR1)
              IP= IP+1
              XC= XSPEC(IP)
              IF(XC.GE.-100.0) THEN
                X1= (XC-3.0)*MAGN1*CHRSIZ*OBLAT1
                Y1= (YSPEC(IP)-2.0)*MAGN1*CHRSIZ
                XNOW= XCHAR+X1*COSCHR-Y1*SINCHR
                YNOW= YCHAR+X1*SINCHR+Y1*COSCHR
                CALL G1LINE(XNOW,YNOW,DRAW)
                DRAW= .TRUE.
              ELSE
                DRAW= .FALSE.
              ENDIF
C
  400       CONTINUE
C
            IF (IUNDL1.GT.0) THEN
C
C          UNDERLINE HIGH QUALITY CHARACTERS.
C
              XPOS= 3.0*WIDCHR(NCHAR1)
              YPOS= -3.0
              DRAW= .FALSE.
C
              DO 500 LIN= 1,IUNDL1*2
                XNOW= XCHAR+(XPOS*COSCHR*OBLAT1-YPOS*SINCHR)*
     &                MAGN1*CHRSIZ
                YNOW= YCHAR+(XPOS*SINCHR*OBLAT1+YPOS*COSCHR)*
     &                MAGN1*CHRSIZ
                CALL G1LINE(XNOW,YNOW,DRAW)
                IF (MOD(LIN,2).EQ.1) XPOS= -XPOS
                IF (LIN.EQ.2) YPOS= YPOS-1.0
C
                DRAW= .NOT.DRAW
  500         CONTINUE
C
            ENDIF
C
          ELSE
C
C          FONTS 101 ONWARDS ARE POSTSCRIPT FONTS.
C
C          A CHECK IS MADE TO ENSURE THE POSITION LIES WITHIN
C          THE DEVICE WINDOW.
C
            X1= -3.0*MAGN1*CHRSIZ*OBLAT1*WIDCHR(NCHAR1)
            Y1= -2.0*MAGN1*CHRSIZ
            XNOW= XCHAR+X1*COSCHR-Y1*SINCHR
            YNOW= YCHAR+X1*SINCHR+Y1*COSCHR
            XCHARB= XCBX*(XNOW-RPICXB)+XCBY*(YNOW-RPICYB)+RDEVXB
            YCHARB= YCBX*(XNOW-RPICXB)+YCBY*(YNOW-RPICYB)+RDEVYB
            IF (.NOT.CURTRA) THEN
              XCHARA= XCHARB
              YCHARA= YCHARB
            ELSE
              XCHARA= XCCX*(XCHARB-RPICXC)+XCCY*(YCHARB-RPICYC)+RDEVXC
              YCHARA= YCCX*(XCHARB-RPICXC)+YCCY*(YCHARB-RPICYC)+RDEVYC
            ENDIF
C
            IF (XCHARA.LT.0.0.OR.XCHARA.GT.DLIMX) RETURN
            IF (YCHARA.LT.0.0.OR.YCHARA.GT.DLIMY) RETURN
C
            IXCHAR= DRESX*XCHARA+0.5
            IYCHAR= DRESY*YCHARA+0.5
            CALL G1FILB(IXCHAR,IYCHAR,-2,NCHAR)
          ENDIF
        ENDIF
C
        XPLOTB= XSAVEB
        YPLOTB= YSAVEB
        XPLOTA= XSAVEA
        YPLOTA= YSAVEA
        PATREM= REMSAV
      ENDIF
C
      RETURN
      END
