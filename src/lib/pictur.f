      SUBROUTINE PICTUR(NMFILE,NMPICT,NUMPIC)
C
C          ------------------------------------------------
C          ROUTINE NO. ( 311)   VERSION (A8.7)    19:MAR:86
C                         === FORTRAN-77 ===
C          ------------------------------------------------
C
C          THIS READS THE PICTURE FROM THE GIVEN GRIDFILE
C          AND INSERTS IT INTO THE CURRENT OUTPUT PICTURE.
C
C
      INTEGER   KFILIN(32),KPICIN(4)
      LOGICAL   BLANK
      LOGICAL   ERRON
      CHARACTER NMFILE*(*),NMPICT*(*)
C
      COMMON /T3ERRS/ ERRON,NUMERR
      COMMON /T3FILL/ LENDEF,LENSCR
      COMMON /T3FILN/ NAMDEF(32),NAMSCR(32)
      COMMON /T3MACH/ NMCHI,NBITMC
      COMMON /T3SPAC/ ISPACE(1)
C
C
      CALL G3INIT(2)
C
C          THE GIVEN NAMES ARE SAVED IN HOLD AREAS. IF
C          <NUMPIC> IS > 0, THE PICTURE NAME IS IGNORED.
C
      LFILEN= LEN(NMFILE)
      IF(LFILEN.LE.128) GO TO 1
C
      LFILEN= 128
      NUMERR= 19
      IF (ERRON) CALL G0ERMS
C
    1 LPICTN= LEN(NMPICT)
      IF (LPICTN.LE.16) GO TO 2
C
      LPICTN= 16
      NUMERR= 19
      IF (ERRON) CALL G0ERMS
C
    2 LIMIT= NMCHI*32
      BLANK= .TRUE.
      ISPAC= 0
      CALL G4GETK(ISPACE,NMCHI,NBITMC,NMCHI,ISPAC)
C
      DO 100 ISET= 1,LIMIT
        NCHAR= ISPAC
        IF (ISET.LE.LFILEN) NCHAR= ICHAR(NMFILE(ISET:ISET))
        IF (NCHAR.NE.ISPAC) BLANK= .FALSE.
C
        CALL G4PUTK(KFILIN,ISET,NBITMC,NMCHI,NCHAR)
  100 CONTINUE
C
      IF (.NOT.BLANK) GO TO 3
C
      DO 200 ISET= 1,32
        KFILIN(ISET)= NAMDEF(ISET)
  200 CONTINUE
C
      LFILEN= LENDEF
    3 NMPIC1= NUMPIC
      IF (NMPIC1.LT.0) NMPIC1= 0
      IF (NMPIC1.GT.0) LPICTN= 0
C
      LIMIT= NMCHI*4
      BLANK= .TRUE.
C
      DO 300 ISET= 1,LIMIT
        NCHAR= ISPAC
        IF (ISET.LE.LPICTN) NCHAR= ICHAR(NMPICT(ISET:ISET))
        IF (NCHAR.NE.ISPAC) BLANK= .FALSE.
C
        CALL G4PUTK(KPICIN,ISET,NBITMC,NMCHI,NCHAR)
  300 CONTINUE
C
      IF (BLANK) LPICTN= 0
      IF (LPICTN.EQ.0.AND.NMPIC1.LE.0) RETURN
C
C          <G3FLIN> IS THEN USED TO RETRIEVE THE PICTURE.
C
      CALL G3FLIN(KFILIN,KPICIN,LFILEN,LPICTN,NMPIC1)
C
      RETURN
      END
