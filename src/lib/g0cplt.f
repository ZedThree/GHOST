      SUBROUTINE G0CPLT(XPLOTB,YPLOTB,INOTAT)
C
C          ------------------------------------------------
C          ROUTINE NO. ( 127)   VERSION (A8.2)    19:FEB:87
C          ------------------------------------------------
C
C          THIS DRAWS STRAIGHT OR CURVED CONTOUR LINES.
C
C
C          THE ARGUMENTS ARE AS FOLLOWS:
C
C          <XPLOTB>  IS THE POSITION X-COORDINATE.
C          <YPLOTB>  IS THE POSITION Y-COORDINATE.
C          <INOTAT>  IS THE ANOTATION INDICATOR.
C
C
      REAL    RDATA(1)
      INTEGER IDATA(1),ICOPY(10),ITEXT(255)
      LOGICAL OPENCO,FRSTPT,LASTPT,CURVED
C
      COMMON /T0CEND/ FRSTPT,LASTPT
      COMMON /T0CLBL/ LLBCON
      COMMON /T0CLEV/ LEVEL,HEIGHT
      COMMON /T0CNLB/ LBLCON(10,50)
      COMMON /T0CNSV/ XPOSNS(100),YPOSNS(100),NCOPTS
      COMMON /T0CTYP/ OPENCO,CURVED
      COMMON /T0NOTC/ NOTATC
      COMMON /T3MACH/ NMCHI,NBITMC
      COMMON /T3SPAC/ ISPACE(1)
C
      DATA RDATA /0.0/
C
C
      IF (CURVED) GO TO 2
C
C          THIS SECTION IS FOR STRAIGHT LINES.
C
      IF (.NOT.FRSTPT) GO TO 1
C
      CALL POSITN(XPLOTB,YPLOTB)
      IF (NOTATC.GT.0) GO TO 5
      RETURN
C
    1 CALL JOIN(XPLOTB,YPLOTB)
      IF (LASTPT.AND.OPENCO.AND.NOTATC.GT.0) GO TO 5
      RETURN
C
C          THIS SECTION IS FOR CURVED LINES.
C
    2 IF (.NOT.FRSTPT) GO TO 3
C
      CALL POSITN(XPLOTB,YPLOTB)
      XPOSNS(1)= XPLOTB
      YPOSNS(1)= YPLOTB
      NCOPTS= 1
      IF (NOTATC.GT.0) GO TO 5
      RETURN
C
    3 IF (NCOPTS.LT.100) GO TO 4
C
      CALL CURVEO(XPOSNS,YPOSNS,1,100)
      XPOSNS(1)= XPOSNS(100)
      YPOSNS(1)= YPOSNS(100)
      NCOPTS= 1
    4 NCOPTS= NCOPTS+1
      XPOSNS(NCOPTS)= XPLOTB
      YPOSNS(NCOPTS)= YPLOTB
      IF (.NOT.LASTPT) RETURN
C
      CALL CURVEO(XPOSNS,YPOSNS,1,NCOPTS)
      IF (.NOT.OPENCO.OR.NOTATC.LE.0) RETURN
C
C          THIS SECTION WRITES THE ANNOTATION ON THE CONTOURS.
C          ANNOTATION IS ONLY DONE ON THE CURRENT CONTOUR
C          IF THE VALUE OF <NOTDEV> IS ODD.
C
C          THE VALUE OF INOTAT INDICATES THE BORDER BEING INTERSECTED.
C          IT TAKES THE FOLLOWING VALUES:
C          0  INTERNAL CONTOURS
C          1  THE LEFT EDGE,  2  THE RIGHT EDGE,
C          3  THE TOP EDGE,   4  THE BOTTOM EDGE.
C
    5 NOTDEV= NOTATC/2**(INOTAT+1)
      IF (MOD(NOTDEV,2).NE.1) RETURN
      IF (LLBCON.GT.0) GO TO 7
C
C          THIS SECTION WRITES THE CONTOUR LEVEL NUMBER.
C
      IF (INOTAT.GT.1) GO TO 6
C
      CALL PLOTNI(XPLOTB,YPLOTB,LEVEL)
      RETURN
C
    6 IF (INOTAT.EQ.3) CALL LINEFD(-1)
      IF (INOTAT.EQ.4) CALL LINEFD(1)
      IF (INOTAT.GE.3) CALL SPACE(-1)
      CALL TYPENI(LEVEL)
      RETURN
C
C          THIS SECTION WRITES LABELS ON THE CONTOURS.
C
    7 IF (LEVEL.GT.LLBCON.OR.LEVEL.GT.50) RETURN
C
      DO 100 KWORD= 1,10
        ICOPY(KWORD)= LBLCON(KWORD,LEVEL)
  100 CONTINUE
C
      LIMIT= NMCHI*10
      CALL G4GETK(ISPACE,NMCHI,NBITMC,NMCHI,ISP)
      NCHARS= 0
C
      DO 200 ICHAR= 1,LIMIT
        NOCHAR= LIMIT-ICHAR+1
        CALL G4GETK(ICOPY,NOCHAR,NBITMC,NMCHI,ICHR)
        CALL G4COCO(ICHR,ITEXT(NOCHAR))
        IF (ISP.NE.ICHR.AND.NCHARS.EQ.0) NCHARS= NOCHAR
  200 CONTINUE
C
      INOT1= INOTAT+1
      GO TO (12,12,8,9,11), INOT1
C
    8 CALL SPACE(1)
      GO TO 10
C
    9 CALL SPACE(2)
   10 CALL G3LINK(2,11,-NCHARS,ITEXT,RDATA)
C
      RETURN
C
   11 CALL SPACE(-1)
   12 IDATA(1)= 2
      CALL G3LINK(2,17,-1,IDATA,RDATA)
      CALL G3LINK(2,11,-NCHARS,ITEXT,RDATA)
      IDATA(1)= 0
      CALL G3LINK(2,17,-1,IDATA,RDATA)
C
      RETURN
      END
