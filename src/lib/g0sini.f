      SUBROUTINE G0SINI(SURFAS,ISTRTX,ISTOPX,NPTSX,ISTRTY,ISTOPY,NPTSY)
C
C          ------------------------------------------------
C          ROUTINE NO. ( 364)   VERSION (A9.1)    07:MAY:92
C          ------------------------------------------------
C
C          THIS ROUTINE INITIALISES THE SCALING PARAMETERS
C          FOR THE SURFACE PLOTTING ROUTINES.
C
      REAL    SURFAS(NPTSX,NPTSY)
C
      COMMON /T0MAPA/ X1MAPV,X2MAPV,Y1MAPV,Y2MAPV
      COMMON /T0SAN1/ SINAZA,COSAZA,SINTLT,COSTLT
      COMMON /T0SANG/ TLTANG
      COMMON /T0SAZA/ AZIANG
      COMMON /T0SBAS/ IUNDRS,INDBAS,BASEHT
      COMMON /T0SCOM/ XLEN,YLEN,SCALE,XSHIFT,YSHIFT,SURMIN,SURMAX
      COMMON /T0SDL1/ DELTXR,DELTYR,DELTZR,DELTXC,DELTYC,DELTZC,
     &                VSCALE,ZSCALE,ITPBTM,IQDRNT
      COMMON /T0SSCA/ SHTMIN,SHTMAX,XPTCH
      COMMON /T0WNDO/ X1WND0,X2WND0,Y1WND0,Y2WND0
      COMMON /T3CONS/ PI
      COMMON /T3LIMS/ IMAXI,RMAXI,RMINI
C
      DATA VSC /1.2/, TOL /0.005/
C
C
C          THE NEXT SECTION SCANS SURFAS FOR MINIMUM AND
C          MAXIMUM HEIGHTS IF THESE HAVE NOT BEEN SPECIFIED.
C
      SURMIN= SHTMIN
      SURMAX= SHTMAX
      IF (ABS(SHTMAX-SHTMIN).GT.RMINI) GO TO 1
C
      SURMAX= SURFAS(ISTRTX,ISTRTY)
      SURMIN= SURMAX
C
      DO 100 J= ISTRTY,ISTOPY
        DO 100 I= ISTRTX,ISTOPX
          HT= SURFAS(I,J)
          IF (HT.GT.SURMAX) SURMAX= HT
          IF (HT.LT.SURMIN) SURMIN= HT
  100 CONTINUE
C
    1 IF (INDBAS.EQ.2) SURMIN= AMIN1(BASEHT,SURMIN)
C
C          REMAP SO THAT THE ORIGINAL WINDOW AREA IS USED
C          WITH A MAPPING OF 0.0 TO 1.0
C
      DIFF= X2WND0-X1WND0
      IF (ABS(DIFF).LT.RMINI) RETURN
C
      XM1= (X1MAPV-X1WND0)/DIFF
      XM2= (X2MAPV-X1WND0)/DIFF
      DIFF= Y2WND0-Y1WND0
      IF (ABS(DIFF).LT.RMINI) RETURN
C
      YM1= (Y1MAPV-Y1WND0)/DIFF
      YM2= (Y2MAPV-Y1WND0)/DIFF
      CALL MAP(XM1,XM2,YM1,YM2)
C
C          ENLARGE THE WINDOW AREA TO ALLOW ANNOTATION
C          AROUND THE EDGE OF THE DRAWING
C
      CALL WINDOW(-0.5,1.5,-0.5,1.5)
      CALL CTRSIZ(0.01)
C
C          THE TRANSFORMATION EQUATIONS ARE:
C
C          XTRAN= X*COS(AZANGL)-Y*SIN(AZANGL)
C          YTRAN= (X*SIN(AZANGL)+Y*COS(AZANGL))*SIN(TLTANG)
C                 +SCALE*Z*COS(TLTANG)
C          ZTRAN= (X*SIN(AZANGL)+Y*COS(AZANGL))*COS(TLTANG)
C                 -SCALE*Z*SIN(TLTANG)
C
C          WHERE <AZANGL> IS THE AZIMUTH ANGLE,
C                <TLTANG> IS THE TILT ANGLE AND
C                <SCALE>  IS A SUITABLE SCALE FACTOR.
C
C          THE ROTATIONS ARE DONE ABOUT THE 'ORIGIN' CORNER OF
C          THE GRID AND SO AN XSHIFT AND Y SHIFT MUST BE CALCULATED.
C
C          CALCULATE QUADRANT AND NET ROTATIONAL ANGLE.
C
      AZANGL= MOD(AZIANG,PI*2.0)
      IF (AZANGL.LT.0) AZANGL= AZANGL+PI*2.0
C
      IQDRNT= 4
      IF (AZANGL.LT.PI*1.5) IQDRNT= 3
      IF (AZANGL.LT.PI) IQDRNT= 2
      IF (AZANGL.LT.PI*0.5) IQDRNT= 1
C
C          AVOID 0 AND 90 DEGREE CASES BY A SMALL ROTATION.
C
      AZANGL= MOD(AZANGL,PI*0.5)
      AZANGL= MAX(AZANGL,TOL)
      AZANGL= MIN(AZANGL,PI*0.5-TOL)
C
C          REVERSE XLEN AND YLEN FOR QUADRANTS 2 AND 4.
C
      IF (MOD(IQDRNT,2).EQ.1) GO TO 2
C
      TEMP= XLEN
      XLEN= YLEN
      YLEN= TEMP
C
C          CALCULATE SCALE FACTOR FOR THE MAPPING.
C
    2 SINAZA= SIN(AZANGL)
      COSAZA= COS(AZANGL)
      SINTLT= SIN(TLTANG)
      COSTLT= COS(TLTANG)
      XPSUR= 1.0/(XPTCH*VSC)
      XEXT= XLEN*COSAZA+YLEN*SINAZA
      YEXT= (XLEN*SINAZA+YLEN*COSAZA)*ABS(SINTLT)+
     &       MAX(XLEN,YLEN)*COSTLT*XPSUR
      IF (XEXT.GE.YEXT) SCALE= 1.0/XEXT
      IF (YEXT.GE.XEXT) SCALE= 1.0/YEXT
C
      VSCALE= COSTLT*XPSUR*SCALE*MAX(XLEN,YLEN)/(SURMAX-SURMIN)
      ZSCALE= SINTLT*XPSUR*SCALE*MAX(XLEN,YLEN)/(SURMAX-SURMIN)
      DELTXR= COSAZA*SCALE
      DELTYR= SINAZA*SINTLT*SCALE
      DELTZR= SINAZA*COSTLT*SCALE
      DELTXC= -SINAZA*SCALE
      DELTYC= COSAZA*SINTLT*SCALE
      DELTZC= COSAZA*COSTLT*SCALE
C
C          CALCULATE OFFSETS TO CENTRE THE DRAWING
C
      XSHIFT= 0.5-0.5*(XLEN*DELTXR+YLEN*DELTXC)
      YSHIFT= 0.5-0.5*((SURMIN+SURMAX)*VSCALE+
     &                  XLEN*DELTYR+YLEN*DELTYC)
C
      RETURN
      END
