      SUBROUTINE G0PLXL(LINE)
C
C          ------------------------------------------------
C          ROUTINE NO. ( 237)   VERSION (A8.7)    11:JAN:88
C          ------------------------------------------------
C
C          THIS DRAWS TICK MARKS AND (OPTIONALLY) AN AXIS LINE,
C          WITH LOGARITHMIC MARKINGS ALONG THE X-DIRECTION.
C
C          <LINE>   CONTROLS WHETHER THE AXIS LINE IS DRAWN:
C                   IF ZERO, ONLY TICK MARKS ARE DRAWN,
C                   OTHERWISE, THE AXIS LINE IS ALSO DONE.
C
C
C          THE FOLLOWING ARGUMENTS ARE SUPPLIED THROUGH COMMON:
C
C          <NOTATA> IF ZERO, NO ANNOTATION IS REQUIRED,
C                   IF NON-ZERO, ANNOTATION IS DONE.
C          <AXPOSX> THE POSITION OF THE X-AXIS ALONG Y.
C          <DIVLX>  GIVES THE X-AXIS SIGN.
C          <NSKIPX> IS THE X-AXIS START-POINT EXPONENT.
C          <NTIKLX> IS THE X-AXIS START-POINT MANTISSA.
C          <NTIKHX> IS THE NO. OF X-AXIS TICK MARKS.
C          <NDECSX> IS THE NO. OF DECADE MARKS ON THE X-AXIS.
C          <NCHRSX> IS THE NO. OF CHARS. IN X-AXIS ANNOTATION.
C          <KAXIS>  INDICATES WHETHER ANNOTATION MAY BE SUPPRESSED:
C                   > 0, NO, OR
C                   < 0, YES, IF IT INTERFERES WITH THE OTHER AXIS.
C          <TKEN1A> AND
C          <TKEN1B> ARE THE END-POINTS OF THE 5/1000TH.-SIZE TICK MARK.
C          <TKEN2A> AND
C          <TKEN2B> ARE THE END-POINTS OF THE 8/1000TH.-SIZE TICK MARK.
C
C
      REAL    RDATA(1)
      INTEGER IDECFC(3)
C
      COMMON /T0AARG/ KAXIS
      COMMON /T0ADIX/ DIVLX,NTIKLX,NTIKHX
      COMMON /T0ANOD/ KDIRX,KDIRY
      COMMON /T0ANOX/ KANNX,NCHRSX,NAFTPX
      COMMON /T0APOS/ AXPOSX,AXPOSY
      COMMON /T0ASKX/ NSKIPX,NDECSX
      COMMON /T0ATIK/ TKEN1A,TKEN1B,TKEN2A,TKEN2B
      COMMON /T0NOTA/ NOTATA
      COMMON /T0WNDO/ X1WND0,X2WND0,Y1WND0,Y2WND0
      COMMON /T3LIMS/ IMAXI,RMAXI,RMINI
C
      DATA RDATA /0.0/, IDECFC /120,49,48/
C
C
C          THIS PART DRAWS THE AXIS, IF REQUIRED.
C
      IF (LINE.EQ.0) GO TO 1
C
      CALL POSITN(X1WND0,AXPOSX)
      CALL   JOIN(X2WND0,AXPOSX)
C
C          IF THERE ARE NO TICK MARKS, NO MORE NEED BE DONE.
C          OTHERWISE, THE CURRENT FONT IS SAVED, THE START POINT
C          IS FOUND, THEN LOOP-100 DRAWS EACH TICK MARK (OF THE
C          APPROPRIATE SIZE) IN TURN, ANNOTATING IT AS REQUIRED.
C
    1 IF (NTIKHX.LE.0) RETURN
C
      IPOS= NTIKLX-1
      IDEC= NSKIPX
      MULTPT= 3
      IF (IPOS.GT.2.AND.IPOS.LT.8) MULTPT= IPOS+1
C
      DO 100 ITICK= 1,NTIKHX
        TKENDA= TKEN1A
        TKENDB= TKEN1B
        IPOS= IPOS+1
        IF (IPOS.LT.10) GO TO 2
C
        IPOS= 1
        IDEC= IDEC+1
        TKENDA= TKEN2A
        TKENDB= TKEN2B
C
C          THE APPROPRIATE-SIZE TICK MARK IS DRAWN. IF ANNOTATION
C          IS NOT REQUIRED, OR IF THIS POSITION IS NOT ON A DECADE
C          INTERVAL WHEN THERE ARE A LARGE NUMBER OF LOG. CYCLES
C          PRESENT, LOOP-100 CONTINUES WITH THE NEXT TICK MARK.
C
    2   XPOS= DIVLX*IPOS*(10.0**IDEC)
        CALL POSITN(XPOS,TKENDA)
        CALL   JOIN(XPOS,TKENDB)
        IF (NOTATA.EQ.0) GO TO 100
C
C          THIS SECTION SUPPRESSES ANNOTATION WHEN THIS
C          COINCIDES WITH THE OTHER AXIS, IF REQUIRED.
C
        IF (KAXIS.GT.0) GO TO 3
        IF (ABS(AXPOSX-Y1WND0).LT.RMINI.AND.KDIRY.LE.0) GO TO 3
        IF (ABS(AXPOSX-Y2WND0).LT.RMINI.AND.KDIRY.GE.1) GO TO 3
C
        XDIFF= ALOG (XPOS/AXPOSY)
        IF (ABS(XDIFF).LT.0.04*ALOG(X2WND0/X1WND0)) GO TO 6
C
C          WHEN ANNOTATION IS TO BE DONE, THE POSITION IS SET,
C          THE APPROPRIATE FORMAT SELECTED, AND THE VALUE PRINTED.
C          IF THERE ARE MORE THAN 10 TICK MARKS, INTEGER+EXPONENT
C          FORMAT IS USED; OTHERWISE NORMAL REAL OR INTEGER FORMAT
C          IS USED, PROVIDED THE VALUES ARE NOT TOO LARGE OR SMALL.
C
    3   CALL POSITN(XPOS,AXPOSX)
        IF (KDIRX.LE.0) CALL LINEFD (2)
        IF (KDIRX.GE.1) CALL HLINFD(-3)
        IF (NTIKHX.GT.10)                GO TO 5
        IF (NSKIPX.LT.-2.OR.NSKIPX.GT.3) GO TO 5
C
C          THESE SECTIONS PROVIDE NORMAL FORMAT (INTEGER OR REAL).
C
        IF (NSKIPX.LT.0) GO TO 4
C
        CALL SPACE(-1)
        IVALUE= IPOS*(10**IDEC)
        IF (DIVLX.LT.0.0) IVALUE= -IVALUE
C
        CALL TYPENI(IVALUE)
        GO TO 100
C
    4   CALL HSPACE(2-NCHRSX)
        CALL TYPENF(XPOS,-NSKIPX)
        GO TO 100
C
C          THESE SECTIONS PROVIDE INTEGER+EXPONENT FORMAT.
C
    5   CALL SPACE(-1)
        IVALUE= IPOS
        IF (DIVLX.LT.0.0) IVALUE= -IVALUE
        IF (NTIKHX.LE.55.OR.IPOS.EQ.1) CALL TYPENI(IVALUE)
C
C          THIS PART WRITES THE DECADE MULTIPLYING FACTORS.
C
        IF (IPOS.NE.MULTPT) GO TO 100
C
        MULTPT= 3
        IF (ABS(X1WND0).GT.ABS(X2WND0)) CALL SPACE(-4)
        IF (KDIRX.LE.0) CALL HLINFD(3)
        IF (KDIRX.GE.1) CALL HLINFD(-3)
C
        CALL G3LINK(2,11,-3,IDECFC,RDATA)
        CALL SUPFIX
        IF (IDEC.GE.0) CALL SPACE(-1)
C
        CALL TYPENI(IDEC)
        CALL NORMAL
    6   IF (IPOS.EQ.MULTPT) MULTPT= 3
  100 CONTINUE
C
      RETURN
      END
