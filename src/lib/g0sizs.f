      SUBROUTINE G0SIZS(VALUE,LIMSIG,IEXP,ISIG)
C
C          ------------------------------------------------
C          ROUTINE NO. ( 235)   VERSION (A8.3)    25:NOV:86
C          ------------------------------------------------
C
C          THIS CONVERTS THE NUMBER INTO A NORMALISED DECIMAL
C          MANTISSA PLUS DECIMAL EXPONENT. THE MANTISSA IS THEN
C          LEFT-SHIFTED TO THE MAX. NO. OF SIGNIFICANT DIGITS
C          ALLOWED, TRUNCATED, THEN RIGHTMOST ZERO DIGITS ARE
C          REMOVED; THE NO. OF SIG. DIGITS IS HENCE FOUND.
C
C
C          THE FOLLOWING VALUES ARE RETURNED:
C
C          <IEXP>   IS THE DECIMAL EXPONENT VALUE OF THE NUMBER.
C          <ISIG>   IS THE NO. OF SIG. DIGITS IN THE ROUNDED NUMBER.
C
C
      DOUBLE PRECISION DVALUE,POWR10,ROUNDD
C
      COMMON /T3LIMS/ IMAXI,RMAXI,RMINI
C
C
      DVALUE= ABS(VALUE)
      IEXP= 0
      ISIG= 0
      IF (DABS(DVALUE).LT.RMINI) RETURN
C
      POWR10= DLOG10(DVALUE)
      IEXP= POWR10+DSIGN(1.0D-6,POWR10)
      IF (POWR10.LT.0.0D0) IEXP= IEXP-1
C
      ROUNDD= DVALUE*(10.0D0**(LIMSIG-IEXP-1))
      IROUND= ROUNDD+0.5D0
      ISIG= LIMSIG
    1   IF (MOD(IROUND,10).GT.0) RETURN
C
        IROUND= IROUND/10
        ISIG= ISIG-1
        IF (ISIG.GT.0) GO TO 1
C
C          IF THE MANTISSA IS CLOSE TO UNITY, <ROUNDD> MAY BE
C          ROUNDED-UP TO OVER 10**LIMSIG SO THAT <ISIG> GOES
C          TO ZERO IN THE LOOP. IF THIS OCCURS, THIS SECTION
C          RESETS <ISIG> AND ALSO ADJUSTS <IEXP> IF NECESSARY.
C
      ISIG= 1
      IF (IEXP.LT.0) IEXP= IEXP+1
C
      RETURN
      END
