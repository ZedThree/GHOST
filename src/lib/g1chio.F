      SUBROUTINE G1CHIO(ICOMND,NOCHAR)
C
C          ------------------------------------------------
C          ROUTINE NO. (1035)   VERSION (A8.1)    14:JUL:97
C                         === FORTRAN-77 ===
C          ------------------------------------------------
C
C          THIS OPENS, READS AND CLOSES THE HERSHEY AND
C          POSTSCRIPT CHARACTER FILES.
C          (THIS VERSION IS FOR THE UNIX OPERATING SYSTEM).
C
C
C          THE ARGUMENTS ARE AS FOLLOWS:
C
C          <ICOMND> SETS THE I/O OPERATION:
C                   = 1, THE CHARACTER FONT IS OPENED,
C                   = 2, A HERSHEY CHARACTER DEFINITION IS READ,
C                   = 3, THE CHARACTER FONT IS CLOSED.
C          <NOCHAR> CHARACTER NUMBER TO BE READ.
C
C
      REAL      XTEMP(200),YTEMP(200)
      LOGICAL   OPENED
      LOGICAL   ERRON
      CHARACTER FNTNAM*128,FNTDEF*128,FNTADD*12,FNTAD*11
      CHARACTER CHCMND*128
C
      COMMON /T1CATT/ IUNDL1,ITAL1
      COMMON /T1CFCH/ KFNTCH
      COMMON /T1CFIN/ INDLEN(2,256),WIDCHR(256)
      COMMON /T1CFON/ KFONT1
      COMMON /T1CMLC/ CHCMND(10)
      COMMON /T1CMLN/ NMFILE(32),LNFILE,ICMND(35,2)
      COMMON /T1SPEC/ XSPEC(4600),YSPEC(4600),ICHPTR(128),NEXTPT,NSTORE
      COMMON /T3ERRS/ ERRON,NUMERR
C
C
C          <IRECL> IS THE LENGTH OF THE DATA RECORD. THE UNITS
C          WHICH THE LENGTH IS SPECIFIED ARE MACHINE DEPENDANT.
C
C          THE UNITS ARE '4-CHARACTER WORDS' FOR THE FOLLOWING COMPUTERS:
C          VAX, MISTRAL, SILICON GRAPHICS
C          (SET IRECL TO 60).
C
C          THE UNITS ARE '2-CHARACTER WORDS' FOR THE FOLLOWING COMPUTERS:
C          PRIME (F77 AND FTN77 COMPILERS)
C          (SET IRECL TO 120).
C
C          THE UNITS ARE 'CHARACTERS' FOR THE FOLLOWING COMPUTERS:
C          APOLLO, GEC, HP, IBMPC, IBM WS, SUN, WHITECHAPEL
C          (SET IRECL TO 240).
C
C          THE UNITS ARE 'CHARACTERS' FOR THE FOLLOWING COMPUTERS:
C          CRAY
C          (SET IRECL TO 480).
C
      PARAMETER (IRECL = GHOST_RECL_UNIT*60)
C
#ifdef _WIN32
      CALL GET_FONT_DIR(FNTDEF)
#else
      FNTDEF = GHOST_EXEC_PREFIX//'/lib/fonts'
#endif
      FNTADD = '/GHOST80FONT'
      FNTAD  = '/GHOST80FNT'
C
      IF (ICOMND.GT.3) RETURN
C
C           THIS SECTION OPENS A NEW CHARACTER FONT FILE, CLOSING ANY
C           PREVIOUSLY OPENED ONE. THE HEADER INFORMATION IS THEN READ,
C           AND THE CHARACTER POINTERS ARE INITIALISED.
C
      IF (ICOMND.NE.1) GO TO 2
      IF (ICMND(28,1).NE.1) THEN
        CALL GETENV('GHOST',FNTNAM)
        IF (INDEX(FNTNAM,' ') .LE. 1) THEN
          FNTNAM = FNTDEF
        ELSE
#ifdef _WIN32
          FNTNAM(LENSTR(FNTNAM)+1:) = '/fonts'
#else
          FNTNAM(LENSTR(FNTNAM)+1:) = '/lib/fonts'
#endif
        ENDIF
        IFPNTR = LENSTR(FNTNAM)
C
      ELSE
        IFPNTR= ICMND(28,2)
        FNTNAM(1:IFPNTR)= CHCMND(3)(1:IFPNTR)
      ENDIF
C
      IF (KFONT1.LT.101) THEN
        FNTNAM(IFPNTR+1:IFPNTR+12)= FNTADD
      ELSE
        FNTNAM(IFPNTR+1:IFPNTR+11)= FNTAD
      ENDIF
C
        IFPNTR= IFPNTR+14
      IF (KFNTCH.LT.0) THEN
C
        DO 100 IUNIT= 99,1,-1
          INQUIRE(IUNIT,OPENED=OPENED)
          IF (.NOT.OPENED) THEN
            KFNTCH= IUNIT
            GO TO 1
          ENDIF
C
  100   CONTINUE
C          
      ENDIF
C
      INQUIRE(KFNTCH,OPENED= OPENED)
      IF (OPENED) CLOSE(KFNTCH)
C
C           AN INTERNAL WRITE IS USED TO ADD THE FONT
C           NUMBER ONTO THE FILENAME.
C
    1 IND= 1
      IF (KFONT1.LT.100) THEN
        WRITE(FNTNAM(IFPNTR-1:IFPNTR),'(I2)') KFONT1
        OPEN(KFNTCH,FILE= FNTNAM(1:IFPNTR),ACCESS= 'DIRECT',
     &       RECL= IRECL,STATUS= 'OLD',ERR= 901)
C
        DO 200 IREC= 1,16
          READ(KFNTCH,REC= IREC,ERR= 902)
     &        ((INDLEN(J,K),J= 1,2),WIDCHR(K),K= IND,IND+15)
          IND= IND+16
  200   CONTINUE
C
        DO 300 ICLEAR= 1,128
          ICHPTR(ICLEAR)= 0
  300   CONTINUE
C
        NSTORE= 0
        NEXTPT= 101
      ELSE
        WRITE(FNTNAM(IFPNTR-2:IFPNTR),'(I3)') KFONT1
        OPEN(KFNTCH,FILE= FNTNAM(1:IFPNTR),STATUS= 'OLD',ERR= 901)
C
        DO 400 IREC= 1,32
          READ(KFNTCH,*,ERR= 902) (WIDCHR(K),K= IND,IND+7)
          IND= IND+8
  400   CONTINUE
C
        DO 500 IMULT= 1,256
          WIDCHR(IMULT)= WIDCHR(IMULT)*7.0/6.0
  500   CONTINUE
C
      ENDIF
C
      RETURN
C
C           THIS SECTION READS CHARACTER 'NOCHAR' INTO 'XSPEC' AND
C           'YSPEC' UPDATING THE APPROPRIATE POINTER IN 'ICHPTR'.
C
    2 IF (ICOMND.NE.2) GO TO 3
      IF (KFNTCH.LT.0) RETURN
C
      LENGTH= INDLEN(2,NOCHAR)
      IF (LENGTH.EQ.0) RETURN
C
      IND= 1
      NRECS= (LENGTH-1)/30+1
C
      DO 600 IREC= INDLEN(1,NOCHAR),INDLEN(1,NOCHAR)+NRECS-1
        READ(KFNTCH,REC= IREC,ERR= 902)
     &      (XTEMP(I),YTEMP(I),I= IND,IND+29)
        IND= IND+30
  600 CONTINUE
C
      INXTPT= NEXTPT
      IF (NOCHAR.GT.128) INXTPT= 1
C
      DO 700 ILOAD= 1,LENGTH
        XSPEC(INXTPT)= XTEMP(ILOAD)
        YSPEC(INXTPT)= YTEMP(ILOAD)
        INXTPT= INXTPT+1
  700 CONTINUE
C
      IF (NOCHAR.GT.128) RETURN
C
      ICHPTR(NOCHAR)= NEXTPT
      NEXTPT= INXTPT
      RETURN
C
    3 IF (KFNTCH.LT.0) RETURN
C
      INQUIRE(KFNTCH,OPENED= OPENED)
      IF (.NOT.OPENED) RETURN
C
      CLOSE(KFNTCH)
      RETURN
C
C           THIS SECTION IS ENTERED IF THERE WAS AN ERROR.
C
  901 NUMERR= 1005
      IF (.NOT.ERRON) RETURN
C
      CALL G1FILB(0,0,-84,0)
      CALL G1ERMS
      RETURN
C
  902 NUMERR= 1006
      IF (.NOT.ERRON) RETURN
C
      CALL G1FILB(0,0,-84,0)
      CALL G1ERMS
      RETURN
C
      END
