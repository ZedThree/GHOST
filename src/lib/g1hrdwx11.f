      SUBROUTINE G1HRDW(ICODE)
C
C          ------------------------------------------------
C          ROUTINE NO. (1002)   VERSION (A8.1X11) 15:OCT:91
C          ------------------------------------------------
C
C          THIS SETS HARDWARE-IMPLEMENTED FACILITIES.
C          (THIS VERSION IS TO INTERFACE TO X WINDOWS VERSION 11).
C
C
C          <ICODE> DETERMINES THE HARDWARE FUNCTION REQUIRED:
C
C                  =  0, DEVICE INITIALISATION IS DONE.
C                  =  1, BROKEN LINE TYPE IS SET.
C                  =  2, CHAR. ORENTATION IS SET.
C                  =  3, CHARACTER SIZE IS SET.
C                  =  4, LINE COLOUR NUMBER IS SET.
C                  =  .
C                  =  7, CHARACTER OBLATENESS IS SET.
C                  =  8, A COLOUR TABLE ENTRY IS SET.
C                  =  .
C                  = 10, BACKGROUND COLOUR IS SET.
C                  = 11, A POLYGONAL BOUNDARY IS BEGUN.
C                  = 12, A POLYGONAL AREA IS FILLED.
C                  = 13, SHADING OPTION FLAG IS RESET.
C
C
      INTEGER   IRED(9),IGREEN(9),IBLUE(9)
      LOGICAL   KHRDW1,LHRDW1
      CHARACTER CHCMND*128
C
      COMMON /T1CANU/ STANGU,CRANGU
      COMMON /T1CDIM/ MAGN1,OBLAT1
      COMMON /T1CMLC/ CHCMND(10)
      COMMON /T1CMLN/ NMFILE(32),LNFILE,ICMND(35,2)
      COMMON /T1DRES/ DRESX,DRESY
      COMMON /T1FLTY/ IFLTY1,IFLRP1
      COMMON /T1HCOL/ IHCOL1(255,2)
      COMMON /T1HRDC/ KHRDW1
      COMMON /T1HRDL/ LHRDW1
      COMMON /T1KBAC/ KOLBA1
      COMMON /T1KDEF/ KOLLID,KOLBAD,MAXCLS
      COMMON /T1KFIL/ KOLFL1
      COMMON /T1KTAB/ REDCO1(255),GRNCO1(255),BLUCO1(255),NCOLS1
      COMMON /T1LATT/ KOLIN1,ITHIK1
      COMMON /T1LPAT/ MARKA1,MISSA1,MARKB1,MISSB1
      COMMON /T1X11B/ KOLOUR,KOLNUM,IREDIN(1),UPDFRM
C
      DATA IRED   /0,0,255,0,0,255,0,255,255/,
     &     IGREEN /0,0,0,255,0,255,255,0,255/,
     &     IBLUE  /0,0,0,0,255,255,255,255,0/
C
C
      IF (ICODE.LT.0.OR.ICODE.GT.13) RETURN
      IF (ICODE.NE.0) GO TO 1
C
C          THIS SECTION DOES DEVICE INITIALISATION.
C
      CALL G1CMLN
      CALL XINIT(IXEXT,IYEXT,IGDEEP)
      MAXCLS= 0
      IF (IGDEEP.GE.8) IGDEEP=8
      IF (IGDEEP.GT.1) MAXCLS= 2**IGDEEP-1
      IF (MAXCLS.GE.255) MAXCLS= 255
C
      DRESX= MIN(IXEXT,IYEXT)
      DRESY= DRESX
      KOLLID= 1
      KOLBAD= 5
      IF (MAXCLS.GT.0) THEN
C
        DO 200 J= 1,9
          CALL XCOLTB(J-1,IRED(J),IGREEN(J),IBLUE(J))
  200   CONTINUE
C
        IF (MAXCLS.EQ.255)
     &      CALL XCOLTB(255,IRED(6),IGREEN(6),IBLUE(6))
C
        KOLLID= 5
        KOLBAD= 1
        CALL XSETFC(KOLLID)
      ENDIF
C
      DO 300 ISETCO= 1,255
        IHCOL1(ISETCO,1)= KOLLID
        IHCOL1(ISETCO,2)= KOLLID
  300 CONTINUE
C
      KOLIN1= KOLLID
      KOLOUR= KOLLID
      RETURN
C
C          THIS SECTION SETS THE MOST APPROPRIATE
C          HARDWARE LINE TYPE FOR THE GIVEN ARGUMENTS.
C
    1 IF (ICODE.NE.1) GO TO 8
      IF (LHRDW1) GO TO 2
C
      LHDEF= 0
      GO TO 7
C
    2 IF (MARKA1.NE.0) GO TO 3
      IF (MISSA1.NE.0) GO TO 3
      IF (MARKB1.NE.0) GO TO 3
      IF (MISSB1.NE.0) GO TO 3
C
      LHDEF= 0
      GO TO 7
C
    3 IF (MARKA1.GT.5.OR.MARKB1.GT.5) GO TO 4
C
      LHDEF= 2
      GO TO 7
C
    4 IF (MARKA1.LT.2*MARKB1.AND.MARKB1.LT.2*MARKA1) GO TO 5
C
      LHDEF= 1
      GO TO 7
C
    5 IF (MARKA1.GT.15) GO TO 6
C
      LHDEF= 3
      GO TO 7
C
    6 LHDEF= 5
    7 CALL G1FILB(0,0,-15,LHDEF)
      RETURN
C
C          THIS SECTION SETS HARDWARE CHAR. ROTATION.
C
    8 IF (ICODE.NE.2) GO TO 9
      IF (.NOT.KHRDW1) RETURN
C
      KHAR= CRANGU*1000.0
      CALL G1FILB(0,0,-12,KHAR)
      RETURN
C
C          THIS SECTION SETS THE CHAR. MAGN. TO THE
C          MOST APPROPRIATE VALUE FOR HARDWARE CHARS.
C
    9 IF (ICODE.NE.3) GO TO 10
      IF (.NOT.KHRDW1) RETURN
C
      KHAR= (MAGN1*INT(DRESY)+500)/1000
      CALL G1FILB(0,0,-11,KHAR)
      RETURN
C
C          THIS SECTION SETS THE LINE COLOUR NUMBER.
C
   10 IF (ICODE.NE.4) GO TO 11
      IF (MAXCLS.LE.0) RETURN
      IF (KOLIN1.GT.MAXCLS) KOLIN1= KOLLID
C
      CALL G1FILB(0,0,-17,KOLIN1)
      RETURN
C
C          THIS SECTION SETS PORTRAIT MODE.
C          (IT IS NOT IMPLEMENTED IN THIS VERSION)
C
   11 IF (ICODE.NE.5) GO TO 12
      RETURN
C
C          THIS SECTION SETS HARDWARE ITALIC.
C          (IT IS NOT IMPLEMENTED IN THIS VERSION)
C
   12 IF (ICODE.NE.6) GO TO 13
      RETURN
C
C          THIS SECTION SETS HARDWARE CHARACTER OBLATENESS.
C
   13 IF (ICODE.NE.7) GO TO 14
      IF (.NOT.KHRDW1) RETURN
C
      IOBLAT= OBLAT1*1000.0
      CALL G1FILB(0,0,-14,IOBLAT)
      RETURN
C
C          THIS SECTION SETS A COLOUR TABLE ENTRY.
C
   14 IF (ICODE.NE.8) GO TO 15
      IF (MAXCLS.LE.0) RETURN
      IF (NCOLS1.GT.MAXCLS) RETURN
C
      IRED1= REDCO1(NCOLS1)*255.0
      IGREN1= GRNCO1(NCOLS1)*255.0
      IBLUE1= BLUCO1(NCOLS1)*255.0
      CALL G1FILB(0,0,-41,-2)
      CALL G1FILB(0,0,NCOLS1,IRED1)
      CALL G1FILB(0,0,IGREN1,IBLUE1)
      RETURN
C
C          THIS SECTION SETS THE DEVICE RESOLUTION.
C          (IT IS NOT IMPLEMENTED IN THIS VERSION).
C
   15 IF (ICODE.NE.9) GO TO 16
      RETURN
C
C          THIS SECTION SETS THE BACKGROUND COLOUR.
C
   16 IF (ICODE.NE.10) GO TO 17
      IF (MAXCLS.LE.0) RETURN
      IF (KOLBA1.GT.MAXCLS) KOLBA1= KOLBAD
C
      IRED1= REDCO1(KOLBA1)*255.0
      IGREN1= GRNCO1(KOLBA1)*255.0
      IBLUE1= BLUCO1(KOLBA1)*255.0
      CALL G1FILB(0,0,-43,-2)
      CALL G1FILB(0,0,0,IRED1)
      CALL G1FILB(0,0,IGREN1,IBLUE1)
      RETURN
C
C          THIS SECTION BEGINS A FILL-AREA.
C
   17 IF (ICODE.NE.11) GO TO 18
      IF (MAXCLS.LE.0) RETURN
      IF (KOLFL1.GT.MAXCLS) KOLFL1= KOLLID
C
      CALL G1FILB(0,0,-17,KOLFL1)
      RETURN
C
C          THIS SECTION FILLS THE POLYGONAL AREA.
C
   18 IF (ICODE.NE.12) GO TO 19
      IF (MAXCLS.LE.0) RETURN
C
      CALL G1FILB(0,0,-17,KOLIN1)
      RETURN
C
C          THIS SECTION RESETS THE SHADING OPTION FLAG
C          FOR COLOUR MACHINES ONLY.
C
   19 IF (MAXCLS.GT.0) IFLRP1 = 0
      RETURN
      END
