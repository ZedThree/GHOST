      SUBROUTINE TYPENF(VALUE,NAFTDP)
C
C          ------------------------------------------------
C          ROUTINE NO. ( 167)   VERSION (A7.5)    11:FEB:85
C          ------------------------------------------------
C
C          THIS DRAWS OUT THE NUMBER <VALUE> IN REAL FORMAT,
C          WITH <NAFTDP> DIGITS AFTER THE DECIMAL POINT.
C
C
      REAL    RDATA(1)
      INTEGER IDATA(20)
      LOGICAL ERRON
C
      COMMON /T3ERRS/ ERRON,NUMERR
      COMMON /T3LIMS/ IMAXI,RMAXI,RMINI
C
      DATA ISPACE /32/, MINUS /45/, IPOINT /46/, IZERO /48/,
     &     RDATA /0.0/
C
C
C          THE ERROR EXIT IS TAKEN IF <NAFTDP> IS WRONG.
C          THE FIRST CHARACTER IS SET TO SPACE OR ZERO,
C          DEPENDING ON THE SIGN OF THE NUMBER.
C
      IF (NAFTDP.LE.0)  GO TO 901
      IF (NAFTDP.GT.17) GO TO 901
C
      IDATA(1)= ISPACE
      IF (VALUE.LT.0.0) IDATA(1)= MINUS
C
C          THE NUMBER IS LEFT-SHIFTED BY <NAFTDP> PLACES THEN
C          ROUNDED. IF THIS RESULTS IN A ZERO NUMBER, THE SIGN
C          IS SUPPRESSED. IF THE NUMBER IS GREATER THAN UNITY,
C          A DECIMAL IPOINT IS INSERTED IN THE CHARACTER STRING
C          AT THE APPROPRIATE POSITION; IF IT IS FRACTIONAL,
C          AN INITIAL ZERO, A DECIMAL POINT, THEN AS MANY
C          FOLLOWING ZEROES AS NECESSARY ARE ADDED INSTEAD.
C
      ABVAL= ABS(VALUE)
      SHIFAC= 10.0**NAFTDP
      SHIFTD= ABVAL*SHIFAC
      IF (SHIFTD.GT.FLOAT(IMAXI)) GO TO 901
      IVALUE= SHIFTD+0.5
      IF (IVALUE.EQ.0) IDATA(1)= ISPACE
C
      IF (ABVAL.LT.1.0.AND.SHIFAC-SHIFTD.GT.0.5) GO TO 1
C
      INDEX= 2
      CALL G0CONI(IVALUE,IDATA,INDEX)
      NMOVE= INDEX
      DO 100 IMOVE= 1,NAFTDP
        NMOVE= NMOVE-1
        IDATA(NMOVE+1)= IDATA(NMOVE)
  100 CONTINUE
      IDATA(NMOVE)= IPOINT
      NCHARS= INDEX
      GO TO 2
C
    1 IDATA(2)= IZERO
      IDATA(3)= IPOINT
      INDEX= 4
      CALL G0CONI(IVALUE,IDATA,INDEX)
      DO 200 IMOVE= 1,NAFTDP
        ITO= NAFTDP-IMOVE+4
        IFROM= INDEX-IMOVE
        IVALUE= IZERO
        IF (IFROM.GT.3) IVALUE= IDATA(IFROM)
        IDATA(ITO)= IVALUE
  200 CONTINUE
      NCHARS= NAFTDP+3
C
    2 CALL G3LINK(2,11,-NCHARS,IDATA,RDATA)
      RETURN
C
  901 NUMERR= 30
      IF (ERRON) CALL G0ERMS
      RETURN
      END
