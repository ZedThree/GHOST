      SUBROUTINE COLSET(ARG1,ARG2,ARG3,NCOLOR)
C
C          ------------------------------------------------
C          ROUTINE NO. (  62)   VERSION (A7.2)    14:MAR:94
C          ------------------------------------------------
C
C          THIS DEFINES A COLOUR (IN TERMS
C          OF THE CURRENT COLOUR SYSTEM).
C
C
C          THE ARGUMENTS ARE AS FOLLOWS:
C
C          <ARG1>   IS THE FIRST COLOUR PARAMETER,
C          <ARG2>   IS THE SECOND COLOUR PARAMETER,
C          <ARG3>   IS THE THIRD COLOUR PARAMETER, AND
C          <NCOLOR> IS THE ASSOCIATED COLOUR NUMBER.
C
C
      REAL    RDATA(4),SHVAL(4)
      INTEGER IDATA(1),IPNTR(6,3)
      LOGICAL ERRON
C
      COMMON /T0KSYS/ KOLSYS
      COMMON /T0KTAB/ REDCO0(255),GRNCO0(255),BLUCO0(255),NCOLS0
      COMMON /T0KTFL/ NCFLG0(255)
      COMMON /T0TRAC/ IPRINT
      COMMON /T0TRAR/ RTRAC1,RTRAC2,RTRAC3,RTRAC4
      COMMON /T3CONS/ PI
      COMMON /T3ERRS/ ERRON,NUMERR
      COMMON /T3NBYR/ NBYTR
C
      DATA IDATA /0/
      DATA IPNTR /4,1,1,3,2,2,
     &            2,2,4,1,1,3,
     &            1,3,2,2,4,1/
C
C
      CALL G3INIT(2)
C
      RTRAC1= ARG1
      RTRAC2= ARG2
      RTRAC3= ARG3
      RTRAC4= NCOLOR
      IF (IPRINT.EQ.1) CALL G0MESG(128,4)
C
      IF (NCOLOR.LT.1.OR.NCOLOR.GT.255) RETURN
      RDATA(1)= NCOLOR
      CPARA1= ARG1
      CPARA2= ARG2
      CPARA3= ARG3
      IF (CPARA2.LT.0.0) CPARA2= 0.0
      IF (CPARA2.GT.1.0) CPARA2= 1.0
      IF (CPARA3.LT.0.0) CPARA3= 0.0
      IF (CPARA3.GT.1.0) CPARA3= 1.0
      IF (KOLSYS.GE.4) GO TO 11
C
C          THIS SECTION FINDS THE HUE IN DEGREES IN THE
C          RANGE [0.0 TO 360.0] FOR THE OTHER THREE SYSTEMS.
C
      HUE= CPARA1
    1 HUE= HUE+1.0
      IF (HUE.LT.0.0) GO TO 1
C
      HUE= AMOD(HUE*360.0,360.0)
C
C          THIS SECTION CONVERTS <HSV> VALUES INTO <RGB ONES.
C
      IF (KOLSYS.NE.1) GO TO 2
C
      HUE= HUE/60.0
      ISWTCH= HUE+1.0
      SHADE= AMOD(HUE,1.0)
C
      SHVAL(1)= CPARA3
      SHVAL(2)= CPARA3*(1.0-CPARA2)
      SHVAL(3)= CPARA3*(1.0-(CPARA2*SHADE))
      SHVAL(4)= CPARA3*(1.0-(CPARA2*(1.0-SHADE)))
C
      INDEX= IPNTR(ISWTCH,1)
      RDATA(2)= SHVAL(INDEX)
      INDEX= IPNTR(ISWTCH,2)
      RDATA(3)= SHVAL(INDEX)
      INDEX= IPNTR(ISWTCH,3)
      RDATA(4)= SHVAL(INDEX)
      GO TO 12
C
C          THIS SECTION CONVERTS <HLS> VALUES INTO <RGB> ONES.
C
   2  IF (KOLSYS.NE.2) GO TO 6
C
      SHMAX= CPARA2+CPARA2*CPARA3
      IF (CPARA2.GT.0.5) SHMAX= CPARA2+CPARA3-CPARA2*CPARA3
      SHMIN= CPARA2*2.0-SHMAX
C
      DO 100 ISHADE= 1,3
        HUEDIS= HUE-(ISHADE-1)*120.0
        IF (HUEDIS.LT.0.0) HUEDIS= HUEDIS+360.0
C
        IF (HUEDIS.GE.60.0) GO TO 3
        SHVAL(ISHADE)= SHMIN+(SHMAX-SHMIN)*HUEDIS/60.0
        GO TO 100
C
    3   IF (HUEDIS.GE.180.0) GO TO 4
        SHVAL(ISHADE)= SHMAX
        GO TO 100
C
    4   IF (HUEDIS.GE.240.0) GO TO 5
        SHVAL(ISHADE)= SHMIN+(SHMAX-SHMIN)*(240.0-HUEDIS)/60.0
        GO TO 100
C
    5   SHVAL(ISHADE)= SHMIN
  100 CONTINUE
C
      GO TO 10
C
C          THIS SECTION CONVERTS <HSI> VALUES INTO <RGB> ONES.
C
    6 SHADE= CPARA2*CPARA3
      HUEANG= HUE
      IF (HUEANG.GE.120.0) HUEANG= HUEANG-120.0
      IF (HUEANG.GE.120.0) HUEANG= HUEANG-120.0
      HUEANG= HUEANG*PI/180.0
C
      DO 200 ISHADE= 1,3
        HUEDIS= HUE-(ISHADE-1)*120.0
        IF (HUEDIS.LT.0.0) HUEDIS= HUEDIS+360.0
C
        IF (HUEDIS.GE.120.0) GO TO 7
        SHVAL(ISHADE)= CPARA3-SHADE*COS(HUEANG+PI/3.0)/
     &                              COS(HUEANG-PI/3.0)
        GO TO 200
C
    7   IF (HUEDIS.GE.240.0) GO TO 8
        SHVAL(ISHADE)= CPARA3+SHADE*COS(HUEANG)/COS(HUEANG-PI/3.0)
        GO TO 200
C
    8   SHVAL(ISHADE)= CPARA3-SHADE
  200 CONTINUE
C
C          THIS SECTION CHECKS TO SEE IF ANY OF THE <RGB> VALUES
C          BECOME SATURATED WHEN USING THE <HSI> METHOD.
C
      DO 300 LOOK= 1,3
        IF (SHVAL(LOOK).GE.0.0) GO TO 9
        SHVAL(LOOK)= 0.0
        NUMERR= 34
        IF (ERRON) CALL G0ERMS
    9   IF (SHVAL(LOOK).LE.1.0) GO TO 300
        SHVAL(LOOK)= 1.0
        NUMERR= 34
        IF (ERRON) CALL G0ERMS
  300 CONTINUE
C
   10 RDATA(2)= SHVAL(1)
      RDATA(3)= SHVAL(2)
      RDATA(4)= SHVAL(3)
      GO TO 12
C
C          THIS SECTION IS FOR THE <RGB> SYSTEM. NO
C          CONVERSION NEEDS TO BE DONE IN THIS CASE.
C
   11 IF (CPARA1.LT.0.0) CPARA1= 0.0
      IF (CPARA1.GT.1.0) CPARA1= 1.0
      RDATA(2)= CPARA1
      RDATA(3)= CPARA2
      RDATA(4)= CPARA3
C
   12 IF (NCOLS0.LT.NCOLOR) NCOLS0= NCOLOR
      REDCO0(NCOLOR)= RDATA(2)
      GRNCO0(NCOLOR)= RDATA(3)
      BLUCO0(NCOLOR)= RDATA(4)
      NCFLG0(NCOLOR)= 1
C
      CALL G3LINK(5,1,4*NBYTR,IDATA,RDATA)
      RETURN
      END
