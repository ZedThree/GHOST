      SUBROUTINE MULBAR(BASPOS,BARWID,BARPOS,VALSTS,ISTART,ISTOP,NOVALS,
     &                  NOSETS)
C
C          ------------------------------------------------
C          ROUTINE NO. (  95)   VERSION (A8.1)    14:JUL:88
C          ------------------------------------------------
C
C          THIS DRAWS A MULTIPLE BAR CHART OF A GIVEN SET OF
C          VALUES,WITH HORIZONTAL OR VERTICAL BARS.
C          THIS USES ABSOLUTE VALUES.
C
C
C          THE ARGUMENTS ARE AS FOLLOWS:
C
C          <BASPOS>  IS THE BASELINE ORIGIN OF THE GRAPH,
C          <BARWID>  IS THE THICKNESS OF THE BARS,
C          [BARPOS]  ARE THE POSITIONS OF THE BARS,
C          [VALSTS]  ARE THE VALUES TO BE GRAPHED,
C          <ISTART>  IS THE LOWER BOUNDARY, AND
C          <ISTOP>   IS THE UPPER BOUNDARY OF THE BAR NUMBERS,
C          <NOVALS>  IS THE TOTAL NUMBER OF BARS IN THE VALUES ARRAY,
C          <NOSETS>  IS THE TOTAL NUMBER OF SETS OF BARS IN THE VALUES ARRAY.
C
C
      REAL    BAUTO(2),VAUTO(2),BARPOS(ISTOP),VALSTS(NOVALS,NOSETS)
      REAL    RDATA(1)
      INTEGER IDATA(1)
C
      COMMON /T0BRTY/ IBRTYP
      COMMON /T0DBND/ IDRBND
      COMMON /T0KLST/ LSTCL0(100),LENLST
      COMMON /T0MULF/ INFLGS(100)
      COMMON /T0PPOS/ XPLOT0,YPLOT0
      COMMON /T0TRAC/ IPRINT
      COMMON /T0TRAR/ RTRAC1,RTRAC2,RTRAC3,RTRAC4
      COMMON /T3LIMS/ IMAXI,RMAXI,RMINI
C
      DATA RDATA /0.0/
C
      RTRAC1= BARWID
      IF (IPRINT.EQ.1) CALL G0MESG(161,1)
C
C          AUTOMATIC MAPPING IS DONE FOR THE GIVEN VALUES.
C          THE CURRENT PLOTTING POSITION IS SAVED, THE
C          HISTOGRAM IS DRAWN, THEN THE POSITION IS RESTORED.
C
      IF (ISTOP.LT.ISTART) RETURN
C
      BAUTO(1)= BARPOS(ISTART)-BARWID*0.5
      BAUTO(2)= BARPOS(ISTOP)+BARWID*0.5
      VAUTO(1)= BASPOS
      VAUTO(2)= BASPOS
C
      DO 100 ISTCNT= 1,NOSETS
        IF (ISTCNT.GT.100) GO TO 1
        IF (INFLGS(ISTCNT).EQ.0) GO TO 100
C
    1   DO 200 IBRCNT= ISTART,ISTOP
          VAL= VALSTS(IBRCNT,ISTCNT)
          IF (VAL.LT.VAUTO(1)) VAUTO(1)= VAL
          IF (VAL.GT.VAUTO(2)) VAUTO(2)= VAL
  200   CONTINUE
  100 CONTINUE
C
      IF (ABS(BAUTO(2)-BAUTO(1)).LT.RMINI.OR.
     &    ABS(VAUTO(2)-VAUTO(1)).LT.RMINI) RETURN
      IF (IBRTYP.EQ.0) CALL G0AUTO(BAUTO,VAUTO,1,2,1,2,1)
      IF (IBRTYP.NE.0) CALL G0AUTO(VAUTO,BAUTO,1,2,1,2,1)
C
      XHERE= XPLOT0
      YHERE= YPLOT0
      IF (LENLST.LE.0) GO TO 9
C
      DO 300 IBAR= ISTART,ISTOP
        BRPOS= BARPOS(IBAR)-BARWID*0.5
        BRPOS1= BRPOS+BARWID
C
        DO 400 IEASET= 1,NOSETS
          IF (IEASET.GT.100) GO TO 2
          IF (INFLGS(IEASET).EQ.0) GO TO 400
C
    2     KOLIND= MOD(IEASET,LENLST)
          IF (KOLIND.EQ.0) KOLIND= LENLST
          IF (LSTCL0(KOLIND).EQ.0) GO TO 400
C
          IDATA(1)= 0
          IF (LSTCL0(KOLIND).LT.0) IDATA(1)= 1
C
          CALL G3LINK(5,13,-1,IDATA,RDATA)
          IDATA(1)= IABS(LSTCL0(KOLIND))
          CALL G3LINK(5,3,-1,IDATA,RDATA)
          CURVAL= VALSTS(IBAR,IEASET)
          FROM= BASPOS
          IF (CURVAL.LT.BASPOS) GO TO 4
C
          DO 500 ISTCNT= 1,NOSETS
            IF (ISTCNT.GT.100) GO TO 3
            IF (INFLGS(ISTCNT).EQ.0) GO TO 500
C
    3       VAL= VALSTS(IBAR,ISTCNT)
            IF (VAL.LT.CURVAL.AND.VAL.GT.FROM) FROM= VAL
  500     CONTINUE
C
          GO TO 6
C
    4     DO 600 ISTCNT= 1,NOSETS
            IF (ISTCNT.GT.100) GO TO 5
            IF (INFLGS(ISTCNT).EQ.0) GO TO 600
C
    5       VAL= VALSTS(IBAR,ISTCNT)
            IF (VAL.GT.CURVAL.AND.VAL.LT.FROM) FROM= VAL
  600     CONTINUE
C
    6     IF (IBRTYP.NE.0) GO TO 7
C
          CALL POSITN(BRPOS,FROM)
          CALL JOIN(BRPOS,CURVAL)
          CALL JOIN(BRPOS1,CURVAL)
          CALL JOIN(BRPOS1,FROM)
          CALL JOIN(BRPOS,FROM)
          GO TO 8
C
    7     CALL POSITN(FROM,BRPOS)
          CALL JOIN(CURVAL,BRPOS)
          CALL JOIN(CURVAL,BRPOS1)
          CALL JOIN(FROM,BRPOS1)
          CALL JOIN(FROM,BRPOS)
    8     CALL G3LINK(5,4,0,IDATA,RDATA)
  400   CONTINUE
  300 CONTINUE
C
      IF (IDRBND.EQ.0) GO TO 14
C
    9 DO 700 IBAR= ISTART,ISTOP
        RLOWST= BASPOS
        HIGST= BASPOS
        BRPOS= BARPOS(IBAR)-BARWID*0.5
        BRPOS1= BRPOS+BARWID
C
        DO 800 ISTCNT= 1,NOSETS
C
C       DO FOR EACH VALUE
C
          IF (ISTCNT.GT.100) GO TO 10
          IF (INFLGS(ISTCNT).EQ.0) GO TO 800
C
   10     CURVAL= VALSTS(IBAR,ISTCNT)
          IF (CURVAL.LT.RLOWST) RLOWST= CURVAL
          IF (CURVAL.GT.HIGST) HIGST= CURVAL
          IF (IBRTYP.NE.0) GO TO 11
C
          CALL POSITN(BRPOS,CURVAL)
          CALL JOIN(BRPOS1,CURVAL)
          GO TO 800
C
   11     CALL POSITN(CURVAL,BRPOS)
          CALL JOIN(CURVAL,BRPOS1)
  800   CONTINUE
C
        IF (IBRTYP.NE.0) GO TO 12
C
        CALL POSITN(BRPOS,RLOWST)
        CALL JOIN(BRPOS,HIGST)
        CALL POSITN(BRPOS1,HIGST)
        CALL JOIN(BRPOS1,RLOWST)
        GO TO 700
C
   12   CALL POSITN(RLOWST,BRPOS)
        CALL JOIN(HIGST,BRPOS)
        CALL POSITN(HIGST,BRPOS1)
        CALL JOIN(RLOWST,BRPOS1)
  700 CONTINUE
C
C     DRAW BASELINE
C
      IF (IBRTYP.NE.0) GO TO 13
C
      CALL POSITN(BAUTO(2),BASPOS)
      CALL JOIN(BAUTO(1),BASPOS)
      GO TO 14
C
   13 CALL POSITN(BASPOS,BAUTO(2))
      CALL JOIN(BASPOS,BAUTO(1))
   14 CALL POSITN(XHERE,YHERE)
      RETURN
      END
