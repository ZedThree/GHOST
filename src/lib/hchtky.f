      SUBROUTINE HCHTKY(XFIRST,XLAST,YFIRST,YLAST,LABELS,NLABLS)
C
C          ------------------------------------------------
C          ROUTINE NO. ( 118)   VERSION (A8.1)    25:JUN:87
C                         === FORTRAN-77 ===
C          ------------------------------------------------
C
C          THIS DRAWS A CHART KEY HORIZONTALLY.
C
C          THE ARGUMENTS ARE AS FOLLOWS:
C
C          <XFIRST,YFIRST> IS THE START CORNER, AND
C          <XLAST,YLAST>   IS THE FINISH CORNER OF THE KEY AREA.
C          [LABELS]   CONTAINS THE LABELS TO BE WRITTEN
C                     IN THE MENU BOXES.
C          <NLABLS>   IS THE NUMBER OF LABELS.
C
C
      CHARACTER LABELS(NLABLS)*(*)
C
      COMMON /T0CDIM/ MAGN0,OBLAT0
      COMMON /T0KFIL/ KOLFL0
      COMMON /T0KLAB/ LABCL0(100),LENLAB
      COMMON /T0KLST/ LSTCL0(100),LENLST
      COMMON /T0LATT/ KOLIN0,ITHIK0
      COMMON /T0MAPP/ X1MAP0,X2MAP0,Y1MAP0,Y2MAP0
      COMMON /T0PPOS/ XPLOT0,YPLOT0
      COMMON /T0TRAC/ IPRINT
C
C
      IF (IPRINT.EQ.1) CALL G0MESG(170,0)
      IF (NLABLS.LE.0) RETURN
C
      IPRSAV= IPRINT
      IPRINT= 0
      XSAVE= XPLOT0
      YSAVE= YPLOT0
      KOLSAV= KOLFL0
      KOLLSV= KOLIN0
      MAGSAV= MAGN0
      KOLIND= 1
      KOLLIN= 1
      NCHARS= LEN(LABELS(1))
      XINT= (XLAST-XFIRST)/NLABLS
      XSTART= XFIRST
      XEND= XFIRST+XINT
      XSIZE= ABS(XINT*(Y2MAP0-Y1MAP0)*1.167/
     &          ((NCHARS+1)*OBLAT0*(X2MAP0-X1MAP0)))
      YSIZE= ABS(YLAST-YFIRST)*0.65
      SIZE= AMIN1(XSIZE,YSIZE)
      YCEN= (YLAST+YFIRST)*0.5-SIZE*0.2143
      CALL CTRSIZ(SIZE)
C
      DO 100 NBOX= 1,NLABLS
        IF (LENLST.LE.0) GO TO 1
C
        KOLFL0= LSTCL0(KOLIND)
        KOLIND= KOLIND+1
        IF(KOLIND.GT.LENLST) KOLIND= 1
C
    1   CALL BOX(XSTART,XEND,YLAST,YFIRST)
        IF (LENLAB.GT.0) CALL LINCOL(LABCL0(KOLLIN))
C
        KOLLIN= KOLLIN+1
        IF (KOLLIN.GT.LENLAB) KOLLIN= 1
C
        XCEN= XSTART+XINT*0.5
        CALL PCSCEN(XCEN,YCEN,LABELS(NBOX))
        CALL LINCOL(KOLLSV)
        XSTART= XEND
        XEND= XEND+XINT
  100 CONTINUE
C
      KOLFL0= KOLSAV
      CALL CTRMAG(MAGSAV)
      CALL POSITN(XSAVE,YSAVE)
      IPRINT= IPRSAV
      RETURN
      END
