      SUBROUTINE TYPENE(VALUE,NAFTDP)
C
C          ------------------------------------------------
C          ROUTINE NO. ( 168)   VERSION (A8.5)    11:FEB:87
C          ------------------------------------------------
C
C          THIS DRAWS OUT THE GIVEN NUMBER <VALUE> IN EXPONENT
C          FORMAT, WITH <NAFTDP> DIGITS AFTER THE DECIMAL POINT.
C
C
      DOUBLE PRECISION DVALUE,VALNRM
      REAL    RDATA(1)
      INTEGER IDATA(20)
      LOGICAL ERRON
C
      COMMON /T3ERRS/ ERRON,NUMERR
      COMMON /T3LIMS/ IMAXI,RMAXI,RMINI
C
      DATA ISPACE /32/, MINUS /45/, IPOINT /46/, IZERO /48/, IELETR /69/
      DATA RDATA /0.0/
C
C
      IF (NAFTDP.LE.0) GO TO 901
C
      IDATA(1)= ISPACE
      IF (VALUE.LT.0.0) IDATA(1)= MINUS
C
      IDATA(2)= IZERO
      IDATA(3)= IPOINT
C
C          IF THE NUMBER IS ZERO, THE FIELD IS FILLED WITH ZEROES;
C          OTHERWISE, THE VALUE IS ROUNDED TO THE REQUIRED NO. OF
C          PLACES AND THE DECIMAL EXPONENT IS CALCULATED. ACCOUNT
C          IS TAKEN OF THE CASE WHERE ROUNDING BRINGS THE NUMBER
C          UP TO THE NEXT-HIGHER DECADE, AND NEGATIVE NUMBERS
C          ROUNDED TO ZERO HAVE THE MINUS SIGN SUPPRESSED.
C
C          THE MANTISSA IS CONVERTED AND STORED IN THE CHARACTER
C          STRING, FOLLOWED BY THE LETTER 'E' AND THE EXP. SIGN.
C          THE EXPONENT IS THEN CONVERTED AND STORED (TWO DIGITS
C          ARE ALWAYS DONE; A LEADING ZERO IS ADDED IF NECESSARY).
C
      DVALUE= ABS(VALUE)
      IEXP= 0
      IF (ABS(VALUE).GT.RMINI) GO TO 1
C
      DO 100 IFILL= 1,NAFTDP
        IDATA(IFILL+3)= IZERO
  100 CONTINUE
C
      GO TO 3
C
    1 IEXP= DLOG10(DVALUE)
      IF (DVALUE.GE.1.0D0) IEXP= IEXP+1
C
      VALNRM= DVALUE*(10.0D0**(NAFTDP-IEXP))
      VALNRM= VALNRM+0.5D0
      NRMVAL= VALNRM
      IF (IDINT(DLOG10(VALNRM)).LT.NAFTDP) GO TO 2
C
      IEXP= IEXP+1
      NRMVAL= NRMVAL/10
    2 IF (NRMVAL.EQ.0) IDATA(1)= ISPACE
C
      INDEX= 4
      CALL G0CONI(NRMVAL,IDATA,INDEX)
    3 NCHARS= NAFTDP+4
      IDATA(NCHARS)= IELETR
      IDATA(NCHARS+1)= ISPACE
      IF (IEXP.LT.0) IDATA(NCHARS+1)= MINUS
C
      IDATA(NCHARS+2)= IZERO
      INDEX= NCHARS+3
      IF (IABS(IEXP).GE.10) INDEX= INDEX-1
C
      CALL G0CONI(IEXP,IDATA,INDEX)
      NCHARS= INDEX-1
      CALL G3LINK(2,11,-NCHARS,IDATA,RDATA)
      RETURN
C
  901 NUMERR= 31
      IF (ERRON) CALL G0ERMS
      RETURN
      END
