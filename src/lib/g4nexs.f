      SUBROUTINE G4NEXS(IARRAY,ISTRT,ISTOP,ISIZE,STRING,LENSTR,
     &                  LEN,IPOSN)
C
C          ------------------------------------------------
C          ROUTINE NO. ( 280)   VERSION (A8.4)    05:DEC:86
C          ------------------------------------------------
C
C          THIS FINDS THE NEXT STRING IN THE ARRAY OF
C          UNPACKED RIGHT-JUSTIFIED GHOST CHARACTERS
C          [IARRAY] OF SIZE <ISIZE> BETWEEN THE ELEMENTS
C          <ISTRT> AND <ISTOP>. THE STRING BEGINS WITH
C          ANY LETTER AND ENDS WITH THE FIRST SUCCEEDING
C          SPACE, AND IS PLACED IN CHARACTER FORM INTO
C          THE OUTPUT ARRAY [ISTRNG]. THE STRING LENGTH IS
C          <LENSTR> AND THE NEXT POSITION IN [IARRAY] IS <IPOSN>.
C
      INTEGER IARRAY(ISIZE)
      CHARACTER STRING*(*)
C
C
      LEN= 0
      IPOSN= 0
      IF (ISTRT.GT.ISIZE) RETURN
      IF (ISTOP.LE.0)     RETURN
      IF (ISTRT.GT.ISTOP) RETURN
C
      LIM1= ISTRT
      IF (ISTRT.LT.1) LIM1= 1
C
      LIM2= ISTOP
      IF (ISTOP.GT.ISIZE) LIM2= ISIZE
C
      CALL G4NEXC(IARRAY, LIM1,LIM2,ISIZE,+3,IPOS1)
      IF (IPOS1.LE.0) RETURN
C
      CALL G4NEXC(IARRAY,IPOS1,LIM2,ISIZE,+4,IPOS2)
      IF (IPOS2.GE.0) LIM2= IPOS2-1
C
      DO 100 ICOPY= 1,LENSTR
        STRING(ICOPY:ICOPY)= ' '
  100 CONTINUE
C
      LIM1= IPOS1
      LEN= 0
C
      DO 200 ICOPY= LIM1,LIM2
        KCHAR= MOD(IARRAY(ICOPY),128)
        CALL G4BACO(KCHAR,MCHAR)
        LEN= LEN+1
        STRING(LEN:LEN)= CHAR(MCHAR)
  200 CONTINUE
C
      IPOSN= IPOS2
C
      RETURN
      END
