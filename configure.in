dnl Process this file with autoconf to produce a configure script.
AC_INIT([GHOST], [1.1])
AC_CONFIG_SRCDIR(src/lib/paper.f)
AM_INIT_AUTOMAKE
AC_CANONICAL_HOST
LT_INIT

dnl set default prefixes
test "$prefix" = NONE && prefix=/opt/culham
test "$exec_prefix" = NONE && exec_prefix=$prefix
AC_DEFINE_UNQUOTED(GHOST_PREFIX,"${prefix}")
AC_DEFINE_UNQUOTED(GHOST_EXEC_PREFIX,"${exec_prefix}")
dnl AC_DEFINE_UNQUOTED(GHOST_LIBDIR,["${libdir}"])
dnl AC_DEFINE_UNQUOTED(GHOST_LIBEXECDIR,["${libexecdir}"])

dnl set default compilers and mandatory options
case "$host" in 
  sparc-sun-solaris*)
    : ${CC=cc}
    : ${CXX=CC}
    : ${FC=f77}
    AC_DEFINE(USEUNDERSCORE)
    AC_DEFINE(GHOST_RECL_UNIT,4)
    ;;
  i686-pc-linux-*)
    AC_DEFINE(USEUNDERSCORE)
    AC_DEFINE(LITTLE_ENDIAN)
    AC_DEFINE(GHOST_RECL_UNIT,4)
    ;;
  i386-pc-solaris*)
    AC_DEFINE(USEUNDERSCORE)
    AC_DEFINE(LITTLE_ENDIAN)
    AC_DEFINE(GHOST_RECL_UNIT,4)
    ;;
  mips-sgi-irix*)
    : ${CC=cc}
    : ${FC=f77}
    AC_DEFINE(USEUNDERSCORE) 
    AC_DEFINE(GHOST_RECL_UNIT,1)
    ;;	
  *-hp-hpux*)
    : ${CC=c89}
    : ${FC=fort77}
    AC_DEFINE(GHOST_RECL_UNIT,4)
    : ${FFLAGS="+U77 -O"}
    ;;
  *)
    AC_DEFINE(USEUNDERSCORE)
    AC_DEFINE(LITTLE_ENDIAN)
    AC_DEFINE(GHOST_RECL_UNIT,4)
   ;;
esac

dnl set default compiler options 
: ${CFLAGS="-O"}
: ${CXXFLAGS="-O"}
: ${FFLAGS="-O"}

dnl Checks for programs.
AC_PROG_CC
AC_PROG_FC
AC_PROG_F77
AM_DISABLE_STATIC
AM_PROG_LIBTOOL

dnl Checks for header files.
AC_PATH_XTRA
AC_HEADER_STDC

dnl Get flags for linking C programs against Fortran
AC_FC_LIBRARY_LDFLAGS
dnl Get flags for increasing the line length
AC_FC_LINE_LENGTH([132])
dnl The above two macros only work for FC and not F77 but automake
dnl *really* wants to compile everything with F77, so switch the flags
F77=${FC}
FFLAGS=${FCFLAGS}

AC_OUTPUT([Makefile src/Makefile src/lib/Makefile src/libexec/Makefile 
           src/bin/Makefile src/hershey/Makefile])

